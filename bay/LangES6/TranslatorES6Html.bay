/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangES6;

use Runtime.BaseObject;
use BayLang.LangBay.ParserBayHtml;
use BayLang.LangES6.TranslatorES6;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAssign;
use BayLang.OpCodes.OpAssignValue;
use BayLang.OpCodes.OpComment;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpFor;
use BayLang.OpCodes.OpIf;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlContent;
use BayLang.OpCodes.OpHtmlCSS;
use BayLang.OpCodes.OpHtmlCSSAttribute;
use BayLang.OpCodes.OpHtmlItems;
use BayLang.OpCodes.OpHtmlSlot;
use BayLang.OpCodes.OpHtmlStyle;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpPreprocessorIfCode;
use BayLang.OpCodes.OpPreprocessorIfDef;
use BayLang.OpCodes.OpPreprocessorSwitch;
use BayLang.OpCodes.OpTypeIdentifier;
use BayLang.OpCodes.OpUse;


class TranslatorES6Html extends BaseObject
{
	TranslatorES6 translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorES6 translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpHtmlContent
	 */
	void OpHtmlContent(OpHtmlContent op_code, Collection<string> result)
	{
		string var_name = this.translator.html_var_names.last();
		result.push(var_name ~ ".push(" ~ this.translator.toString(op_code.value) ~ ");");
	}
	
	
	/**
	 * OpHtmlExpression
	 */
	void OpHtmlExpression(BaseOpCode op_code, Collection<string> result)
	{
		string var_name = this.translator.html_var_names.last();
		result.push(var_name ~ ".push(");
		this.translator.expression.translate(op_code, result);
		result.push(");");
	}
	
	
	/**
	 * OpHtmlSlot
	 */
	void OpHtmlSlot(BaseOpCode op_code, Collection<string> result)
	{
		string var_name = this.translator.html_var_names.last();
		result.push("/* Slot " ~ op_code.name ~ " */");
		result.push(this.translator.newLine());
		result.push(var_name ~ ".slot(");
		result.push(this.translator.toString(op_code.name) ~ ", ");
		this.translator.expression.translate(op_code, result);
		result.push(");");
	}
	
	
	/**
	 * OpHtmlAttrs
	 */
	Collection OpHtmlAttrs(OpHtmlTag op_code)
	{
		Collection<OpHtmlAttribute> attrs = op_code.attrs;
		
		Vector spread = [];
		Map result = {};
		for (int i=0; i<attrs.count(); i++)
		{
			OpHtmlAttribute item = attrs.get(i);
			if (item.is_spread)
			{
				Vector item_result = [];
				this.translator.expression.translate(item.expression, item_result);
				spread.push(rs::join("", item_result));
				continue;
			}
			
			string key = item.key;
			if (key == "@key") key = "key";
			else if (rs::substr(key, 0, 7) == "@event:")
			{
				key = "on" ~ rs::upper(rs::charAt(key, 7)) ~ rs::substr(key, 8);
			}
			
			Vector item_value = [];
			bool is_function = item.expression instanceof OpDeclareFunction;
			if (is_function)
			{
				int hash_value = this.translator.componentHashInc();
				item_value.push("this.hash(" ~ hash_value ~ ") ? this.hash(" ~ hash_value ~
					") : this.hash(" ~ hash_value ~ ", ");
			}
			this.translator.expression.translate(item.expression, item_value);
			
			var value = rs::join("", item_value) ~ (is_function ? ")" : "");
			if (result.has(key))
			{
				var arr = result.get(key);
				if (key == "class") arr.push(value);
				result.set(key, arr);
			}
			else
			{
				if (key == "class") value = [value];
				result.set(key, value);
			}
		}
		
		/* Add class name */
		Vector<string> class_name = result.get("class");
		if (class_name)
		{
			class_name.push("componentHash");
		}
		
		/* Get attrs */
		Vector new_attrs = result.transition(string (string value, string key){
			if (key == "class")
			{
				return this.translator.toString(key) ~
					": rs.className([" ~ rs::join(", ", value) ~ "])";
			}
			return this.translator.toString(key) ~ ": " ~ value;
		});
		return [new_attrs, spread];
	}
	
	
	/**
	 * Returns tag name
	 */
	string getTagName(var tag_name)
	{
		if (tag_name instanceof BaseOpCode)
		{
			Vector item_result = [];
			this.translator.expression.translate(tag_name, item_result);
			string value = rs::join("", item_result);
			return [value, value];
		}
		if (ParserBayHtml::isComponent(tag_name))
		{
			string module_name = this.translator.getUseModule(tag_name);
			return [module_name, this.translator.toString(module_name)];
		}
		return [tag_name, this.translator.toString(tag_name)];
	}
	
	
	/**
	 * OpHtmlTag
	 */
	void OpHtmlTag(OpHtmlTag op_code, Collection<string> result)
	{
		string attrs_str = "";
		string var_name = this.translator.html_var_names.last();
		string current_var_name = "";
		
		Collection res = this.OpHtmlAttrs(op_code);
		Collection attrs = res.get(0);
		Collection spread = res.get(1);
		if (attrs.count() > 0 or spread.count() > 0)
		{
			attrs_str = ", new Runtime.Map({" ~ rs::join(", ", attrs) ~ "})";
			for (int i=0; i<spread.count(); i++)
			{
				attrs_str = attrs_str ~ ".concat(" ~ spread.get(i) ~ ")";
			}
		}
		
		string tag_name = this.getTagName(op_code.tag_name);
		result.push("/* Element " ~ tag_name.get(0) ~ " */");
		result.push(this.translator.newLine());
		if (op_code.content and op_code.content.count() > 0)
		{
			current_var_name = this.translator.varInc();
			result.push("let " ~ current_var_name ~ " = ");
		}
		result.push(var_name ~ ".element(" ~ tag_name.get(1) ~ attrs_str ~ ");");
		
		if (op_code.content and op_code.content.count() > 0)
		{
			this.translator.html_var_names.push(current_var_name);
			bool is_slot = op_code.content.items
				.filter(bool (BaseOpCode item) => item instanceof OpHtmlSlot)
				.count() == op_code.content.count()
			;
			if (ParserBayHtml::isComponent(op_code.tag_name) and not is_slot)
			{
				OpDeclareFunction op_code_item = new OpDeclareFunction
				{
					"is_html": true,
					"content": op_code.content,
				};
				result.push(this.translator.newLine());
				result.push(this.translator.newLine());
				result.push("/* Content */");
				result.push(this.translator.newLine());
				result.push(current_var_name ~ ".slot(\"default\", ");
				this.OpDeclareFunction(op_code_item, result);
				result.push(");");
			}
			else
			{
				this.OpHtmlItems(op_code.content, result, true);
			}
			this.translator.html_var_names.pop();
		}
	}
	
	
	/**
	 * Translate item
	 */
	void translateItem(BaseOpCode op_code, Collection<string> result, bool new_line = false)
	{
		if (op_code instanceof OpHtmlContent)
		{
			this.OpHtmlContent(op_code, result);
		}
		else if (op_code instanceof OpHtmlSlot)
		{
			if (new_line) result.push(this.translator.newLine());
			this.OpHtmlSlot(op_code, result);
		}
		else if (op_code instanceof OpHtmlTag)
		{
			if (new_line) result.push(this.translator.newLine());
			this.OpHtmlTag(op_code, result);
		}
		else if (op_code instanceof OpAssign)
		{
			if (new_line) result.push(this.translator.newLine());
			this.translator.operator.OpAssign(op_code, result);
			result.push(";");
		}
		else if (op_code instanceof OpFor)
		{
			if (new_line) result.push(this.translator.newLine());
			this.translator.operator.OpFor(op_code, result);
		}
		else if (op_code instanceof OpIf)
		{
			if (new_line) result.push(this.translator.newLine());
			this.translator.operator.OpIf(op_code, result);
		}
		else if (op_code instanceof OpComment)
		{
			this.translator.operator.OpComment(op_code, result);
		}
		else
		{
			this.OpHtmlExpression(op_code, result);
		}
	}
	
	
	/**
	 * OpHtmlItems
	 */
	void OpHtmlItems(OpHtmlItems op_code, Collection<string> result, bool new_line = false)
	{
		if (not op_code) return;
		BaseOpCode prev_op_code = null;
		for (int i=0; i<op_code.count(); i++)
		{
			BaseOpCode op_code_item = op_code.get(i);
			result.push(this.translator.newLine());
			bool next_line = true;
			if (prev_op_code == null) next_line = new_line;
			else if (prev_op_code instanceof OpAssign and (
				op_code_item instanceof OpAssign or
				op_code_item instanceof OpIf or op_code_item instanceof OpFor
			) or prev_op_code instanceof OpComment)
			{
				next_line = false;
			}
			this.translateItem(op_code_item, result, next_line);
			prev_op_code = op_code_item;
		}
	}
	
	
	/**
	 * OpDeclareFunction
	 */
	bool OpDeclareFunction(OpDeclareFunction op_code, Collection<string> result)
	{
		/* Setup current function */
		OpDeclareFunction old_function = this.translator.current_function;
		this.translator.current_function = op_code;
		
		/* Comments */
		if (op_code.comments)
		{
			for (int i=0; i<op_code.comments.count(); i++)
			{
				OpComment op_code_item = op_code.comments.get(i);
				this.translator.program.OpComment(op_code_item, result);
				result.push(this.translator.newLine());
			}
		}
		
		/* Function name */
		if (old_function == null)
		{
			result.push(op_code.name);
			result.push(": ");
		}
		
		/* Function flags */
		if (op_code.flags)
		{
			Collection flags = [];
			if (op_code.flags.isFlag("async")) flags.push("async");
			result.push(rs::join(" ", flags));
			if (flags.count() > 0) result.push(" ");
		}
		
		if (old_function == null) result.push("function");
		result.push("(");
		this.translator.program.OpDeclareFunctionArgs(op_code, result);
		result.push(")");
		
		/* Add arrow */
		if (this.translator.is_operator_block)
		{
			result.push(" =>");
		}
		
		result.push(this.translator.newLine());
		result.push("{");
		this.translator.levelInc();
		
		/* Add rs */
		result.push(this.translator.newLine());
		result.push("const rs = use(\"Runtime.rs\");");
		result.push(this.translator.newLine());
		result.push("const componentHash = rs.getComponentHash(this.getClassName());");
		
		/* Create Virtual Dom */
		result.push(this.translator.newLine());
		result.push("let __v = new Runtime.VirtualDom(this);");
		result.push(this.translator.newLine());
		
		/* Save old var names */
		int old_var_inc = this.translator.var_inc;
		this.translator.var_inc = 0;
		Collection old_var_names = this.translator.html_var_names.slice();
		this.translator.html_var_names = [];
		this.translator.html_var_names.push("__v");
		
		/* Save modules */
		Dict save_use_modules = this.translator.setUseModules();
		
		/* Function content */
		Collection item_result = [];
		bool save_operator_block = this.translator.is_operator_block;
		this.translator.is_operator_block = true;
		
		/* Translate HTML items */
		if (op_code.content.items.count() == 1 and op_code.content.get(0) instanceof OpHtmlContent)
		{
			this.OpHtmlContent(op_code.content.get(0), result);
		}
		else if (op_code.content.items.count() > 0)
		{
			this.OpHtmlItems(op_code.content, item_result);
			item_result.push(this.translator.newLine());
		}
		
		this.translator.is_operator_block = save_operator_block;
		
		/* Add modules */
		this.translator.addUseModules(result);
		result.appendItems(item_result);
		
		/* Restore */
		this.translator.var_inc = old_var_inc;
		this.translator.html_var_names = old_var_names;
		this.translator.setUseModules(save_use_modules);
		
		/* Return Virtual Dom */
		if (op_code.content.items.count() > 0)
		{
			result.push(this.translator.newLine());
		}
		result.push("return __v;");
		
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push("}");
		
		/* Restore old function */
		this.translator.current_function = old_function;
		
		return true;
	}
	
	
	/**
	 * Translate component body item
	 */
	void translateComponentBodyItem(BaseOpCode op_code, Collection<string> result)
	{
		string kind = this.translator.html_kind;
		if (op_code instanceof OpDeclareFunction)
		{
			/* Check if static */
			bool is_static = op_code.flags != null and
				(op_code.flags.isFlag("static") or op_code.flags.isFlag("pure"));
			
			Vector static_methods = [
				"beforeMount", "mounted",
				"beforeUpdate", "updated",
				"beforeUnmount", "unmounted",
			];
			if (static_methods.indexOf(op_code.name) >= 0) is_static = true;
			
			if ((kind == "methods" or kind == "computed") and is_static or
				kind == "static" and not is_static
			)
			{
				return false;
			}
			if (kind == "methods" and
				op_code.flags and op_code.flags.isFlag("computed"))
			{
				return false;
			}
			if (kind == "computed")
			{
				if (not op_code.flags) return false;
				else if (not op_code.flags.isFlag("computed")) return false;
			}
			
			/* Check if html function */
			if (not op_code.is_html)
			{
				result.push(this.translator.newLine());
				this.translator.class_function = op_code;
				this.translator.program.OpDeclareFunction(op_code, result);
				this.translator.class_function = null;
				result.push(",");
				return true;
			}
			
			result.push(this.translator.newLine());
			this.translator.class_function = op_code;
			this.OpDeclareFunction(op_code, result, false);
			this.translator.class_function = null;
			result.push(",");
			return true;
		}
		else if (op_code instanceof OpItems)
		{
			return this.translateComponentBody(op_code, result, kind);
		}
		else if (op_code instanceof OpPreprocessorIfCode)
		{
			return this.translator.program.OpPreprocessorIfCode(op_code, result);
		}
		else if (op_code instanceof OpPreprocessorIfDef)
		{
			return this.translator.program.OpPreprocessorIfDef(
				op_code, result, OpPreprocessorIfDef::KIND_COMPONENT_BODY
			);
		}
		else if (op_code instanceof OpPreprocessorSwitch)
		{
			return this.translator.program.OpPreprocessorSwitch(
				op_code, result, OpPreprocessorIfDef::KIND_COMPONENT_BODY
			);
		}
	}
	
	
	/**
	 * Translate component body
	 */
	bool translateComponentBody(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpDeclareClass) op_code = op_code.content;
		for (int i=0; i<op_code.count(); i++)
		{
			BaseOpCode op_code_item = op_code.get(i);
			this.translateComponentBodyItem(op_code_item, result);
		}
	}
	
	
	/**
	 * Translate component style
	 */
	void translateComponentStyle(OpDeclareClass op_code, Collection<string> result)
	{
		Collection<OpHtmlStyle> items = op_code.content.items.filter(
			bool (BaseOpCode item) => item instanceof OpHtmlStyle
		);
		
		Collection<string> css_content = [];
		for (int i=0; i<items.count(); i++)
		{
			OpHtmlStyle op_code_item = items.get(i);
			this.translator.style.OpHtmlStyle(op_code_item, css_content);
		}
		
		result.push(this.translator.newLine());
		result.push("getComponentStyle: function(){ ");
		result.push("return " ~ this.translator.toString(rs::join("", css_content)) ~ "; ");
		result.push("},");
	}
	
	
	/**
	 * Translate module components
	 */
	void translateModuleComponents(OpDeclareClass op_code, Collection<string> result)
	{
		Vector components = [];
		for (int i=0; i<this.translator.current_module.items.count(); i++)
		{
			BaseOpCode op_code_item = this.translator.current_module.items.get(i);
			if (op_code_item instanceof OpUse and op_code_item.is_component)
			{
				components.push(op_code_item.name);
			}
		}
		components = components.map(string (string name) => this.translator.toString(name));
		result.push(this.translator.newLine());
		result.push("getRequiredComponents: function(){ ");
		result.push("return new Runtime.Vector(" ~ rs::join(", ", components) ~ "); ");
		result.push("},");
	}
	
	
	/**
	 * Translate component data items
	 */
	void OpDeclareComponentDataItems(OpItems op_code, Collection<string> result)
	{
		for (int i=0; i<op_code.count(); i++)
		{
			BaseOpCode op_code_item = op_code.get(i);
			if (op_code_item instanceof OpAssign and (
				this.translator.is_html_props and op_code_item.flags.isFlag("props") or
				not this.translator.is_html_props and not op_code_item.flags.isFlag("props"))
			)
			{
				for (int j=0; j<op_code_item.items.count(); j++)
				{
					OpAssignValue op_code_assign = op_code_item.items.get(j);
					OpIdentifier item = op_code_assign.value;
					result.push(this.translator.newLine());
					if (this.translator.is_html_props) result.push(item.value ~ ": {default: ");
					else result.push(item.value ~ ": ");
					this.translator.expression.translate(op_code_assign.expression, result);
					if (this.translator.is_html_props) result.push("},");
					else result.push(",");
				}
			}
			else if (op_code_item instanceof OpPreprocessorIfCode)
			{
				return this.translator.program.OpPreprocessorIfCode(op_code_item, result);
			}
			else if (op_code_item instanceof OpPreprocessorIfDef)
			{
				return this.translator.program.OpPreprocessorIfDef(
					op_code_item, result, OpPreprocessorIfDef::KIND_CLASS_INIT
				);
			}
			else if (op_code_item instanceof OpPreprocessorSwitch)
			{
				return this.translator.program.OpPreprocessorSwitch(
					op_code_item, result, OpPreprocessorIfDef::KIND_CLASS_INIT
				);
			}
		}
	}
	
	
	/**
	 * Translate component data
	 */
	void OpDeclareComponentData(OpDeclareClass op_code, Collection<string> result, bool is_props)
	{
		Collection item_result = [];
		item_result.push(this.translator.newLine());
		if (is_props) item_result.push("props: {");
		else item_result.push("data: function()");
		if (not is_props)
		{
			item_result.push(this.translator.newLine());
			item_result.push("{");
			this.translator.levelInc();
			item_result.push(this.translator.newLine());
			item_result.push("return {");
		}
		this.translator.levelInc();
		this.translator.is_html_props = is_props;
		Vector item_content = [];
		this.OpDeclareComponentDataItems(op_code.content, item_content);
		item_result.appendItems(item_content);
		this.translator.levelDec();
		if (not is_props)
		{
			item_result.push(this.translator.newLine());
			item_result.push("};");
			this.translator.levelDec();
		}
		item_result.push(this.translator.newLine());
		item_result.push("},");
		if (item_content.count() > 0)
		{
			result.appendItems(item_result);
		}
	}
	
	
	/**
	 * Translate component
	 */
	bool translateComponent(OpDeclareClass op_code, Collection<string> result)
	{
		/* Class name */
		string class_name = op_code.name.entity_name.items.last().value;
		this.translator.parent_class_name = "";
		this.translator.current_class = op_code;
		this.translator.current_class_name = this.translator.current_namespace_name ~ "." ~
			op_code.name.entity_name.getName();
		
		/* Add use */
		this.translator.uses.set(class_name, this.translator.current_class_name);
		this.translator.class_items.set(class_name, this.translator.current_class_name);
		
		/* Extends */
		if (op_code.class_extends)
		{
			this.translator.parent_class_name = this.translator.getFullName(
				op_code.class_extends.entity_name.getName()
			);
		}
		else
		{
			if (this.translator.current_class_name != "Runtime.Component")
			{
				this.translator.parent_class_name = "Runtime.Component";
			}
		}
		
		/* Define class */
		result.push(this.translator.current_class_name ~ " = {");
		this.translator.levelInc();
		result.push(this.translator.newLine());
		
		/* Component name */
		result.push("name: " ~ this.translator.toString(this.translator.current_class_name) ~ ",");
		if (this.translator.parent_class_name)
		{
			result.push(this.translator.newLine());
			result.push("extends: " ~ this.translator.parent_class_name ~ ",");
		}
		
		/* Component data */
		this.OpDeclareComponentData(op_code, result, true);
		this.OpDeclareComponentData(op_code, result, false);
		
		/* Component methods */
		result.push(this.translator.newLine());
		result.push("methods:");
		result.push(this.translator.newLine());
		result.push("{");
		this.translator.levelInc();
		
		/* Component methods */
		this.translator.html_kind = "methods";
		this.translateComponentBody(op_code, result);
		if (this.translator.html_kind == "methods")
		{
			result.push(this.translator.newLine());
			result.push("getClassName: function(){ ");
			result.push("return \"" ~ this.translator.current_class_name ~ "\"; },");
		}
		
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push("},");
		
		/* Component computed */
		Collection computed_result = [];
		computed_result.push(this.translator.newLine());
		computed_result.push("computed:");
		computed_result.push(this.translator.newLine());
		computed_result.push("{");
		this.translator.levelInc();
		
		/* Translate items */
		Collection item_result = [];
		this.translator.html_kind = "computed";
		this.translateComponentBody(op_code, item_result);
		computed_result.appendItems(item_result);
		
		this.translator.levelDec();
		computed_result.push(this.translator.newLine());
		computed_result.push("},");
		if (item_result.count() > 0) result.appendItems(computed_result);
		
		/* Static functions */
		this.translator.html_kind = "static";
		this.translateComponentBody(op_code, result);
		this.translateComponentStyle(op_code, result);
		this.translateModuleComponents(op_code, result);
		this.translator.program.translateClassInit(op_code, result, false);
		
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push("};");
		
		/* Register class */
		if (this.translator.use_module_name)
		{
			result.push(this.translator.newLine());
			result.push("use.add(" ~ this.translator.current_class_name ~ ");");
		}
		if (this.translator.use_window)
		{
			result.push(this.translator.newLine());
			result.push("window[\"" ~ this.translator.current_class_name ~ "\"] = " ~
				this.translator.current_class_name ~ ";");
		}
		
		return true;
	}
}