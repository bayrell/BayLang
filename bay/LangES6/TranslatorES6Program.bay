/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangES6;

use Runtime.lib;
use Runtime.BaseObject;
use BayLang.SaveOpCode;
use BayLang.Exceptions.DeclaredClass;
use BayLang.LangES6.TranslatorES6;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAnnotation;
use BayLang.OpCodes.OpAssign;
use BayLang.OpCodes.OpAssignValue;
use BayLang.OpCodes.OpComment;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpDeclareFunctionArg;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpModule;
use BayLang.OpCodes.OpNamespace;
use BayLang.OpCodes.OpPreprocessorIfCode;
use BayLang.OpCodes.OpPreprocessorIfDef;
use BayLang.OpCodes.OpPreprocessorSwitch;
use BayLang.OpCodes.OpTypeIdentifier;
use BayLang.OpCodes.OpUse;


class TranslatorES6Program extends BaseObject
{
	TranslatorES6 translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorES6 translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpNamespace
	 */
	void OpNamespace(OpNamespace op_code, Collection<string> result)
	{
		this.translator.current_namespace_name = op_code.name;
		Collection arr = rs::split(".", op_code.name);
		for (int i=0; i<arr.count(); i++)
		{
			string name = rs::join(".", arr.slice(0, i + 1));
			result.push("if (typeof " ~ name ~ " == 'undefined') " ~ name ~ " = {};");
			if (i < arr.count() - 1) result.push(this.translator.newLine());
		}
	}
	
	
	/**
	 * OpUse
	 */
	void OpUse(OpUse op_code, Collection<string> result)
	{
		this.translator.uses.set(op_code.alias, op_code.name);
		return false;
	}
	
	
	/**
	 * OpAnnotation
	 */
	void OpAnnotation(Vector<OpAnnotation> annotations, Collection<string> result)
	{
		result.push("return new Vector(");
		this.translator.levelInc();
		for (int j=0; j<annotations.count(); j++)
		{
			OpAnnotation annotation = annotations.get(j);
			result.push(this.translator.newLine());
			result.push("new ");
			this.translator.expression.OpTypeIdentifier(annotation.name, result);
			result.push("(");
			if (annotation.params != null)
			{
				this.translator.expression.OpDict(annotation.params, result);
			}
			result.push(")");
			if (j < annotations.count() - 1)
			{
				result.push(",");
				result.push(this.translator.newLine());
			}
		}
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push(");");
	}
	
	
	/**
	 * OpAssign
	 */
	void OpAssign(OpAssign op_code, Collection<string> result, bool is_expression = true)
	{
		bool last_result = false;
		for (int i=0; i<op_code.items.count(); i++)
		{
			OpAssignValue op_code_item = op_code.items.get(i);
			if (last_result)
			{
				result.push(this.translator.newLine());
			}
			if (op_code.isStatic()) result.push("static ");
			if (op_code_item.value instanceof OpIdentifier)
			{
				this.translator.expression.OpIdentifier(op_code_item.value, result);
			}
			else
			{
				last_result = false;
				continue;
			}
			if (op_code_item.expression and is_expression)
			{
				result.push(" = ");
				this.translator.expression.translate(op_code_item.expression, result);
			}
			result.push(";");
			last_result = true;
		}
		return true;
	}
	
	
	/**
	 * OpComment
	 */
	void OpComment(OpComment op_code, Collection<string> result)
	{
		Vector<string> lines = rs::split("\n", op_code.value);
		if (lines.count() == 1)
		{
			result.push("/*");
			result.push(op_code.value);
			result.push("*/");
			return;
		}
		
		string first_line = rs::trim(lines.get(0));
		bool is_comment_function = first_line == "*";
		if (first_line == "" or first_line == "*") lines = lines.slice(1);
		if (rs::trim(lines.get(lines.count() - 1)) == "" and lines.count() > 1)
		{
			lines = lines.slice(0, lines.count() - 1);
		}
		
		if (is_comment_function) result.push("/**");
		else result.push("/*");
		for (int i=0; i<lines.count(); i++)
		{
			string line = lines.get(i);
			int start = 0;
			int len = rs::strlen(line);
			while (start < len and (rs::charAt(line, start) == " " or
				rs::charAt(line, start) == "\t")) start++;
			if (start < len and rs::charAt(line, start) == "*") start++;
			result.push(this.translator.newLine());
			if (is_comment_function) result.push(" *");
			result.push(rs::substr(line, start));
		}
		result.push(this.translator.newLine());
		if (is_comment_function) result.push(" */");
		else result.push("*/");
	}
	
	
	/**
	 * OpPreprocessorIfCode
	 */
	bool OpPreprocessorIfCode(OpPreprocessorIfCode op_code, Collection<string> result)
	{
		if (op_code.condition instanceof OpIdentifier)
		{
			string name = op_code.condition.value;
			if (this.translator.preprocessor_flags.get(name))
			{
				result.push(op_code.content);
				return true;
			}
		}
		return false;
	}
	
	
	/**
	 * OpPreprocessorIfDef
	 */
	bool OpPreprocessorIfDef(
		OpPreprocessorIfDef op_code, Collection<string> result, string current_block = ""
	)
	{
		if (op_code.condition instanceof OpIdentifier)
		{
			string name = op_code.condition.value;
			if (this.translator.preprocessor_flags.get(name))
			{
				if (current_block == OpPreprocessorIfDef::KIND_PROGRAM)
				{
					this.translate(op_code.content, result);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_CLASS_BODY)
				{
					this.translateClassBody(op_code, result, false);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_CLASS_INIT)
				{
					this.translator.html.OpDeclareComponentDataItems(op_code.content, result);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_COMPONENT_BODY)
				{
					return this.translator.html.translateComponentBodyItem(
						op_code.content, result, false
					);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_OPERATOR)
				{
					this.translator.operator.translateItems(op_code.content, result, false);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_COLLECTION)
				{
					for (int i=0; i<op_code.content.items.count(); i++)
					{
						BaseOpCode op_code_item = op_code.content.items.get(i);
						if (i > 0) result.push(this.translator.newLine());
						this.translator.expression.translate(op_code_item, result);
						if (i < op_code.content.items.count() - 1) result.push(",");
					}
				}
				else if (current_block == OpPreprocessorIfDef::KIND_EXPRESSION)
				{
					this.translator.expression.translate(op_code.content, result);
				}
				return true;
			}
		}
		return false;
	}
	
	
	/**
	 * OpPreprocessorSwitch
	 */
	void OpPreprocessorSwitch(
		OpPreprocessorSwitch op_code, Collection<string> result, string current_block = ""
	)
	{
		bool last_result = false;
		for (int i=0; i<op_code.items.count(); i++)
		{
			BaseOpCode item = op_code.items.get(i);
			if (item instanceof OpPreprocessorIfCode)
			{
				Collection result1 = [];
				if (this.OpPreprocessorIfCode(item, result1))
				{
					result.push(this.translator.newLine());
					result.appendItems(result1);
					last_result = true;
				}
			}
			else if (item instanceof OpPreprocessorIfDef)
			{
				if (this.OpPreprocessorIfDef(item, result, current_block))
				{
					last_result = true;
				}
			}
		}
		return last_result;
	}
	
	
	/**
	 * OpDeclareFunctionArg
	 */
	void OpDeclareFunctionArg(OpDeclareFunctionArg op_code, Collection<string> result)
	{
		result.push(op_code.name);
		if (op_code.expression)
		{
			result.push(" = ");
			this.translator.expression.translate(op_code.expression, result);
		}
	}
	
	
	/**
	 * OpDeclareFunctionArgs
	 */
	void OpDeclareFunctionArgs(OpDeclareFunction op_code, Collection<string> result)
	{
		if (op_code.args and op_code.args.count() > 0)
		{
			int args_count = op_code.args.count();
			for (int i=0; i<args_count; i++)
			{
				OpDeclareFunctionArg op_code_item = op_code.args.get(i);
				result.push(op_code_item.name);
				if (i < args_count - 1) result.push(", ");
			}
		}
	}
	
	
	/**
	 * OpDeclareFunctionInitArgs
	 */
	void OpDeclareFunctionInitArgs(
		OpDeclareFunction op_code, Collection<string> result, bool is_multiline = true
	)
	{
		if (op_code.args and op_code.args.count() > 0)
		{
			int args_count = op_code.args.count();
			for (int i=0; i<args_count; i++)
			{
				OpDeclareFunctionArg op_code_item = op_code.args.get(i);
				if (op_code_item.expression != null)
				{
					if (is_multiline)
					{
						result.push(this.translator.newLine());
					}
					result.push("if (" ~ op_code_item.name ~ " == undefined) ");
					result.push(op_code_item.name ~ " = ");
					this.translator.expression.translate(op_code_item.expression, result);
					result.push(";");
				}
			}
		}
	}
	
	
	/**
	 * OpDeclareFunction
	 */
	void OpDeclareFunction(OpDeclareFunction op_code, Collection<string> result)
	{
		/*if (not (op_code.pattern instanceof OpTypeIdentifier)) return;*/
		if (op_code.is_html)
		{
			this.html.OpDeclareFunction(op_code, result);
			return;
		}
		
		bool is_component = this.translator.current_class.is_component;
		
		/* Setup current function */
		OpDeclareFunction old_function = this.translator.current_function;
		this.translator.current_function = op_code;
		
		/* Comments */
		if (op_code.comments)
		{
			for (int i=0; i<op_code.comments.count(); i++)
			{
				OpComment op_code_item = op_code.comments.get(i);
				this.OpComment(op_code_item, result);
				result.push(this.translator.newLine());
			}
		}
		
		/* Function flags */
		Collection flags = [];
		if (op_code.flags)
		{
			if (not is_component and (
				op_code.flags.isFlag("static") or op_code.flags.isFlag("pure")
			))
			{
				flags.push("static");
			}
			if (op_code.flags.isFlag("async")) flags.push("async");
		}
		
		/* Add flags if class is not component */
		if (not is_component)
		{
			result.push(rs::join(" ", flags));
			if (flags.count() > 0) result.push(" ");
		}
		
		/* Function name */
		if (old_function == null) result.push(op_code.name);
		
		/* Add flags if class is component */
		if (is_component)
		{
			if (old_function == null)
			{
				flags.push("function");
				result.push(": ");
			}
			result.push(rs::join(" ", flags));
		}
		
		/* Arguments */
		result.push("(");
		this.OpDeclareFunctionArgs(op_code, result);
		result.push(")");
		
		/* Add arrow */
		if (this.translator.is_operator_block)
		{
			result.push(" =>");
		}
		
		/* Multiline */
		bool is_multiline = this.translator.allow_multiline and (
			op_code.content.isMultiLine() or op_code.content instanceof OpItems
		);
		if (is_multiline)
		{
			result.push(this.translator.newLine());
		}
		else
		{
			if (this.translator.is_operator_block) result.push(" ");
		}
		result.push("{");
		if (is_multiline)
		{
			this.translator.levelInc();
		}
		else
		{
			result.push(" ");
		}
		
		/* Save modules */
		Dict save_use_modules = this.translator.setUseModules();
		
		/* Expression */
		Collection result1 = [];
		bool is_expression = not (op_code.content instanceof OpItems);
		if (is_expression)
		{
			this.OpDeclareFunctionInitArgs(op_code, result1, is_multiline);
			if (is_multiline) result1.push(this.translator.newLine());
			result1.push("return ");
			bool save_operator_block = this.translator.is_operator_block;
			this.translator.is_operator_block = true;
			this.translator.expression.translate(op_code.content, result1);
			this.translator.is_operator_block = save_operator_block;
			result1.push(";");
		}
		
		/* Items */
		else
		{
			this.OpDeclareFunctionInitArgs(op_code, result1);
			this.translator.operator.translateItems(op_code.content, result1, false);
		}
		
		/* Add modules */
		this.translator.addUseModules(result, is_multiline);
		result.appendItems(result1);
		
		/* Restore */
		this.translator.setUseModules(save_use_modules);
		
		if (is_multiline)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
		}
		else
		{
			result.push(" ");
		}
		result.push("}");
		
		/* Restore function */
		this.translator.current_function = old_function;
	}
	
	
	/**
	 * Translate class item
	 */
	bool translateClassItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpAssign)
		{
			if (op_code.isStatic())
			{
				return this.OpAssign(op_code, result);
			}
			return true;
		}
		else if (op_code instanceof OpComment)
		{
			this.OpComment(op_code, result);
		}
		else if (op_code instanceof OpDeclareFunction)
		{
			this.translator.class_function = op_code;
			if (op_code.is_html) this.translator.html.OpDeclareFunction(op_code, result);
			else this.OpDeclareFunction(op_code, result);
			this.translator.class_function = null;
		}
		else if (op_code instanceof OpPreprocessorIfCode)
		{
			return this.OpPreprocessorIfCode(op_code, result);
		}
		else if (op_code instanceof OpPreprocessorIfDef)
		{
			return this.OpPreprocessorIfDef(
				op_code, result, OpPreprocessorIfDef::KIND_CLASS_BODY
			);
		}
		else if (op_code instanceof OpPreprocessorSwitch)
		{
			return this.OpPreprocessorSwitch(
				op_code, result, OpPreprocessorIfDef::KIND_CLASS_BODY
			);
		}
		else
		{
			return false;
		}
		
		return true;
	}
	
	
	/**
	 * Translate class body
	 */
	void translateClassBody(
		OpDeclareClass op_code,
		Collection<string> result, bool match_brackets = true
	)
	{
		/* Begin bracket */
		if (match_brackets)
		{
			result.push("{");
			this.translator.levelInc();
		}
		
		/* Class body items */
		BaseOpCode prev_op_code = null;
		int result_count = result.count();
		bool next_new_line = true, prev_next_new_line = true, is_first = true;
		for (int i=0; i<op_code.content.count(); i++)
		{
			Collection item_result = [];
			BaseOpCode op_code_item = op_code.content.get(i);
			if (op_code_item instanceof OpAssign and not op_code_item.isStatic() or
				op_code_item instanceof OpAnnotation or op_code_item instanceof OpComment)
			{
				/*prev_op_code = null;*/
				continue;
			}
			if (next_new_line)
			{
				int lines = not is_first ? 3 : 1;
				if (prev_op_code instanceof OpAssign and op_code_item instanceof OpAssign)
				{
					lines = 1;
					prev_op_code = null;
				}
				for (int j=0; j<lines; j++) item_result.push(this.translator.newLine());
			}
			prev_op_code = op_code_item;
			prev_next_new_line = next_new_line;
			next_new_line = this.translateClassItem(op_code_item, item_result);
			if (rs::trim(rs::join("", item_result)) != "")
			{
				is_first = false;
				result.appendItems(item_result);
			}
			else next_new_line = prev_next_new_line;
		}
		
		/* Class init */
		if (op_code instanceof OpDeclareClass)
		{
			this.translateClassInit(op_code, result, result_count != result.count());
		}
		
		/* End bracket */
		if (match_brackets)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
			result.push("}");
		}
	}
	
	
	/**
	 * Translate class init item
	 */
	void translateClassInitItem(BaseOpCode op_code, Vector result)
	{
		if (op_code instanceof OpAssign and not op_code.isStatic())
		{
			for (int j=0; j<op_code.items.count(); j++)
			{
				OpAssignValue assign_item = op_code.items.get(j);
				if (assign_item.expression)
				{
					result.push(this.translator.newLine());
					result.push("this." ~ assign_item.value.value ~ " = ");
					this.translator.expression.translate(assign_item.expression, result);
					result.push(";");
				}
			}
		}
	}
	
	
	/**
	 * Translate class init
	 */
	void translateClassInit(OpDeclareClass op_code, Collection<string> result, bool newline = true)
	{
		if (not op_code.is_component)
		{
			result.push(this.translator.newLine(newline and op_code.content.count() > 0 ? 3 : 1));
			result.push("/* ========= Class init functions ========= */");
			result.push(this.translator.newLine());
			result.push("_init()");
			result.push(this.translator.newLine());
			result.push("{");
			this.translator.levelInc();
			if (this.translator.parent_class_name != "")
			{
				result.push(this.translator.newLine());
				result.push("super._init();");
			}
			Dict save_use_modules = this.translator.setUseModules();
			Collection result1 = [];
			for (int i=0; i<op_code.content.items.count(); i++)
			{
				BaseOpCode op_code_item = op_code.content.items.get(i);
				this.translateClassInitItem(op_code_item, result1);
			}
			this.translator.addUseModules(result);
			this.translator.setUseModules(save_use_modules);
			result.appendItems(result1);
			this.translator.levelDec();
			result.push(this.translator.newLine());
			result.push("}");
		}
		
		if (not op_code.is_component)
		{
			result.push(this.translator.newLine());
			
			/* Get class name */
			result.push("static getClassName(){ ");
			result.push("return \"" ~ this.translator.current_class_name ~ "\"; }");
			result.push(this.translator.newLine());
			
			/* Get class annotations */
			if (op_code.annotations.count() > 0)
			{
				result.push("static getClassInfo()");
				result.push(this.translator.newLine());
				result.push("{");
				this.translator.levelInc();
				Vector result1 = [];
				Map save_use_modules = this.translator.setUseModules();
				result1.push(this.translator.newLine());
				this.OpAnnotation(op_code.annotations, result1);
				this.translator.addUseModules(result);
				result.appendItems(result1);
				this.translator.setUseModules(save_use_modules);
				this.translator.levelDec();
				result.push(this.translator.newLine());
				result.push("}");
				result.push(this.translator.newLine());
			}
			
			/* Get methods with annotations */
			Vector<OpDeclareFunction> methods = [];
			this.translator.helper.getMethodsWithAnnotations(op_code.content, methods);
			
			/* Get methods list */
			result.push("static getMethodsList()");
			if (methods.count() > 0)
			{
				result.push(this.translator.newLine());
				result.push("{");
				this.translator.levelInc();
				result.push(this.translator.newLine());
				result.push("const Vector = use(\"Runtime.Vector\");");
				result.push(this.translator.newLine());
				result.push("return new Vector(");
				for (int i=0; i<methods.count(); i++)
				{
					OpDeclareFunction op_code_item = methods.get(i);
					result.push(this.translator.toString(op_code_item.name));
					if (i < methods.count() - 1) result.push(", ");
				}
				result.push(");");
				this.translator.levelDec();
				result.push(this.translator.newLine());
				result.push("}");
			}
			else result.push("{ return null; }");
			
			result.push(this.translator.newLine());
			
			/* Get method info by name */
			result.push("static getMethodInfoByName(field_name)");
			if (methods.count() > 0)
			{
				result.push(this.translator.newLine());
				result.push("{");
				this.translator.levelInc();
				result.push(this.translator.newLine());
				result.push("const Vector = use(\"Runtime.Vector\");");
				for (int i=0; i<methods.count(); i++)
				{
					OpDeclareFunction op_code_item = methods.get(i);
					string method_name = this.translator.toString(op_code_item.name);
					result.push(this.translator.newLine());
					result.push("if (field_name == " ~ method_name ~ ") ");
					this.OpAnnotation(op_code_item.annotations, result);
				}
				result.push(this.translator.newLine());
				result.push("return null;");
				this.translator.levelDec();
				result.push(this.translator.newLine());
				result.push("}");
			}
			else result.push("{ return null; }");
			
			/* Implements */
			if (op_code.class_implements)
			{
				Collection<string> class_implements = op_code.class_implements.map(
					string (OpTypeIdentifier class_name) => this.translator.toString(
						this.translator.getFullName(
							class_name.entity_name.getName()
						)
					)
				);
				result.push(this.translator.newLine());
				
				/* Get interfaces */
				result.push("static getInterfaces(){ ");
				result.push("return [" ~ rs::join(",", class_implements) ~ "]; }");
			}
		}
	}
	
	
	/**
	 * Translate class name
	 */
	string translateClassName(string class_name) => class_name;
	
	
	/**
	 * Translate class
	 */
	bool translateClass(OpDeclareClass op_code, Collection<string> result)
	{
		if (op_code.kind == OpDeclareClass::KIND_INTERFACE) return false;
		if (op_code.is_component) return this.translator.html.translateComponent(op_code, result);
		
		/* Class name */
		string class_name = op_code.name.entity_name.items.last().value;
		this.translator.parent_class_name = "";
		this.translator.current_class = op_code;
		this.translator.current_class_name = this.translator.current_namespace_name ~ "." ~
			op_code.name.entity_name.getName();
		
		/* Add use */
		this.translator.uses.set(class_name, this.translator.current_class_name);
		this.translator.class_items.set(class_name, this.translator.current_class_name);
		
		/* Abstract class */
		if (op_code.is_abstract)
		{
			return false;
		}
		
		/* Define class */
		result.push(this.translator.current_class_name ~ " = class");
		
		/* Extends */
		if (op_code.class_extends)
		{
			result.push(" extends ");
			this.translator.parent_class_name = this.translator.getFullName(
				op_code.class_extends.entity_name.getName()
			);
			result.push(this.translateClassName(this.translator.parent_class_name));
		}
		
		result.push(this.translator.newLine());
		this.translateClassBody(op_code, result);
		result.push(";");
		
		/* Register class */
		if (this.translator.use_module_name)
		{
			result.push(this.translator.newLine());
			result.push("use.add(" ~ this.translator.current_class_name ~ ");");
		}
		if (this.translator.use_window)
		{
			result.push(this.translator.newLine());
			result.push("window[\"" ~ this.translator.current_class_name ~ "\"] = " ~
				this.translator.current_class_name ~ ";");
		}
		
		return true;
	}
	
	
	/**
	 * Translate item
	 */
	void translateItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpDeclareClass)
		{
			return this.translateClass(op_code, result);
		}
		else if (op_code instanceof OpNamespace)
		{
			this.OpNamespace(op_code, result);
		}
		else if (op_code instanceof OpUse)
		{
			return this.OpUse(op_code, result);
		}
		else if (op_code instanceof OpPreprocessorIfCode)
		{
			return this.OpPreprocessorIfCode(op_code, result);
		}
		else if (op_code instanceof OpPreprocessorIfDef)
		{
			return this.OpPreprocessorIfDef(
				op_code, result, OpPreprocessorIfDef::KIND_PROGRAM
			);
		}
		else if (op_code instanceof OpPreprocessorSwitch)
		{
			return this.OpPreprocessorSwitch(
				op_code, result, OpPreprocessorIfDef::KIND_PROGRAM
			);
		}
		else
		{
			this.translator.last_semicolon = false;
			bool res = this.translator.operator.translateItem(op_code, result);
			this.translator.operator.addSemicolon(op_code, result);
			return res;
		}
		return true;
	}
	
	
	/**
	 * Translate items
	 */
	void translateItems(Collection<BaseOpCode> items, Collection<string> result)
	{
		int op_code_use_count = 0;
		bool prev_op_code_use = false;
		bool last_result = false;
		BaseOpCode prev_op_code = null;
		for (int i=0; i<items.count(); i++)
		{
			BaseOpCode op_code_item = items.get(i);
			if (last_result)
			{
				result.push(this.translator.newLine());
			}
			last_result = this.translateItem(items.get(i), result);
			prev_op_code = op_code_item;
		}
	}
	
	
	/**
	 * Add exports
	 */
	void addModuleExports(Collection<string> result)
	{
		result.push(this.translator.newLine());
		result.push("module.exports = {");
		this.translator.levelInc();
		Collection keys = rtl::list(this.translator.class_items.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string class_name = keys.get(i);
			result.push(this.translator.newLine());
			result.push(this.translator.toString(class_name) ~ ": " ~
				this.translator.uses.get(class_name) ~ ",");
		}
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push("};");
	}
	
	
	/**
	 * Translate
	 */
	void translate(OpModule op_code, Collection<string> result)
	{
		this.translator.current_module = op_code;
		this.translateItems(op_code.items, result);
	}
}