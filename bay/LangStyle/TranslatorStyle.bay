/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangStyle;

use Runtime.BaseObject;

use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlCSS;
use BayLang.OpCodes.OpHtmlCSSAttribute;
use BayLang.OpCodes.OpHtmlStyle;
use BayLang.CoreTranslator;


class TranslatorStyle extends BaseObject
{
	CoreTranslator translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(CoreTranslator translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * Combine selector
	 */
	string combineSelector(Vector<string> path, bool is_global)
	{
		Vector media = [];
		Vector items = [];
		
		fn addHash = void (Vector items)
		{
			string last_item = items.pop();
			Collection<string> last_items = rs::split(", ", last_item);
			string css_hash = rs::getCssHash(this.translator.current_class_name);
			last_items = last_items.map(
				string (string s) use (item, css_hash)
				{
					s = rs::trim(s);
					Vector arr = rs::split(":", s);
					string prefix = arr.get(0);
					string postfix = rs::join(":", arr.slice(1));
					if (postfix != "") postfix = ":" ~ postfix;
					return prefix ~ ".h-" ~ css_hash ~ postfix;
				}
			);
			items.push(rs::join(", ", last_items));
		};
		
		bool need_add_hash = not is_global;
		for (int i=0; i<path.count(); i++)
		{
			string item = path.get(i);
			if (rs::charAt(item, 0) == "@")
			{
				media.insert(0, item);
			}
			else if (rs::charAt(item, 0) == "&")
			{
				string last_item = items.pop();
				Collection<string> last_items = rs::split(", ", last_item);
				last_items = last_items.map(
					string (string s) use (item) => rs::trim(s) ~ rs::substr(item, 1)
				);
				items.push(rs::join(", ", last_items));
			}
			else
			{
				string last_item = items.last();
				if (need_add_hash and rs::charAt(last_item, 0) == ".")
				{
					addHash(items);
					need_add_hash = false;
				}
				items.push(item);
			}
		}
		
		if (need_add_hash)
		{
			addHash(items);
		}
		
		return [rs::join(" ", media), rs::join(" ", items)];
	}
	
	
	/**
	 * Add selector content
	 */
	void addSelectorContent(
		Map<Vector<string>> result, string media, var content
	)
	{
		if (not result.has(media)) result.set(media, []);
		Vector<string> items = result.get(media);
		if (rtl::isString(content)) items.push(content);
		else if (content instanceof Vector) items.appendItems(content);
	}
	
	
	/**
	 * Translate CSS
	 */
	void OpHtmlCSS(
		OpHtmlCSS op_code, Map<Vector<string>> result, Vector<string> path, bool is_global
	)
	{
		Vector item_content = [];
		Vector new_path = path.concat([op_code.selector]);
		Vector r = this.combineSelector(new_path, is_global);
		Map<Vector<string>> item_result = {};
		string media = r.get(0);
		string selector = r.get(1);
		for (int i=0; i<op_code.items.count(); i++)
		{
			BaseOpCode op_code_item = op_code.items.get(i);
			if (op_code_item instanceof OpHtmlCSS)
			{
				this.OpHtmlCSS(op_code_item, item_result, new_path, is_global);
			}
			else if (op_code_item instanceof OpHtmlCSSAttribute)
			{
				item_content.push(op_code_item.key ~ ": " ~ op_code_item.value ~ ";");
			}
		}
		if (item_content.count() > 0)
		{
			string content = selector ~ "{" ~ rs::substr(rs::join("", item_content), 0, -1) ~ "}";
			this.addSelectorContent(result, media, content);
		}
		Vector keys = rtl::list(item_result.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			this.addSelectorContent(result, key, item_result.get(key));
		}
	}
	
	
	/**
	 * Translate HTML Style
	 */
	void OpHtmlStyle(OpHtmlStyle op_code, Vector<string> result)
	{
		for (int i=0; i<op_code.content.count(); i++)
		{
			BaseOpCode op_code_item = op_code.content.get(i);
			Map item_result = {};
			this.OpHtmlCSS(op_code_item, item_result, [], op_code.is_global);
			Vector media_keys = rtl::list(item_result.keys());
			for (int j=0; j<media_keys.count(); j++)
			{
				string media = media_keys.get(j);
				Vector items = item_result.get(media);
				string content = rs::join("", items);
				if (media != "") result.push(rs::trim(media) ~ "{" ~ content ~ "}");
				else result.push(content);
			}
		}
	}
}