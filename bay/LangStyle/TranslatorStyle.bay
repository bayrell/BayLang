/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangStyle;

use Runtime.BaseObject;

use BayLang.LangStyle.Selector;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpHtmlCSS;
use BayLang.OpCodes.OpHtmlCSSAttribute;
use BayLang.OpCodes.OpHtmlStyle;
use BayLang.CoreTranslator;


class TranslatorStyle extends BaseObject
{
	CoreTranslator translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(CoreTranslator translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * Add selector content
	 */
	void addSelectorContent(
		Map<Vector<string>> result, string media, var content
	)
	{
		if (not result.has(media)) result.set(media, []);
		Vector<string> items = result.get(media);
		if (rtl::isString(content)) items.push(content);
		else if (content instanceof Vector) items.appendItems(content);
	}
	
	
	/**
	 * Translate CSS
	 */
	void OpHtmlCSS(OpHtmlCSS op_code, Map<Vector<string>> result, Selector selector, bool is_global)
	{
		Vector item_content = [];
		Map<Vector<string>> item_result = {};
		
		/* Get hash */
		string css_hash = rs::getCssHash(this.translator.current_class_name);
		
		/* Add selector */
		if (not is_global and rs::charAt(op_code.selector, 0) != "@")
		{
			selector.add(Selector::addHash(op_code.selector, css_hash));
			is_global = true;
		}
		else
		{
			selector.add(op_code.selector);
		}
		string media = selector.getMedia();
		string selector_path = selector.getSelector();
		
		for (int i=0; i<op_code.items.count(); i++)
		{
			BaseOpCode op_code_item = op_code.items.get(i);
			if (op_code_item instanceof OpHtmlCSS)
			{
				this.OpHtmlCSS(op_code_item, item_result, selector.copy(), is_global);
			}
			else if (op_code_item instanceof OpHtmlCSSAttribute)
			{
				item_content.push(op_code_item.key ~ ": " ~ op_code_item.value ~ ";");
			}
		}
		if (item_content.count() > 0)
		{
			string content = selector_path ~
				"{" ~ rs::substr(rs::join("", item_content), 0, -1) ~ "}"
			;
			this.addSelectorContent(result, media, content);
		}
		Vector keys = rtl::list(item_result.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string key = keys.get(i);
			this.addSelectorContent(result, key, item_result.get(key));
		}
	}
	
	
	/**
	 * Translate HTML Style
	 */
	void OpHtmlStyle(OpHtmlStyle op_code, Vector<string> result)
	{
		Selector selector = new Selector();
		selector.css_hash = rs::getCssHash(this.translator.current_class_name);
		for (int i=0; i<op_code.content.count(); i++)
		{
			BaseOpCode op_code_item = op_code.content.get(i);
			Map item_result = {};
			this.OpHtmlCSS(op_code_item, item_result, selector.copy(), op_code.is_global);
			Vector media_keys = rtl::list(item_result.keys());
			for (int j=0; j<media_keys.count(); j++)
			{
				string media = media_keys.get(j);
				Vector items = item_result.get(media);
				string content = rs::join("", items);
				if (media != "") result.push(rs::trim(media) ~ "{" ~ content ~ "}");
				else result.push(content);
			}
		}
	}
}