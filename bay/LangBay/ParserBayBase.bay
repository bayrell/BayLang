/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangBay;

use Runtime.BaseObject;
use Runtime.Reference;
use BayLang.Caret;
use BayLang.TokenReader;
use BayLang.Exceptions.ParserEOF;
use BayLang.Exceptions.ParserError;
use BayLang.Exceptions.ParserExpected;
use BayLang.LangBay.ParserBay;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAttr;
use BayLang.OpCodes.OpCall;
use BayLang.OpCodes.OpComment;
use BayLang.OpCodes.OpClassOf;
use BayLang.OpCodes.OpClassRef;
use BayLang.OpCodes.OpCollection;
use BayLang.OpCodes.OpCurry;
use BayLang.OpCodes.OpCurryArg;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpDeclareFunctionArg;
use BayLang.OpCodes.OpDict;
use BayLang.OpCodes.OpDictPair;
use BayLang.OpCodes.OpEntityName;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpMethod;
use BayLang.OpCodes.OpNew;
use BayLang.OpCodes.OpNumber;
use BayLang.OpCodes.OpNegative;
use BayLang.OpCodes.OpPreprocessorIfDef;
use BayLang.OpCodes.OpString;
use BayLang.OpCodes.OpTypeConvert;
use BayLang.OpCodes.OpTypeIdentifier;


class ParserBayBase extends BaseObject
{
	ParserBay parser = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(ParserBay parser)
	{
		parent();
		this.parser = parser;
	}
	
	
	/**
	 * Returns true if name is identifier
	 */
	static bool isIdentifier(string name)
	{
		if (name == "") return false;
		if (name == "@") return true;
		if (Caret::isNumber(rs::charAt(name, 0))) return false;
		int sz = rs::strlen(name);
		for (int i=0; i<sz; i++)
		{
			string ch = rs::charAt(name, i);
			if (Caret::isChar(ch) or Caret::isNumber(ch) or ch == "_") continue;
			return false;
		}
		return true;
	}
	
	
	/**
	 * Returns true if reserved words
	 */
	static bool isReserved(string name)
	{
		if (rs::substr(name, 0, 3) == "__v") return true;
		return false;
	}
	
	
	/**
	 * Read number
	 */
	OpNumber readNumber(TokenReader reader, bool flag_negative = false)
	{
		Caret caret_start = reader.start();
		
		/* Read number */
		string value = reader.readToken();
		if (value == "")
		{
			throw caret_start.expected("Number");
		}
		if (not Caret::isNumber(value))
		{
			throw caret_start.expected("Number");
		}
		
		/* Look dot */
		if (reader.nextToken() == ".")
		{
			value ~= reader.readToken();
			value ~= reader.readToken();
		}
		
		/* Returns op_code */
		return new OpNumber
		{
			"value": flag_negative ? "-" ~ value : value,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read string
	 */
	OpString readString(TokenReader reader)
	{
		Caret caret_start = reader.caret();
		string str_char = reader.readToken();
		
		/* Read begin string char */
		if (str_char != "'" and str_char != '"') /* " */
		{
			throw caret_start.expected("String");
		}
		
		/* Read string value */
		Caret caret = reader.caret();
		string value_str = "";
		string ch = caret.nextChar();
		while (not caret.eof() and ch != str_char)
		{
			if (ch == "\\")
			{
				caret.readChar();
				if (caret.eof())
				{
					throw caret.expected("End of string");
				}
				string ch2 = caret.readChar();
				if (ch2 == "n") value_str ~= "\n";
				else if (ch2 == "r") value_str ~= "\r";
				else if (ch2 == "t") value_str ~= "\t";
				else if (ch2 == "s") value_str ~= " ";
				else if (ch2 == "\\") value_str ~= "\\";
				else if (ch2 == "'") value_str ~= "'";
				else if (ch2 == '"') value_str ~= '"';
				else value_str ~= ch ~ ch2;
			}
			else
			{
				value_str ~= caret.readChar();
			}
			if (caret.eof())
			{
				throw caret.expected("End of string");
			}
			ch = caret.nextChar();
		}
		
		/* Read end string char */
		caret.matchString(str_char);
		
		/* Restore reader */
		reader.init(caret);
		
		/* Returns op_code */
		return new OpString
		{
			"value": value_str,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read comment
	 */
	OpComment readComment(TokenReader reader)
	{
		/* Init caret */
		Caret caret = reader.caret();
		caret.skip_comments = false;
		
		/* Caret start */
		Caret caret_start = caret.copy().skipSpace();
		
		/* Read begin coment */
		caret_start.matchString("/*");
		caret.seek(caret_start);
		
		/* Read comment value */
		string value_str = "";
		string ch2 = caret.nextString(2);
		while (not caret.eof() and ch2 != "*/")
		{
			value_str ~= caret.readChar();
			if (caret.eof())
			{
				throw caret.expected("End of comment");
			}
			ch2 = caret.nextString(2);
		}
		
		/* Restore reader */
		reader.init(caret);
		
		/* Read end coment */
		reader.matchToken("*");
		reader.matchToken("/");
		
		/* Returns op_code */
		return new OpComment
		{
			"value": value_str,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Skip comment
	 */
	bool skipComment(TokenReader reader)
	{
		if (reader.nextToken() != "/") return false;
		this.readComment(reader);
		return true;
	}
	
	
	/**
	 * Read identifier
	 */
	OpIdentifier readIdentifier(TokenReader reader)
	{
		Caret caret_start = reader.start();
		
		/* Read identifier */
		string name = reader.readToken();
		if (not static::isIdentifier(name) or static::isReserved(name))
		{
			throw reader.expected("Identifier");
		}
		
		/* Returns op_code */
		return new OpIdentifier
		{
			"value": name,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read entity name
	 */
	OpEntityName readEntityName(TokenReader reader)
	{
		Caret caret_start = reader.start();
		Collection<string> items = [];
		
		/* Read name */
		items.push(this.readIdentifier(reader));
		
		/* Read names */
		while (reader.nextToken() == ".")
		{
			reader.readToken();
			items.push(this.readIdentifier(reader));
		}
		
		/* Returns op_code */
		return new OpEntityName
		{
			"items": items,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read type identifier
	 */
	OpTypeIdentifier readTypeIdentifier(TokenReader reader,
		bool find_entity = true, bool read_generic = true
	)
	{
		Caret caret_start = reader.start();
		OpEntityName entity_name = this.readEntityName(reader);
		
		/* Find entity */
		if (find_entity) this.parser.findEntity(entity_name);
		
		/* Read generics */
		Collection<OpTypeIdentifier> generics = [];
		if (reader.nextToken() == "<" and read_generic)
		{
			reader.matchToken("<");
			reader.main_caret.skipToken();
			while (not reader.eof() and reader.main_caret.nextChar() != ">")
			{
				generics.push(this.readTypeIdentifier(reader, false));
				reader.main_caret.skipToken();
				reader.init(reader.main_caret);
				if (reader.main_caret.nextChar() != ">")
				{
					reader.matchToken(",");
				}
			}
			reader.main_caret.matchString(">");
			reader.init(reader.main_caret);
		}
		
		return new OpTypeIdentifier
		{
			"entity_name": entity_name,
			"generics": generics,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read collection
	 */
	OpCollection readCollection(TokenReader reader)
	{
		Caret caret_start = reader.start();
		
		reader.matchToken("[");
		Collection items = [];
		while (not reader.eof() and reader.nextToken() != "]")
		{
			if (this.skipComment(reader))
			{
				continue;
			}
			
			BaseOpCode op_code_item = null;
			
			/* Prepocessor */
			bool is_preprocessor = false;
			string next_token = reader.nextToken();
			if (next_token == "#switch" or next_token == "#ifcode" or next_token == "#ifdef")
			{
				is_preprocessor = true;
				op_code_item = this.parser.parser_preprocessor.readPreprocessor(
					reader, OpPreprocessorIfDef::KIND_COLLECTION
				);
			}
			
			/* Expression */
			else
			{
				op_code_item = this.parser.parser_expression.readExpression(reader);
			}
			
			items.push(op_code_item);
			if ((reader.nextToken() == "," or reader.nextToken() != "]") and not is_preprocessor)
			{
				reader.matchToken(",");
			}
		}
		reader.matchToken("]");
		
		return new OpCollection
		{
			"items": items,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read collection
	 */
	OpDict readDict(TokenReader reader)
	{
		Caret caret_start = reader.start();
		
		reader.matchToken("{");
		Collection items = [];
		while (not reader.eof() and reader.nextToken() != "}")
		{
			if (reader.nextToken() == "/")
			{
				this.readComment(reader);
				continue;
			}
			
			bool is_preprocessor = false;
			BaseOpCode condition = null;
			
			/* Prepocessor */
			string next_token = reader.nextToken();
			if (next_token == "#ifdef")
			{
				is_preprocessor = true;
				reader.matchToken("#ifdef");
				bool save_find_variable = this.parser.find_variable;
				this.parser.find_variable = false;
				condition = this.parser.parser_expression.readExpression(reader);
				this.parser.find_variable = save_find_variable;
				reader.matchToken("then");
			}
			
			OpString op_code_name = this.readString(reader);
			reader.matchToken(":");
			BaseOpCode op_code_item = this.parser.parser_expression.readExpression(reader);
			
			items.push(new OpDictPair{
				"key": op_code_name,
				"expression": op_code_item,
				"condition": condition,
			});
			if (reader.nextToken() == "," or reader.nextToken() != "}")
			{
				reader.matchToken(",");
			}
			if (is_preprocessor)
			{
				reader.matchToken("#endif");
			}
		}
		reader.matchToken("}");
		
		return new OpDict
		{
			"items": items,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read new instance
	 */
	BaseOpCode readNew(TokenReader reader)
	{
		Caret caret_start = reader.start();
		
		reader.matchToken("new");
		OpTypeIdentifier pattern = this.readTypeIdentifier(reader);
		Collection<OpDeclareFunctionArg> args = [];
		
		if (reader.nextToken() == "{")
		{
			BaseOpCode item = this.readDict(reader);
			args = [item];
		}
		else if (reader.nextToken() == "(")
		{
			args = this.parser.parser_function.readFunctionArgs(reader);
		}
		
		return new OpNew
		{
			"args": args,
			"pattern": pattern,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read classof
	 */
	BaseOpCode readClassOf(TokenReader reader)
	{
		Caret caret_start = reader.start();
		
		reader.matchToken("classof");
		OpEntityName entity_name = this.readEntityName(reader);
		
		return new OpClassOf
		{
			"entity_name": entity_name,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read item
	 */
	BaseOpCode readItem(TokenReader reader)
	{
		string next_token = reader.nextToken();
		if (Caret::isNumber(next_token))
		{
			return this.readNumber(reader);
		}
		else if (next_token == "'" or next_token == '"') /* " */
		{
			return this.readString(reader);
		}
		else if (next_token == "new")
		{
			return this.readNew(reader);
		}
		else if (next_token == "classof")
		{
			return this.readClassOf(reader);
		}
		return this.readIdentifier(reader);
	}
	
	
	/**
	 * Read dynamic
	 */
	BaseOpCode readDynamic(TokenReader reader, bool read_function = true)
	{
		Caret caret_start = reader.start();
		
		/* Read await */
		bool is_await = false;
		if (reader.nextToken() == "await")
		{
			is_await = true;
			reader.matchToken("await");
		}
		
		BaseOpCode item = this.readItem(reader);
		
		if (reader.nextToken() == "." and item instanceof OpIdentifier)
		{
			this.parser.useVariable(item);
			this.parser.findVariable(item);
		}
		
		Collection operations = [".", "::", "[", "("];
		while (not reader.eof() and (operations.indexOf(reader.nextToken()) >= 0))
		{
			string next_token = reader.nextToken();
			if (next_token == "." or next_token == "::")
			{
				if (next_token == "::" and item instanceof OpIdentifier)
				{
					if (not this.parser.vars.has(item.value))
					{
						item = new OpTypeIdentifier
						{
							"entity_name": new OpEntityName
							{
								"items": [item],
								"caret_start": item.caret_start,
								"caret_end": item.caret_end,
							},
							"caret_start": item.caret_start,
							"caret_end": item.caret_end,
						};
					}
				}
				reader.matchToken(next_token);
				BaseOpCode op_code_item = this.readIdentifier(reader);
				item = new OpAttr
				{
					"kind": next_token == "." ? OpAttr::KIND_ATTR : OpAttr::KIND_STATIC,
					"prev": item,
					"next": op_code_item,
					"caret_start": caret_start,
					"caret_end": reader.caret(),
				};
			}
			else if (next_token == "(")
			{
				if (read_function)
				{
					item = this.parser.parser_function.readCallFunction(reader, item);
					item.is_await = is_await;
					item.caret_start = caret_start;
				}
				else
				{
					break;
				}
			}
			else if (next_token == "[")
			{
				BaseOpCode op_code_item = this.readCollection(reader);
				item = new OpAttr
				{
					"kind": OpAttr::KIND_DYNAMIC,
					"prev": item,
					"next": op_code_item,
					"caret_start": caret_start,
					"caret_end": reader.caret(),
				};
			}
		}
		
		return item;
	}
}