/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangBay;

use Runtime.BaseObject;
use BayLang.Caret;
use BayLang.TokenReader;
use BayLang.Exceptions.ParserError;
use BayLang.Exceptions.ParserExpected;
use BayLang.LangBay.ParserBay;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpCall;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpDeclareFunctionArg;
use BayLang.OpCodes.OpEntityName;
use BayLang.OpCodes.OpFlags;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpPipe;
use BayLang.OpCodes.OpTypeIdentifier;


class ParserBayFunction extends BaseObject
{
	ParserBay parser = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(ParserBay parser)
	{
		parent();
		this.parser = parser;
	}
	
	
	/**
	 * Read function arguments
	 */
	Collection<BaseOpCode> readFunctionArgs(TokenReader reader, bool match_brackets = true)
	{
		if (match_brackets) reader.matchToken("(");
		Collection args = [];
		while (not reader.eof() and reader.nextToken() != ")")
		{
			BaseOpCode expression = this.parser.parser_expression.readExpression(reader);
			args.push(expression);
			if (reader.nextToken() != ")")
			{
				reader.matchToken(",");
			}
		}
		if (match_brackets) reader.matchToken(")");
		return args;
	}
	
	
	/**
	 * Read call function
	 */
	BaseOpCode readCallFunction(TokenReader reader, BaseOpCode pattern = null)
	{
		Caret caret_start = reader.start();
		
		/* Read identifier */
		if (pattern == null)
		{
			pattern = this.parser.parser_base.readDynamic(reader, false);
		}
		
		/* Next token should be bracket */
		if (reader.nextToken() != "(") return null;
		
		/* Find identifier */
		if (pattern instanceof OpTypeIdentifier) pattern = pattern.entity_name.items.last();
		else if (pattern instanceof OpIdentifier)
		{
			this.parser.findVariable(pattern);
		}
		
		/* Read arguments */
		Collection args = this.readFunctionArgs(reader);
		
		return new OpCall
		{
			"args": args,
			"item": pattern,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read function args
	 */
	OpItems<OpDeclareFunctionArg> readDeclareFunctionArgs(
		TokenReader reader, bool match_brackets = true, string end_tag = ""
	)
	{
		Caret caret_start = reader.start();
		Collection<OpDeclareFunctionArg> items = [];
		
		if (match_brackets) reader.matchToken("(");
		while (not reader.eof() and reader.nextToken() != ")" and reader.nextToken() != end_tag)
		{
			Caret caret_start_item = reader.start();
			
			/* Read argument */
			OpTypeIdentifier pattern = this.parser.parser_base.readTypeIdentifier(reader);
			OpIdentifier name = this.parser.parser_base.readIdentifier(reader);
			BaseOpCode expression = null;
			
			/* Read expression */
			if (reader.nextToken() == "=")
			{
				reader.matchToken("=");
				expression = this.parser.parser_expression.readExpression(reader);
			}
			
			/* Add item */
			this.parser.addVariable(name, pattern);
			items.push(new OpDeclareFunctionArg{
				"pattern": pattern,
				"name": name.value,
				"expression": expression,
				"caret_start": caret_start,
				"caret_end": reader.caret(),
			});
			
			if (reader.nextToken() != ")" and reader.nextToken() != end_tag)
			{
				reader.matchToken(",");
			}
		}
		if (match_brackets) reader.matchToken(")");
		
		return new OpItems
		{
			"items": items,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read function variables
	 */
	Collection<OpIdentifier> readDeclareFunctionUse(TokenReader reader)
	{
		if (reader.nextToken() != "use") return [];
		Collection items = [];
		reader.matchToken("use")
		reader.matchToken("(");
		while (not reader.eof() and reader.nextToken() != ")")
		{
			BaseOpCode item = this.parser.parser_base.readIdentifier(reader);
			items.push(item);
			if (reader.nextToken() != ")") reader.matchToken(",");
		}
		reader.matchToken(")");
		return items;
	}
	
	
	/**
	 * Extend variables
	 */
	void extendVariables(Vector<OpIdentifier> vars)
	{
		Map<OpIdentifier> vars_hash = vars.transition(list (OpIdentifier item) => [
			item, item.value
		]);
		Vector<string> keys = rtl::list(this.parser.vars_uses.keys());
		for (int i=0; i<keys.count(); i++)
		{
			string var_name = keys.get(i);
			Map variable = this.parser.vars.get(var_name);
			if (not (variable instanceof Map) or
				variable.get("function_level") >= this.parser.function_level
			)
			{
				continue;
			}
			OpIdentifier item = this.parser.vars_uses.get(var_name);
			if (not vars_hash.has(var_name))
			{
				vars_hash.set(var_name, item);
				vars.push(item);
			}
		}
	}
	
	
	/**
	 * Read function
	 */
	BaseOpCode readDeclareFunction(TokenReader reader, bool read_name = true)
	{
		Caret caret_start = reader.start();
		
		/* Read async */
		bool is_async = false;
		if (reader.nextToken() == "async")
		{
			is_async = true;
			reader.matchToken("async");
		}
		OpFlags flags = new OpFlags
		{
			"items": {
				"async": is_async
			},
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
		
		/* Clear vars */
		if (this.parser.current_class != null and this.parser.function_level == 0)
		{
			this.parser.vars = {};
		}
		
		/* Add new level */
		this.parser.function_level += 1;
		
		/* Read function name */
		string name = "";
		BaseOpCode content = null;
		BaseOpCode pattern = this.parser.parser_base.readTypeIdentifier(reader);
		if (read_name) name = this.parser.parser_base.readIdentifier(reader).value;
		Collection<OpDeclareFunctionArg> args = this.readDeclareFunctionArgs(reader);
		Collection<OpIdentifier> vars = this.readDeclareFunctionUse(reader);
		
		/* Save vars */
		Map<OpIdentifier> vars_uses = this.parser.vars_uses.copy();
		this.parser.vars_uses = {};
		
		/* Read content */
		if (this.parser.current_class != null and
			this.parser.current_class.kind == OpDeclareClass::KIND_INTERFACE
		)
		{
			if (reader.nextToken() == "{")
			{
				reader.matchToken("{");
				reader.matchToken("}");
			}
			else
			{
				reader.matchToken(";");
			}
		}
		else if (reader.nextToken() == "{") content = this.parser.parser_operator.parse(reader);
		else
		{
			reader.matchToken("=>");
			content = this.parser.parser_expression.readExpression(reader);
		}
		
		/* Restore vars */
		this.extendVariables(vars);
		this.parser.vars_uses = this.parser.vars_uses.concat(vars_uses);
		
		/* Dec level */
		this.parser.function_level -= 1;
		
		return new OpDeclareFunction
		{
			"args": args,
			"flags": flags,
			"name": name,
			"vars": vars,
			"pattern": pattern,
			"content": content,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Try to read function
	 */
	BaseOpCode tryReadFunction(TokenReader reader, bool read_name = true)
	{
		Caret save_caret = reader.caret();
		bool error = false;
		try
		{
			if (reader.nextToken() == "async") reader.matchToken("async");
			BaseOpCode pattern = this.parser.parser_base.readTypeIdentifier(reader);
			if (read_name) this.parser.parser_base.readIdentifier(reader);
			
			/* Read function args */
			reader.matchToken("(");
			while (not reader.eof() and reader.nextToken() != ")")
			{
				this.parser.parser_base.readTypeIdentifier(reader, false);
				this.parser.parser_base.readIdentifier(reader);
				if (reader.nextToken() == "=")
				{
					reader.matchToken("=");
					this.parser.parser_expression.readExpression(reader);
				}
				if (reader.nextToken() != ")") reader.matchToken(",");
			}
			reader.matchToken(")");
			
			/* Read use */
			if (reader.nextToken() == "use")
			{
				reader.matchToken("use")
				reader.matchToken("(")
				while (not reader.eof() and reader.nextToken() != ")")
				{
					this.parser.parser_base.readIdentifier(reader);
					if (reader.nextToken() != ")") reader.matchToken(",");
				}
				reader.matchToken(")")
			}
			
			if (this.parser.current_class != null and
				this.parser.current_class.kind == OpDeclareClass::KIND_INTERFACE and
				reader.nextToken() == ";"
			)
			{
				reader.matchToken(";");
			}
			else if (reader.nextToken() != "=>" and reader.nextToken() != "use")
			{
				reader.matchToken("{");
			}
		}
		catch (ParserError e)
		{
			error = true;
		}
		reader.init(save_caret);
		if (error) return null;
		return this.readDeclareFunction(reader, read_name);
	}
}