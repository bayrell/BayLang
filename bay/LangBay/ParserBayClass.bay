/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangBay;

use Runtime.BaseObject;
use BayLang.Caret;
use BayLang.TokenReader;
use BayLang.LangBay.ParserBay;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpCall;
use BayLang.OpCodes.OpComment;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpTypeIdentifier;


class ParserBayClass extends BaseObject
{
	ParserBay parser = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(ParserBay parser)
	{
		parent();
		this.parser = parser;
	}
	
	
	/**
	 * Read class item
	 */
	BaseOpCode readItem(TokenReader reader)
	{
		string next_token = reader.nextToken();
		
		/* Comment */
		if (next_token == "/")
		{
			return this.parser.parser_base.readComment(reader);
		}
		
		/* Save caret */
		Caret save_caret = reader.caret();
		
		/* Try to read call function */
		OpCall op_code = this.parser.parser_function.readDeclareFunction(reader);
		if (op_code) return op_code;
		
		/* Restore reader */
		reader.init(save_caret);
		
		/* Assign operator */
		return this.parser.parser_operator.readAssign(reader);
	}
	
	
	/**
	 * Process items
	 */
	Collection processItems(Collection<BaseOpCode> items)
	{
		Collection<BaseOpCode> result = [];
		Collection<BaseOpCode> comments = [];
		for (int i=0; i<items.count(); i++)
		{
			BaseOpCode item = items.get(i);
			if (item instanceof OpComment)
			{
				comments.push(item);
			}
			else if (item instanceof OpDeclareFunction)
			{
				item.comments = [];
				int line = item.caret_start.y - 1;
				for (int j=comments.count() - 1; j>=0; j--)
				{
					OpComment op_code = comments.get(j);
					if (op_code.caret_end.y == line)
					{
						item.comments.push(op_code);
						comments.remove(j);
						line = op_code.caret_start.y - 1;
					}
				}
				item.comments.reverse();
				result.appendItems(comments);
				result.push(item);
			}
			else
			{
				result.push(item);
			}
		}
		return result;
	}
	
	
	/**
	 * Read class body
	 */
	BaseOpCode readBody(TokenReader reader)
	{
		Caret caret_start = reader.start();
		Collection<BaseOpCode> items = [];
		
		reader.matchToken("{");
		
		/* Read class */
		while (not reader.eof() and reader.nextToken() != "}")
		{
			BaseOpCode op_code = this.readItem(reader);
			if (op_code)
			{
				items.push(op_code);
			}
			else
			{
				break;
			}
			
			/* Match semicolon */
			if (reader.nextToken() == ";")
			{
				reader.matchToken(";");
			}
		}
		
		reader.matchToken("}");
		
		/* Process items */
		items = this.processItems(items);
		return new OpItems
		{
			"items": items,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
	
	
	/**
	 * Read class
	 */
	BaseOpCode readClass(TokenReader reader)
	{
		Caret caret_start = reader.start();
		reader.matchToken("class");
		OpTypeIdentifier class_extends = null;
		OpIdentifier class_name = this.parser.parser_base.readIdentifier(reader);
		if (reader.nextToken() == "extends")
		{
			reader.readToken();
			class_extends = this.parser.parser_base.readTypeIdentifier(reader);
		}
		BaseOpCode content = this.readBody(reader);
		return new OpDeclareClass
		{
			"kind": OpDeclareClass::KIND_CLASS,
			"content": content,
			"name": class_name.value,
			"class_extends": class_extends,
			"caret_start": caret_start,
			"caret_end": reader.caret(),
		};
	}
}