/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangBay;

use Runtime.lib;
use Runtime.BaseObject;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAnnotation;
use BayLang.OpCodes.OpAssign;
use BayLang.OpCodes.OpCall;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpDeclareFunctionArg;
use BayLang.OpCodes.OpFor;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlContent;
use BayLang.OpCodes.OpHtmlCSS;
use BayLang.OpCodes.OpHtmlCSSAttribute;
use BayLang.OpCodes.OpHtmlItems;
use BayLang.OpCodes.OpHtmlSlot;
use BayLang.OpCodes.OpHtmlStyle;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.OpCodes.OpHtmlValue;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpIf;
use BayLang.OpCodes.OpIfElse;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpNamespace;
use BayLang.OpCodes.OpString;
use BayLang.OpCodes.OpUse;
use BayLang.LangBay.TranslatorBay;


class TranslatorBayHtml extends BaseObject
{
	TranslatorBay translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorBay translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpAssign
	 */
	void OpAssign(OpAssign op_code, Collection<string> result)
	{
		result.push("%set ");
		this.translator.operator.OpAssign(op_code, result);
		result.push(";");
	}
	
	
	/**
	 * OpUse
	 */
	void OpUse(OpUse op_code, Collection<string> result)
	{
		Collection items = rs::split(".", op_code.name);
		string last_name = items.last();
		
		/* Get attrs */
		Collection attrs = [
			"name=\"" ~ op_code.name ~ "\"",
		];
		
		/* Add alias name */
		if (op_code.alias != "" and op_code.alias != last_name)
		{
			attrs.push("as=\"" ~ op_code.alias ~ "\"");
		}
		
		/* Add component */
		if (op_code.is_component)
		{
			attrs.push("component=\"true\"");
		}
		
		/* Add result */
		result.push("<use " ~ rs::join(" ", attrs) ~ " />");
	}
	
	
	/**
	 * Translate html content
	 */
	void OpHtmlContent(OpHtmlContent op_code, Collection<string> result)
	{
		result.push(op_code.value);
	}
	
	
	/**
	 * Translate attrs
	 */
	void OpHtmlAttrs(
		Collection<OpHtmlAttribute> op_code_attrs,
		Collection<string> result,
		bool is_multiline
	)
	{
		/* Filter attrs */
		op_code_attrs = op_code_attrs.filter
		(
			bool (OpHtmlAttribute op_code_attr)
			{
				/* Skip @key_debug attr */
				if (
					not this.translator.preprocessor_flags.get("DEBUG_COMPONENT") and
					op_code_attr.key == "@key_debug"
				)
				{
					return false;
				}
				return true;
			}
		);
		
		if (is_multiline) this.translator.levelInc();
		
		int attrs_count = op_code_attrs.count();
		for (int i=0; i<attrs_count; i++)
		{
			OpHtmlAttribute op_code_attr = op_code_attrs.get(i);
			if (is_multiline) result.push(this.translator.newLine());
			result.push(op_code_attr.key);
			result.push("=");
			
			/* Value */
			if (op_code_attr.expression instanceof OpString)
			{
				this.translator.expression.translate(op_code_attr.expression, result);
			}
			else if (op_code_attr.expression instanceof OpDeclareFunction)
			{
				result.push("\"");
				OpDeclareFunction op_code = op_code_attr.expression;
				for (int i=0; i<op_code.content.count(); i++)
				{
					BaseOpCode item = op_code.content.get(i);
					this.translator.operator.translateItem(item, result);
					if (i < op_code.content.count() - 1) result.push(";");
				}
				result.push("\"");
				
				/*result.push("{{");
				this.translator.levelInc();
				result.push(this.translator.newLine());
				this.translator.expression.translate(op_code_attr.expression, result);
				this.translator.levelDec();
				result.push(this.translator.newLine());
				result.push("}}");*/
			}
			else
			{
				result.push("{{ ");
				this.translator.expression.translate(op_code_attr.expression, result);
				result.push(" }}");
			}
			
			if (i < attrs_count - 1 and not is_multiline) result.push(" ");
		}
		
		if (is_multiline)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
		}
	}
	
	
	/**
	 * Html code multiline
	 */
	void isOpHtmlTagMultiline(OpHtmlTag op_code)
	{
		int attrs_count = 0;
		for (int i=0; i<op_code.attrs.count(); i++)
		{
			OpHtmlAttribute op_code_attr = op_code.attrs.get(i);
			if (op_code_attr.key != "@key_debug") attrs_count++;
			if (op_code_attr.caret_start and op_code_attr.caret_start.y > 0)
			{
				if (op_code_attr.isMultiLine()) return true;
				if (
					op_code.caret_start.y > 0 and
					op_code_attr.caret_start.y != op_code.caret_start.y
				)
				{
					return true;
				}
			}
			if (op_code_attr.value instanceof OpDeclareFunction) return true;
		}
		if (attrs_count > 3) return true;
		return false;
	}
	
	
	/**
	 * Translate html tag
	 */
	void OpHtmlTag(OpHtmlTag op_code, Collection<string> result)
	{
		bool is_multiline = op_code.isMultiLine();
		bool is_multiline_attrs = this.isOpHtmlTagMultiline(op_code);
		
		/* Component attrs */
		Collection args_content = [];
		this.OpHtmlAttrs(op_code.attrs, args_content, is_multiline_attrs);
		string args = rs::join("", args_content);
		if (args != "" and not is_multiline_attrs) args = " " ~ args;
		
		if (op_code.content == null)
		{
			result.push("<" ~ op_code.tag_name ~ args ~ " />");
		}
		else
		{
			/* Begin tag */
			result.push("<" ~ op_code.tag_name ~ args ~ ">");
			if (is_multiline) this.translator.levelInc();
			
			/* Items */
			this.OpHtmlItems(op_code.content, result, is_multiline);
			
			/* End tag */
			if (is_multiline)
			{
				this.translator.levelDec();
				result.push(this.translator.newLine());
			}
			result.push("</" ~ op_code.tag_name ~ ">");
		}
	}
	
	
	/**
	 * OpHtmlSlot
	 */
	void OpHtmlSlot(BaseOpCode op_code, Collection<string> result)
	{
		/* Slot attrs */
		Collection args_content = [];
		this.OpHtmlAttrs(op_code.attrs, args_content);
		
		/* Add slot args */
		if (op_code.args)
		{
			Collection<string> args = op_code.args.map
			(
				string (OpDeclareFunctionArg item)
				{
					Collection<string> res = new Vector();
					this.translator.expression.OpTypeIdentifier(item.pattern, res);
					res.push(" ");
					res.push(item.name);
					return rs::join("", res);
				}
			);
			if (args_content.count() > 0) args_content.push(" ");
			args_content.push("args=\"" ~ rs::join(",", args) ~ "\"");
		}
		
		/* Add slot vars */
		if (op_code.vars)
		{
			Collection<string> vars = op_code.vars.map
			(
				string (OpIdentifier item) => item.value
			);
			if (args_content.count() > 0) args_content.push(" ");
			args_content.push("use=\"" ~ rs::join(",", vars) ~ "\"");
		}
		
		/* Slot args */
		string args = rs::join("", args_content);
		if (args != "") args = " " ~ args;
		
		/* Begin slot */
		result.push("<slot name=\"" ~ op_code.name ~ "\"" ~ args ~ ">");
		
		/* Items */
		this.translator.levelInc();
		this.OpHtmlItems(op_code.items, result);
		this.translator.levelDec();
		
		/* End slot */
		result.push(this.translator.newLine());
		result.push("</slot>");
	}
	
	
	/**
	 * OpFor
	 */
	void OpFor(OpFor op_code, Collection<string> result)
	{
		result.push("%for (");
		this.translateItem(op_code.expr1, result);
		result.push("; ");
		this.translator.expression.translate(op_code.expr2, result);
		result.push("; ");
		this.translateItem(op_code.expr3, result);
		result.push(")");
		this.translateItems(op_code);
	}
	
	
	/**
	 * OpIf
	 */
	void OpIf(OpIf op_code, Collection<string> result)
	{
		result.push("%if (");
		this.translator.expression.translate(op_code.condition, result);
		result.push(")");
		this.translateItems(op_code.if_true, result);
		if (op_code.if_else and op_code.if_else.count() > 0)
		{
			for (int i=0; i<op_code.if_else.count(); i++)
			{
				OpIfElse op_code_item = op_code.if_else.get(i);
				result.push(this.translator.newLine());
				result.push("%else if (");
				this.translator.expression.translate(op_code_item.condition, result);
				result.push(")");
				this.translateItems(op_code_item.content, result);
			}
		}
		if (op_code.if_false)
		{
			result.push(this.translator.newLine());
			result.push("%else");
			this.translateItems(op_code.if_false, result);
		}
	}
	
	
	/**
	 * Translate html item
	 */
	void OpHtmlItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpAssign)
		{
			this.OpAssign(op_code, result);
		}
		else if (op_code instanceof OpHtmlTag)
		{
			this.OpHtmlTag(op_code, result);
		}
		else if (op_code instanceof OpHtmlContent)
		{
			this.OpHtmlContent(op_code, result);
		}
		else if (op_code instanceof OpHtmlSlot)
		{
			this.OpHtmlSlot(op_code, result);
		}
		else if (op_code instanceof OpCall and op_code.is_html)
		{
			result.push("%render ");
			this.translator.expression.translate(op_code, result);
			result.push(";");
		}
		else if (op_code instanceof OpHtmlValue)
		{
			if (op_code.kind == "raw")
			{
				result.push("@raw{{ ");
				this.translator.expression.translate(op_code.value, result);
				result.push(" }}");
			}
			else
			{
				result.push("{{ ");
				this.translator.expression.translate(op_code.value, result);
				result.push(" }}");
			}
		}
		else if (op_code instanceof OpIf)
		{
			this.OpIf(op_code, result);
		}
		else if (op_code instanceof OpFor)
		{
			this.OpFor(op_code, result);
		}
		else
		{
			result.push("{{ ");
			this.translator.expression.translate(op_code, result);
			result.push(" }}");
		}
	}
	
	
	/**
	 * Translate html items
	 */
	void OpHtmlItems(OpHtmlItems op_code, Collection<string> result, bool is_multiline = true)
	{
		int items_count = op_code.count();
		for (int i=0; i<items_count; i++)
		{
			if (is_multiline) result.push(this.translator.newLine());
			this.OpHtmlItem(op_code.get(i), result);
		}
	}
	
	
	/**
	 * Translate items
	 */
	void translateItems(OpHtmlItems op_code, Vector<string> result)
	{
		result.push(this.translator.newLine());
		result.push("{");
		this.translator.levelInc();
		this.OpHtmlItems(op_code, result);
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push("}");
	}
	
	
	/**
	 * Translate template
	 */
	void translateTemplate(OpDeclareFunction op_code, Collection<string> result)
	{
		if (not op_code.is_html) return;
		
		/* Begin template */
		if (op_code.name == "render")
		{
			result.push("<template>");
		}
		else
		{
			Collection args_content = [];
			if (op_code.args and op_code.args.count() > 0)
			{
				this.translator.program.OpDeclareFunctionArgs(op_code, args_content);
			}
			string args = rs::join("", args_content);
			if (args != "") args = " args=\"" ~ args ~ "\"";
			result.push("<template name=\"" ~ op_code.name ~ "\"" ~ args ~ ">");
		}
		
		/* Items */
		this.translator.levelInc();
		this.OpHtmlItems(op_code.content, result);
		this.translator.levelDec();
		
		/* End template */
		result.push(this.translator.newLine());
		result.push("</template>");
		result.push(this.translator.newLine());
	}
	
	
	/**
	 * Translate class item
	 */
	bool translateClassItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpDeclareFunction)
		{
			this.translateTemplate(op_code, result);
		}
	}
	
	
	/**
	 * Translate style item
	 */
	void translateStyleItem(BaseOpCode op_code, Vector<string> result)
	{
		if (op_code instanceof OpHtmlCSS)
		{
			result.push(this.translator.newLine());
			result.push(op_code.selector ~ "{");
			this.translator.levelInc();
			this.translateStyleItems(op_code.items, result);
			this.translator.levelDec();
			result.push(this.translator.newLine());
			result.push("}");
		}
		else if (op_code instanceof OpHtmlCSSAttribute)
		{
			result.push(this.translator.newLine());
			result.push(op_code.key ~ ": " ~ op_code.value ~ ";")
		}
	}
	
	
	/**
	 * Translate style items
	 */
	void translateStyleItems(OpItems op_code, Vector<string> result)
	{
		for (int i=0; i<op_code.count(); i++)
		{
			BaseOpCode item = op_code.get(i);
			this.translateStyleItem(item, result);
		}
	}
	
	
	/**
	 * Translate style
	 */
	void translateStyle(OpHtmlStyle op_code, Collection<string> result)
	{
		if (op_code.is_global)
		{
			result.push("<style global=\"true\">");
		}
		else
		{
			result.push("<style>");
		}
		if (op_code.content)
		{
			this.translateStyleItems(op_code.content, result);
			result.push(this.translator.newLine());
		}
		result.push("</style>");
		result.push(this.translator.newLine());
	}
	
	
	/**
	 * Translate class
	 */
	void translateClassBody(OpDeclareClass op_code, Collection<string> result)
	{
		if (op_code.content.count() == 0) return;
		
		/* Get styles */
		Collection<BaseOpCode> styles = op_code.content.items.filter(
			bool (BaseOpCode op_code) =>
				op_code instanceof OpHtmlStyle
		);
		
		/* Translate styles */
		for (int i=0; i<styles.count(); i++)
		{
			result.push(this.translator.newLine());
			OpHtmlStyle op_code_item = styles.get(i);
			this.translateStyle(op_code_item, result);
		}
		
		/* Get templates */
		Collection<BaseOpCode> templates = op_code.content.items.filter(
			bool (BaseOpCode op_code) =>
				op_code instanceof OpDeclareFunction and op_code.is_html
		);
		
		/* Translate template */
		for (int i=0; i<templates.count(); i++)
		{
			result.push(this.translator.newLine());
			BaseOpCode op_code_item = templates.get(i);
			this.translateClassItem(op_code_item, result);
		}
		
		/* Get scripts */
		Collection<BaseOpCode> scripts = op_code.content.items.filter(
			bool (BaseOpCode op_code) =>
				op_code instanceof OpAnnotation or
				op_code instanceof OpAssign or
				op_code instanceof OpDeclareFunction and
				not op_code.is_html and
				not op_code.name == "components"
		);
		
		/* Translate scripts */
		if (scripts.count() > 0)
		{
			result.push(this.translator.newLine());
			result.push("<script>");
			result.push(this.translator.newLine());
			result.push(this.translator.newLine());
			for (int i=0; i<scripts.count(); i++)
			{
				BaseOpCode op_code_item = scripts.get(i);
				this.translator.program.translateClassItem(op_code_item, result);
				result.push(this.translator.newLine());
			}
			result.push(this.translator.newLine());
			result.push("</script>");
			result.push(this.translator.newLine());
		}
	}
	
	
	/**
	 * Translate
	 */
	void translate(BaseOpCode op_code, Collection<string> result)
	{
		OpNamespace space = op_code.items.find(
			bool (BaseOpCode item) => item instanceof OpNamespace
		);
		OpDeclareClass component = op_code.items.find(
			bool (BaseOpCode item) => item instanceof OpDeclareClass
		);
		Collection<OpUse> uses = op_code.items.filter(
			bool (BaseOpCode item) => item instanceof OpUse
		);
		
		if (not component) return;
		
		/* Get component name */
		Collection<string> component_names = [];
		if (space) component_names.push(space.name);
		component_names.push(component.name.entity_name.getName());
		string component_name = rs::join(".", component_names);
		
		result.push("<class name=\"" ~ component_name ~ "\">");
		result.push(this.translator.newLine());
		
		/* Add uses */
		if (uses.count() > 0)
		{
			result.push(this.translator.newLine());
			for (int i=0; i<uses.count(); i++)
			{
				OpUse use_item = uses.get(i);
				this.OpUse(use_item, result);
				result.push(this.translator.newLine());
			}
		}
		
		/* Declare class */
		this.translateClassBody(component, result);
		if (component.content.count() > 0 or uses.count() > 0)
		{
			result.push(this.translator.newLine());
		}
		result.push("</class>");
	}
}