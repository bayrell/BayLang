/*!
 *  Bayrell Common Languages Transcompiler
 *
 *  (c) Copyright 2016-2018 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayrellLang.LangBay;

use BayrellRtl.Lib.rs;
use BayrellRtl.Types.Vector;
use BayrellLang.CommonParser;
use BayrellLang.OpCodes.BaseOpCode;
use BayrellLang.OpCodes.OpAdd;
use BayrellLang.OpCodes.OpSub;
use BayrellLang.OpCodes.OpMult;
use BayrellLang.OpCodes.OpDiv;
use BayrellLang.OpCodes.OpNumber;
use BayrellLang.OpCodes.OpHexNumber;
use BayrellLang.OpCodes.OpString;
use BayrellLang.OpCodes.OpIdentifier;
use BayrellLang.OpCodes.OpTypeIdentifier;
use BayrellLang.OpCodes.OpCall;
use BayrellLang.OpCodes.OpCallAwait;
use BayrellLang.OpCodes.OpDynamic;
use BayrellLang.OpCodes.OpStatic;
use BayrellLang.OpCodes.OpNew;
use BayrellLang.LangBay.ParserBayToken;
use BayrellLang.Exceptions.HexNumberExpected;
use BayrellParser.Exceptions.ParserError;



export class ParserBay extends CommonParser{
	
	
	
	/**
	 * Tokens Fabric
	 * @return BayrellParserToken
	 */
	BayrellParserToken createToken(){
		return new ParserBayToken(this.context(), this);
	}
	
	
	
	/**
	 * Read double value and
	 * @return BaseOpCode
	 */
	BaseOpCode readFixed(){
		
		BaseOpCode res = null;
		
		
		/* Try to read HEX Number */
		this.pushToken();
		try{
			res = this.matchHexNumber();
		}
		catch (Exception ex){
			if (ex instanceof HexNumberExpected){
				throw ex;
			}
			else if (ex instanceof ParserError){
				res = null;
			}
			else{
				throw ex;
			}
		}
		if (res != null){
			this.popToken();
			return new OpHexNumber(res);
		}
		else{
			this.popAssignToken();
		}
		
		
		/* Try to read number */
		this.pushToken();
		try{
			res = this.matchDouble();
		}
		catch (Exception ex){
			if (ex instanceof ParserError){
				res = null;
			}
			else{
				throw ex;
			}
		}
		if (res != null){
			this.popToken();
			return new OpNumber(res);
		}
		else{
			this.popAssignToken();
		}
		
		
		/* Try to read string */
		if (this.lookType() == ParserBayToken::TOKEN_STRING){
			return new OpString(this.readAnyNextToken().token);
		}
		
		
		return null;
	}
	
	
	
	/**
	 * Read Identifier
	 * @return OpIdentifier
	 */
	OpIdentifier readIdentifier(){
		string res = this.readNextToken().token;
		
		if (not this.isLetterChar(rs::charAt(res, 0))){
			this.parserError(this.translate("ERROR_PARSER_FIRST_CHAR_MUST_BE_LETTER"));
		}
		
		return new OpIdentifier(res);
	}
	
	
	
	/**
	 * Read call args
	 * @return BaseOpCode
	 */
	Vector<BaseOpCode> readCallArgs(){
		
		Vector<BaseOpCode> v = new Vector();
		BaseOpCode res = this.readExpression();
		v.push(res);
		
		while (this.findNextToken(",")){
			this.matchNextToken(",");
			res = this.readExpression();
			v.push(res);
		}
		
		return v;
	}
	
	
	
	/**
	 * Read call body
	 * @return BaseOpCode
	 */
	Vector<BaseOpCode> readCallBody(){
	
		Vector<BaseOpCode> v = null;
		this.matchNextToken("(");
		if (not this.findNextToken(")")){
			v = this.readCallArgs();
		}
		else{
			v = new Vector<BaseOpCode>();
		}
		this.matchNextToken(")");
		
		return v;
	}
	
	
	
	/**
	 * Read call dynamic
	 * @return BaseOpCode
	 */
	BaseOpCode readCallDynamic(bool allow_dynamic = true, bool allow_static = true, bool allow_call = true){
		
		bool can_static = true;
		
		BaseOpCode obj = this.readIdentifier();
		BaseOpCode ident = null;
		while ((this.findNextToken(".") and allow_dynamic) or 
		       (this.findNextToken("::") and allow_static) or 
			   (this.findNextToken("(") and allow_call)) {
		
			if (this.findNextToken(".") and allow_dynamic){
				this.matchNextToken(".");
				ident = this.readIdentifier();
				obj = new OpDynamic(obj, ident);
			}
			
			else if (this.findNextToken("::") and allow_static){
				if (not can_static){
					this.parserError(this.translate("ERROR_PARSER_STATIC_METHOD_IS_NOT_ALOWED_HERE"));
				}
				this.matchNextToken("::");
				ident = this.readIdentifier();
				obj = new OpStatic(obj, ident);
				can_static = false;
			}
			
			else if (this.findNextToken("(") and allow_call){
				Vector<BaseOpCode> v = this.readCallBody();
				obj = new OpCall(obj, v);
				can_static = false;
			}
			
		}
		
		return obj;
	}
	
	
	
	/**
	 * Read type identifier
	 * @return BaseOpCode
	 */
	BaseOpCode readTypeIdentifier(){
		BaseOpCode res1 = this.readCallDynamic(true, false, false);
		if (not this.findNextToken("<")){
			return res1;
		}
		
		Vector<BaseOpCode> v = new Vector();
		
		this.matchNextToken("<");
		BaseOpCode res2 = this.readCallDynamic(true, false, false);
		v.push(res2);
		
		while (this.findNextToken(",")){
			BaseOpCode res2 = this.readCallDynamic(true, false, false);
			v.push(res2);		
		}
		this.matchNextToken(">");
		
		return new OpTypeIdentifier(res1, v);
	}
	
	
	
	/**
	 * Read new instance
	 * @return BaseOpCode
	 */
	BaseOpCode readNewInstance(){
		this.matchNextToken("new");
		BaseOpCode ident = this.readTypeIdentifier();
		Vector<BaseOpCode> v = this.readCallBody();
		return new OpNew(ident, v);
	}
	
	
	
	/**
	 * Read call await
	 * @return BaseOpCode
	 */
	BaseOpCode readCallAwait(){
		this.matchNextToken("await");
		BaseOpCode ident = this.readCallDynamic(true, true, false);
		Vector<BaseOpCode> v = this.readCallBody();
		return new OpCallAwait(ident, v);
	}
	
	
	
	
	/**
	 * Read op name
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionElement(){
		
		if (this.findNextToken('(')){
			this.matchNextToken('(');
			BaseOpCode res = this.readExpression();
			this.matchNextToken(')');
			return res;
		}
		else if (this.findNextToken('new')){
			return this.readNewInstance();
		}
		else if (this.findNextToken('await')){
			return this.readCallAwait();
		}
		
		BaseOpCode res = this.readFixed();
		if (res != null)
			return res;
			
		return this.readCallDynamic();
	}
	
	
	
	/**
	 * Read arithmetic multiply and divide
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionFactor (){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionElement();
		
		while (this.findNextToken('*') or this.findNextToken('/')){
			if (this.findNextToken('*')){
				this.matchNextToken('*');
				op_code = new OpMult(op_code, this.readExpressionElement());
			}
			else if (this.findNextToken('/')){
				this.matchNextToken('/');
				op_code = new OpDiv(op_code, this.readExpressionElement());
			}
			else{
				this.nextTokenExpected('"*" or "/"');
			}
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read arithmetic expression
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionArithmetic(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionFactor();
		
		while (this.findNextToken('+') or this.findNextToken('-')){
			if (this.findNextToken('+')){
				this.matchNextToken('+');
				op_code = new OpAdd(op_code, this.readExpressionFactor());
			}
			else if (this.findNextToken('-')){
				this.matchNextToken('-');
				op_code = new OpSub(op_code, this.readExpressionFactor());
			}
			else{
				this.nextTokenExpected('"+" or "-"');
			}
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read expression
	 * @return BaseOpCode
	 */
	BaseOpCode readExpression(){
		return this.readExpressionArithmetic();
	}
	
	
	
	/**
	 * Parser function
	 */
	void runParser(){
		this._result = this.readExpression();
		/* this.matchNextToken(';'); */
	}
	
	
	
}