/*!
 *  Bayrell Common Languages Transcompiler
 *
 *  (c) Copyright 2016-2018 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayrellLang.LangBay;

use BayrellRtl.Lib.rs;
use BayrellRtl.Types.Vector;
use BayrellLang.CommonParser;
use BayrellLang.OpCodes.BaseOpCode;
use BayrellLang.OpCodes.OpAdd;
use BayrellLang.OpCodes.OpAnd;
use BayrellLang.OpCodes.OpArray;
use BayrellLang.OpCodes.OpAssign;
use BayrellLang.OpCodes.OpAssignDeclare;
use BayrellLang.OpCodes.OpBitAnd;
use BayrellLang.OpCodes.OpBitNot;
use BayrellLang.OpCodes.OpBitOr;
use BayrellLang.OpCodes.OpBitXor;
use BayrellLang.OpCodes.OpBreak;
use BayrellLang.OpCodes.OpCall;
use BayrellLang.OpCodes.OpCallAwait;
use BayrellLang.OpCodes.OpChilds;
use BayrellLang.OpCodes.OpClassDeclare;
use BayrellLang.OpCodes.OpClone;
use BayrellLang.OpCodes.OpCompare;
use BayrellLang.OpCodes.OpConcat;
use BayrellLang.OpCodes.OpContinue;
use BayrellLang.OpCodes.OpDelete;
use BayrellLang.OpCodes.OpDiv;
use BayrellLang.OpCodes.OpDynamic;
use BayrellLang.OpCodes.OpFlags;
use BayrellLang.OpCodes.OpFor;
use BayrellLang.OpCodes.OpFunctionDeclare;
use BayrellLang.OpCodes.OpHexNumber;
use BayrellLang.OpCodes.OpIdentifier;
use BayrellLang.OpCodes.OpIf;
use BayrellLang.OpCodes.OpIfElse;
use BayrellLang.OpCodes.OpInterfaceDeclare;
use BayrellLang.OpCodes.OpMod;
use BayrellLang.OpCodes.OpMult;
use BayrellLang.OpCodes.OpNamespace;
use BayrellLang.OpCodes.OpNew;
use BayrellLang.OpCodes.OpNope;
use BayrellLang.OpCodes.OpNot;
use BayrellLang.OpCodes.OpNumber;
use BayrellLang.OpCodes.OpOr;
use BayrellLang.OpCodes.OpPostDec;
use BayrellLang.OpCodes.OpPostInc;
use BayrellLang.OpCodes.OpPow;
use BayrellLang.OpCodes.OpPreDec;
use BayrellLang.OpCodes.OpPreInc;
use BayrellLang.OpCodes.OpPreprocessorCase;
use BayrellLang.OpCodes.OpPreprocessorSwitch;
use BayrellLang.OpCodes.OpReturn;
use BayrellLang.OpCodes.OpShiftLeft;
use BayrellLang.OpCodes.OpShiftRight;
use BayrellLang.OpCodes.OpStatic;
use BayrellLang.OpCodes.OpString;
use BayrellLang.OpCodes.OpSub;
use BayrellLang.OpCodes.OpTemplateIdentifier;
use BayrellLang.OpCodes.OpTernary;
use BayrellLang.OpCodes.OpThrow;
use BayrellLang.OpCodes.OpTryCatch;
use BayrellLang.OpCodes.OpTryCatchChilds;
use BayrellLang.OpCodes.OpUse;
use BayrellLang.OpCodes.OpWhile;
use BayrellLang.LangBay.ParserBayToken;
use BayrellLang.LangBay.ParserBayNameToken;
use BayrellLang.Exceptions.HexNumberExpected;
use BayrellLang.Exceptions.TwiceDeclareElseError;
use BayrellParser.Exceptions.ParserError;



export class ParserBay extends CommonParser{
	
	
	
	/**
	 * Tokens Fabric
	 * @return BayrellParserToken
	 */
	BayrellParserToken createToken(){
		return new ParserBayToken(this.context(), this);
	}
	
	
	
	/**
	 * Read double value and
	 * @return BaseOpCode
	 */
	BaseOpCode readFixed(){
		
		string res = "";
		
		
		/* Try to read HEX Number */
		this.pushToken();
		try{
			res = this.matchHexNumber();
		}
		catch (Exception ex){
			if (ex instanceof HexNumberExpected){
				throw ex;
			}
			else if (ex instanceof ParserError){
				res = null;
			}
			else{
				throw ex;
			}
		}
		if (res != null){
			this.popToken();
			return new OpHexNumber(res);
		}
		else{
			this.popAssignToken();
		}
		
		
		/* Try to read number */
		this.pushToken();
		try{
			res = this.matchDouble();
		}
		catch (Exception ex){
			if (ex instanceof ParserError){
				res = null;
			}
			else{
				throw ex;
			}
		}
		if (res != null){
			this.popToken();
			return new OpNumber(res);
		}
		else{
			this.popAssignToken();
		}
		
		
		/* Try to read string */
		if (this.lookNextTokenType() == ParserBayToken::TOKEN_STRING){
			return new OpString(this.readAnyNextToken().token);
		}
		
		
		return null;
	}
	
	
	
	/**
	 * Read name
	 */
	string readIdentifierName(){
		string res = this.lookNextToken();
		string s = rs::charAt(res, 0);
		if (not this.isLetterChar(s) and s != "_"){
			this.parserError(this.translate("ERROR_PARSER_FIRST_CHAR_MUST_BE_LETTER"));
		}
		this.readNextToken();
		return res;
	}
	
	
	
	/**
	 * Read name
	 */
	string readDynamicName(){
		/* Create new token */
		ParserBayNameToken token = new ParserBayNameToken(this.context(), this);
		token.assign( this.current_token );
		token.readNextToken();
		
		/* Get name */
		string name = token.token;
		
		/* Assign next token */
		this.current_token.assign(token);
		this.next_token.assign(token);
		this.next_token.readNextToken();
		
		/* Destroy new token */
		delete token;
		
		return name;
	}
	
	
	
	/**
	 * Read Identifier
	 * @return OpIdentifier
	 */
	OpIdentifier readIdentifier(){
		string res = this.readIdentifierName();
		return new OpIdentifier(res);
	}
	
	
	
	/**
	 * Read call args
	 * @return BaseOpCode
	 */
	Vector<BaseOpCode> readCallArgs(){
		
		Vector<BaseOpCode> v = new Vector();
		BaseOpCode op_code = this.readExpression();
		v.push(op_code);
		
		while (this.findNextToken(",")){
			this.matchNextToken(",");
			op_code = this.readExpression();
			v.push(op_code);
		}
		
		return v;
	}
	
	
	
	/**
	 * Read call body
	 * @return BaseOpCode
	 */
	Vector<BaseOpCode> readCallBody(){
	
		Vector<BaseOpCode> v = null;
		this.matchNextToken("(");
		if (not this.findNextToken(")")){
			v = this.readCallArgs();
		}
		else{
			v = new Vector<BaseOpCode>();
		}
		this.matchNextToken(")");
		
		return v;
	}
	
	
	
	/**
	 * Read new or await function
	 */
	BaseOpCode readGroupExpression(){
		if (this.findNextToken('(')){
			this.matchNextToken('(');
			BaseOpCode op_code = this.readExpression();
			this.matchNextToken(')');
			return op_code;
		}
		return this.readIdentifier();
	}
	
	
	
	/**
	 * Read new instance
	 * @return BaseOpCode
	 */
	BaseOpCode readNewInstance(){
		this.matchNextToken("new");
		BaseOpCode ident = this.readTemplateIdentifier();
		Vector<BaseOpCode> v = this.readCallBody();
		return new OpNew(ident, v);
	}
	
	
	
	/**
	 * Read call await
	 * @return BaseOpCode
	 */
	BaseOpCode readCallAwait(){
		this.matchNextToken("await");
		BaseOpCode ident = this.readCallDynamic(true, true, true, true);
		Vector<BaseOpCode> v = this.readCallBody();
		return new OpCallAwait(ident, v);
	}
	
	
	
	/**
	 * Read clone
	 * @return BaseOpCode
	 */
	BaseOpCode readClone(){
		this.matchNextToken("clone");
		BaseOpCode value = this.readExpression();
		return new OpClone(value);
	}
	
	
	
	/**
	 * Read new or await function
	 */
	BaseOpCode readNewAwait(){
		if (this.findNextToken('new')){
			return this.readNewInstance();
		}
		else if (this.findNextToken('await')){
			return this.readCallAwait();
		}
		else if (this.findNextToken('clone')){
			return this.readClone();
		}
		return this.readGroupExpression();
	}
	
	
	
	/**
	 * Read call dynamic
	 * @return BaseOpCode
	 */
	BaseOpCode readCallDynamic(bool allow_dynamic = true, bool allow_bracket = true, 
	bool allow_static = true, bool allow_call = true){
		
		string name = "";
		bool can_static = true;
		BaseOpCode obj = this.readGroupExpression();
		BaseOpCode ident = null;
		while ((this.findNextToken(".") and allow_dynamic) or 
		       (this.findNextToken("[") and allow_bracket) or 
		       (this.findNextToken("::") and allow_static) or 
			   (this.findNextToken("(") and allow_call)) {
		
			if (this.findNextToken(".") and allow_dynamic){
				this.matchNextToken(".");
				name = this.readIdentifierName();
				obj = new OpDynamic(obj, name);
			}
			
			else if (this.findNextToken("[") and allow_bracket){
				this.matchNextToken("[");
				ident = this.readExpression();
				this.matchNextToken("]");
				obj = new OpArray(obj, ident);
			}
			
			else if (this.findNextToken("::") and allow_static){
				if (not can_static){
					this.parserError(this.translate("ERROR_PARSER_STATIC_METHOD_IS_NOT_ALOWED_HERE"));
				}
				this.matchNextToken("::");
				name = this.readIdentifierName();
				obj = new OpStatic(obj, name);
				can_static = false;
			}
			
			else if (this.findNextToken("(") and allow_call){
				Vector<BaseOpCode> v = this.readCallBody();
				obj = new OpCall(obj, v);
				can_static = false;
			}
			
		}
		
		return obj;
	}
	
	
	
	/**
	 * Read type identifier
	 * @return BaseOpCode
	 */
	BaseOpCode readTemplateIdentifier(){
		BaseOpCode op_code1 = this.readCallDynamic(true, false, false, false);
		if (not this.findNextToken("<")){
			return op_code1;
		}
		
		Vector<BaseOpCode> v = new Vector();
		
		this.matchNextToken("<");
		while (true){
			BaseOpCode op_code2 = this.readCallDynamic(true, false, false, false);
			v.push(op_code2);
			if (this.findNextToken(",")){
				this.matchNextToken(",");
				continue;
			}
			break;
		}
		this.matchNextToken(">");
		
		return new OpTemplateIdentifier(op_code1, v);
	}
	
	
	
	/**
	 * Read element
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionElement(){
		
		if (this.findNextToken('new')){
			return this.readNewInstance();
		}
		else if (this.findNextToken('await')){
			return this.readCallAwait();
		}
		else if (this.findNextToken('clone')){
			return this.readClone();
		}
		
		BaseOpCode op_code = this.readFixed();
		if (op_code != null)
			return op_code;
			
		return this.readCallDynamic(true, true, true, true);
	}
	
	
	
	/**
	 * Read postfix
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionPostfix(){
		BaseOpCode op_code = this.readExpressionElement();
		if (this.findNextToken('++')){
			this.matchNextToken('++');
			return new OpPostInc(op_code);
		}
		else if (this.findNextToken('--')){
			this.matchNextToken('--');
			return new OpPostDec(op_code);
		}
		return op_code;
	}
	
	
	
	/**
	 * Read prefix
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionPrefix(){
		if (this.findNextToken('++')){
			this.matchNextToken('++');
			return new OpPreInc( this.readExpressionPostfix() );
		}
		else if (this.findNextToken('--')){
			this.matchNextToken('--');
			return new OpPreDec( this.readExpressionPostfix() );
		}
		return this.readExpressionPostfix();
	}
	
	
	
	/**
	 * Read bit NOT
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionBitNot(){
		if (this.findNextToken('!')){
			this.matchNextToken('!');
			return new OpBitNot( this.readExpressionPrefix() );
		}
		return this.readExpressionPrefix();
	}
	
	
	
	/**
	 * Read pow
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionPow(){
		BaseOpCode op_code = this.readExpressionBitNot();
		while (this.findNextToken('**')){
			this.matchNextToken('**');
			op_code = new OpPow(op_code, this.readExpressionBitNot() );
		}
		return op_code;
	}
	
	
	
	/**
	 * Read arithmetic multiply and divide
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionFactor (){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionPow();
		
		while (this.findNextToken('*') or this.findNextToken('/') or this.findNextToken('%')){
			if (this.findNextToken('*')){
				this.matchNextToken('*');
				op_code = new OpMult(op_code, this.readExpressionPow());
			}
			else if (this.findNextToken('/')){
				this.matchNextToken('/');
				op_code = new OpDiv(op_code, this.readExpressionPow());
			}
			else if (this.findNextToken('%')){
				this.matchNextToken('%');
				op_code = new OpMod(op_code, this.readExpressionPow());
			}
			else{
				this.nextTokenExpected('"*", "/" or "%"');
			}
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read arithmetic expression
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionArithmetic(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionFactor();
		
		while (this.findNextToken('+') or this.findNextToken('-')){
			if (this.findNextToken('+')){
				this.matchNextToken('+');
				op_code = new OpAdd(op_code, this.readExpressionFactor());
			}
			else if (this.findNextToken('-')){
				this.matchNextToken('-');
				op_code = new OpSub(op_code, this.readExpressionFactor());
			}
			else{
				this.nextTokenExpected('"+" or "-"');
			}
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read shift
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionShift(){
	
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionArithmetic();
		
		while (this.findNextToken('<<') or this.findNextToken('>>')){
			if (this.findNextToken('<<')){
				this.matchNextToken('<<');
				op_code = new OpShiftLeft(op_code, this.readExpressionArithmetic());
			}
			else if (this.findNextToken('>>')){
				this.matchNextToken('>>');
				op_code = new OpShiftRight(op_code, this.readExpressionArithmetic());
			}
			else{
				this.nextTokenExpected('"<<" or ">>"');
			}
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read concat string
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionConcat(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionShift();
		while (this.findNextToken('~')){
			this.matchNextToken('~');
			op_code = new OpConcat(op_code, this.readExpressionShift() );
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read compare
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionCompare1(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionConcat();
		
		while (this.findNextToken('<') or this.findNextToken('<=') or
		       this.findNextToken('>') or this.findNextToken('>=') or
		       this.findNextToken('in') or this.findNextToken('instanceof') or
		       this.findNextToken('implements')
		){
			string cond = this.readNextToken().token;
			op_code = new OpCompare(cond, op_code, this.readExpressionConcat());
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read compare
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionCompare2(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionCompare1();
		
		while (this.findNextToken('==') or this.findNextToken('===') or
		       this.findNextToken('!=') or this.findNextToken('!==')
		){
			string cond = this.readNextToken().token;
			op_code = new OpCompare(cond, op_code, this.readExpressionCompare1());
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read bit AND
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionBitAnd(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionCompare2();
		while (this.findNextToken('&')){
			this.matchNextToken('&');
			op_code = new OpBitAnd(op_code, this.readExpressionCompare2() );
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read bit XOR
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionBitXor(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionBitAnd();
		while (this.findNextToken('^')){
			this.matchNextToken('^');
			op_code = new OpBitXor(op_code, this.readExpressionBitAnd() );
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read bit OR
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionBitOr(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionBitXor();
		while (this.findNextToken('|')){
			this.matchNextToken('|');
			op_code = new OpBitOr(op_code, this.readExpressionBitXor() );
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read NOT
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionNot(){
		
		if (this.findNextToken('not')){
			this.matchNextToken('not');
			return new OpNot(this.readExpressionBitOr() );
		}
		
		return this.readExpressionBitOr();
	}
	
	
	
	/**
	 * Read AND
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionAnd(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionNot();
		while (this.findNextToken('and')){
			this.matchNextToken('and');
			op_code = new OpAnd(op_code, this.readExpressionNot() );
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read OR
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionOr(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionAnd();
		while (this.findNextToken('or')){
			this.matchNextToken('or');
			op_code = new OpOr(op_code, this.readExpressionAnd() );
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read ternary operator
	 * @return BaseOpCode
	 */
	BaseOpCode readExpressionTernary(){
		
		/* Read first opcode */
		BaseOpCode op_code = this.readExpressionOr();
		if (this.findNextToken('?')){
			this.matchNextToken('?');
			BaseOpCode if_true = this.readExpressionOr();
			
			this.matchNextToken(':');
			BaseOpCode if_false = this.readExpressionOr();
			
			return new OpTernary(op_code, if_true, if_false);
		}
		
		return op_code;
	}
	
	
	
	/**
	 * Read expression
	 * @return BaseOpCode
	 */
	BaseOpCode readExpression(){
		return this.readExpressionTernary();
	}
	
	
	
	/**
	 * Read operator assign
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorAssign(){
		
		BaseOpCode op_type = null;
		BaseOpCode op_ident = null;
		string op_ident_name = "";
		BaseOpCode op_exp = null;
		bool success = false;
		
		Vector<string> v = (new Vector())
			.push("=")
			.push("~=")
			.push("+=")
			.push("-=")
		;
		
		/* Read assign */
		success = false;
		this.pushToken();
		try{
			op_ident = this.readCallDynamic(true, true, false, false);
			if (this.findNextTokenVector(v) != -1){
				success = true;
			}
		}
		catch (Exception ex){
			if (ex instanceof ParserError){
				success = false;
			}
			else{
				delete v;
				throw ex;
			}
		}
		if (success){
			this.popToken();
			int pos = this.findNextTokenVector(v);
			string op_name = v.item(pos);
			this.matchNextToken(op_name);
			op_exp = this.readExpression();
			delete v;
			return new OpAssign(op_ident, op_exp, op_name);
		}
		this.popAssignToken();
		delete v;
		
		
		/* Read declare */
		this.pushToken();
		try{
			op_type = this.readTemplateIdentifier();
			op_ident_name = this.readIdentifierName();
			success = true;
		}
		catch (Exception ex){
			if (ex instanceof ParserError){
				success = false;
			}
			else{
				throw ex;
			}
		}
		if (success){
			this.popToken();
			if (this.findNextToken("=")){
				this.matchNextToken("=");
				op_exp = this.readExpression();
			}
			return new OpAssignDeclare(op_type, op_ident_name, op_exp);
		}
		this.popAssignToken();
		
		return null;
	}
	
	
	
	/**
	 * Read operator if
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorIf(){
		
		BaseOpCode condition = null;
		Vector<BaseOpCode> if_true = null;
		Vector<BaseOpCode> if_false = null;
		Vector<OpIfElse> if_else = new Vector();
		
		/* Read condition */
		this.matchNextToken("if");
		this.matchNextToken("(");
		condition = this.readExpression();
		this.matchNextToken(")");
		
		/* Read if true operators block */
		if (this.lookNextToken() == '{'){
			this.matchNextToken("{");
			if_true = this.readOperatorsBlock();
			this.matchNextToken("}");
		}
		else{
			if_true = new Vector<BaseOpCode>();
			if_true.push(this.readOperator());
		}
		
		/* Read else or elseif */
		while (this.findNextToken("elseif") or this.findNextToken("else")){
			
			if (this.findNextToken("else")){
				this.matchNextToken("else");
				
				/* Read elseif */
				if (this.findNextToken("if")){
					OpIfElse op_if_else = new OpIfElse();
					
					/* Read condition */
					this.matchNextToken("if");
					this.matchNextToken("(");
					op_if_else.condition = this.readExpression();
					this.matchNextToken(")");
					
					/* Read operators block */
					if (this.lookNextToken() == '{'){
						this.matchNextToken("{");
						op_if_else.if_true = this.readOperatorsBlock();
						this.matchNextToken("}");
					}
					else{
						op_if_else.if_true = new Vector<BaseOpCode>();
						op_if_else.if_true.push(this.readOperator());
					}
					if_else.push(op_if_else);
				}
				
				/* Read false operators block */
				else{
					if (this.lookNextToken() == '{'){
						this.matchNextToken("{");
						if_false = this.readOperatorsBlock();
						this.matchNextToken("}");
					}
					else{
						if_false = new Vector<BaseOpCode>();
						if_false.push(this.readOperator());
					}
					break;
				}
			}
			
			/* Read elseif */
			else if (this.findNextToken("elseif")){
				OpIfElse op_if_else = new OpIfElse();
				
				/* Read condition */
				this.matchNextToken("elseif");
				this.matchNextToken("(");
				op_if_else.condition = this.readExpression();
				this.matchNextToken(")");
				
				/* Read operators block */
				if (this.lookNextToken() == '{'){
					this.matchNextToken("{");
					op_if_else.if_true = this.readOperatorsBlock();
					this.matchNextToken("}");
					if_else.push(op_if_else);
				}
				else{
					op_if_else.if_true = new Vector<BaseOpCode>();
					op_if_else.if_true.push(this.readOperator());
				}
			}
		}
		
		return new OpIf(condition, if_true, if_false, if_else);
	}
	
	
	
	/**
	 * Read operator while
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorWhile(){
		
		BaseOpCode condition = null;
		Vector<BaseOpCode> childs = null;
		
		/* Read condition */
		this.matchNextToken("while");
		this.matchNextToken("(");
		condition = this.readExpression();
		this.matchNextToken(")");
		
		/* Read operators block */
		this.matchNextToken("{");
		childs = this.readOperatorsBlock();
		this.matchNextToken("}");
		
		return new OpWhile(condition, childs);
	}
	
	
	
	/**
	 * Read operator for
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorFor(){
		
		BaseOpCode loop_condition = null;
		BaseOpCode loop_init = null;
		BaseOpCode loop_inc = null;
		Vector<BaseOpCode> childs = null;
		
		/* Read loop header */
		this.matchNextToken("for");
		this.matchNextToken("(");
		loop_init = this.readOperatorAssign();
		this.matchNextToken(";");
		loop_condition = this.readExpression();
		this.matchNextToken(";");
		loop_inc = this.readExpression();
		this.matchNextToken(")");
		
		/* Read operators block */
		this.matchNextToken("{");
		childs = this.readOperatorsBlock();
		this.matchNextToken("}");
		
		return new OpFor(loop_condition, loop_init, loop_inc, childs);
	}
	
	
	
	/**
	 * Read operator try
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorTry(){
		
		Vector<BaseOpCode> op_try = null;
		Vector<OpTryCatchChilds> op_catch = new Vector();
		
		/* Read try block */
		this.matchNextToken("try");
		this.matchNextToken("{");
		op_try = this.readOperatorsBlock();
		this.matchNextToken("}");
		
		/* Read catch */
		while (this.findNextToken("catch")){
			OpTryCatchChilds try_catch_child = new OpTryCatchChilds();
			this.matchNextToken("catch");
			this.matchNextToken("(");
			try_catch_child.op_type = this.readTemplateIdentifier();
			try_catch_child.op_ident = this.readIdentifier();
			this.matchNextToken(")");
			this.matchNextToken("{");
			try_catch_child.childs = this.readOperatorsBlock();
			this.matchNextToken("}");
			op_catch.push(try_catch_child);
		}
		
		return new OpTryCatch(op_try, op_catch);
	}
	
	
	
	/**
	 * Read operator return
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorReturn(){
		this.matchNextToken("return");
		BaseOpCode value = this.readExpression();
		this.matchNextToken(";");
		return new OpReturn(value);
	}
	
	
	
	/**
	 * Read operator throw
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorThrow(){
		this.matchNextToken("throw");
		BaseOpCode value = this.readExpression();
		this.matchNextToken(";");
		return new OpThrow(value);
	}
	
	
	
	/**
	 * Read operator delete
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorDelete(){
		this.matchNextToken("delete");
		BaseOpCode value = this.readCallDynamic(true, true, false, false);
		this.matchNextToken(";");
		return new OpDelete(value);
	}
	
	
	
	/**
	 * Read operator 
	 * @return BaseOpCode
	 */
	BaseOpCode readOperator(){
		
		if (this.findNextToken(";")){
			this.matchNextToken(';');
			return null;
		}
		
		else if (this.findNextToken("if")){
			return this.readOperatorIf();
		}
		else if (this.findNextToken("while")){
			return this.readOperatorWhile();
		}
		else if (this.findNextToken("for")){
			return this.readOperatorFor();
		}
		else if (this.findNextToken("try")){
			return this.readOperatorTry();
		}
		else if (this.findNextToken("return")){
			return this.readOperatorReturn();
		}
		else if (this.findNextToken("throw")){
			return this.readOperatorThrow();
		}
		else if (this.findNextToken("delete")){
			return this.readOperatorDelete();
		}
		else if (this.findNextToken("break")){
			this.matchNextToken('break');
			this.matchNextToken(';');
			return new OpBreak();
		}
		else if (this.findNextToken("continue")){
			this.matchNextToken('continue');
			this.matchNextToken(';');
			return new OpContinue();
		}
		else if (this.findNextTokenPreprocessor()){
			return this.readPreprocessor();
		}
		
		BaseOpCode res = this.readOperatorAssign();
		if (res){
			this.matchNextToken(";");
			return res;
		}
		
		return this.readExpressionElement();
		/*return this.readCallDynamic(true, true, true, true);*/
	}
	
	
	
	/**
	 * Read operator block
	 * @return BaseOpCode
	 */
	Vector<BaseOpCode> readOperatorsBlock(){
		
		Vector<BaseOpCode> res = new Vector();
		
		bool match_bracket = false;
		if (this.findNextToken("{")){
			this.matchNextToken("{");
			match_bracket = true;
		}
	
		BaseOpCode op_code = null;
		while ( (not this.findNextToken("}")) and (not this.isEOF()) ){
			op_code = this.readOperator();
			if (op_code != null){
				res.push(op_code);
			}
		}
		
		if (match_bracket){
			this.matchNextToken("}");
		}
		
		return res;
	}
	
	
	
	/**
	 * Read operator namespace
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorNamespace(){
		
		this.matchNextToken("namespace");
		
		/* Create new token */
		ParserBayNameToken token = new ParserBayNameToken(this.context(), this);
		token.assign( this.current_token );
		token.readNextToken();
		
		/* Get name */
		string name = token.token;
		
		/* Assign next token */
		this.current_token.assign(token);
		this.next_token.assign(token);
		this.next_token.readNextToken();
		
		/* Destroy new token */
		delete token;
		
		this.matchNextToken(";");
		
		return new OpNamespace(name);
	}
	
	
	
	/**
	 * Read operator namespace
	 * @return BaseOpCode
	 */
	BaseOpCode readOperatorUse(){
		
		this.matchNextToken("use");
		string name = this.readDynamicName();
		this.matchNextToken(";");
		
		return new OpUse(name);
	}
	
	
	
	/**
	 * Read flags
	 * @return OpFlags
	 */
	OpFlags readFlags(){
		
		OpFlags flags = null;
		Vector<string> flags_vector = OpFlags::getFlags();
		
		if (this.findNextTokenVector(flags_vector) != -1){
			flags = new OpFlags();
			while (this.findNextTokenVector(flags_vector) != -1){
				if (not flags.assignFlag(this.lookNextToken())){
					this.parserError("Unknown flag '" ~ this.lookNextToken() ~ "'");
				}
				this.readNextToken();
			}
		}
		
		if (flags_vector != null) delete flags_vector;
		return flags;
	}
	
	
	
	/**
	 * Read operator namespace
	 * @return BaseOpCode
	 */
	OpFunctionDeclare readDeclareClassFunction(){
		OpFunctionDeclare res = new OpFunctionDeclare();
		
		this.pushToken();
		res.result_type = this.readTemplateIdentifier();
		res.name = this.readIdentifierName();
		
		/* If not function */
		if (this.lookNextToken() != "("){
			this.popAssignToken();
			return null;
		}
		
		this.matchNextToken("(");
		while (not this.findNextToken(")") and not this.isEOF()){
			
			BaseOpCode op_code = this.readOperatorAssign();
			if (op_code instanceof OpAssign){
				this.parserError("Assign are not alowed here");
			}
			else if (op_code instanceof OpAssignDeclare){
				res.args.push(op_code);
			}
			
			if (this.findNextToken(",")){
				this.matchNextToken(",");
				continue;
			}
			break;
		}
		this.matchNextToken(")");
		this.popToken();
		
		
		if (this.findNextToken("{")){
			res.childs = this.readOperatorsBlock();
		}
		else{
			this.matchNextToken(";");
		}
		
		return res;
	}
	
	
	
	/**
	 * Read operator namespace
	 * @return BaseOpCode
	 */
	OpClassDeclare readClassBody(OpClassDeclare res){
		
		OpFlags flags = null;
		BaseOpCode op_code = null;
		res.class_name = this.readIdentifierName();
		
		if (this.findNextToken("<")){
			this.matchNextToken("<");
			while (true){
				BaseOpCode op_code2 = this.readCallDynamic(true, false, false, false);
				res.class_template.push(op_code2);
				if (this.findNextToken(",")){
					this.matchNextToken(",");
					continue;
				}
				break;
			}
			this.matchNextToken(">");
		}
		
		
		if (this.findNextToken("extends")){
			this.matchNextToken("extends");
			res.class_extends = this.readIdentifier();
		}
		
		if (this.findNextToken("implements")){
			this.matchNextToken("implements");
			while ((not this.findNextToken("{")) and (not this.isEOF())){
				res.class_implements.push( this.readDynamicName() );
				if (this.findNextToken(",")){
					this.matchNextToken(",");
					continue;
				}
				break;
			}
		}
		
		this.matchNextToken("{");
		while ((not this.findNextToken("}")) and (not this.isEOF())){
			
			flags = this.readFlags();
			op_code = this.readDeclareClassFunction();
			if (op_code){
				op_code.flags = flags;
				res.class_functions.push(op_code);
				continue;
			}
			
			op_code = this.readOperatorAssign();
			if (op_code instanceof OpAssign){
				this.parserError("Assign are not alowed here");
			}
			else if (op_code instanceof OpAssignDeclare){
				op_code.flags = flags;
				res.class_variables.push(op_code);
				this.matchNextToken(";");
				continue;
			}
			
			this.parserError("Unknown operator");
		}
		
		this.matchNextToken("}");
	}
	
	
	
	/**
	 * Read operator namespace
	 * @return BaseOpCode
	 */
	OpClassDeclare readDeclareClass(OpFlags class_flags){
		OpClassDeclare res = new OpClassDeclare();
		this.matchNextToken("class");
		this.readClassBody(res);
		res.flags = class_flags;
		return res;
	}
	
	
	
	/**
	 * Read operator namespace
	 * @return BaseOpCode
	 */
	OpInterfaceDeclare readDeclareInterface(OpFlags class_flags){
		OpInterfaceDeclare res = new OpInterfaceDeclare();
		this.matchNextToken("interface");
		this.readClassBody(res);
		res.flags = class_flags;
		return res;
	}
	
	
	
	/**
	 * Prepocessor
	 */
	bool findNextTokenPreprocessor(){
		string token = this.lookNextToken();
		if (
			(this.lookNextTokenType() == ParserBayToken::TOKEN_BASE) and (
				token == "#switch" or
				token == "#ifcode"
			)
		){
			return true;
		}
		
		return false;
	}
	
	
	
	/**
	 * Read prepocessors block
	 */
	BaseOpCode readPreprocessor(){
	
		if (this.findNextToken("#switch")){
			
			Vector<BaseOpCode> childs = new Vector();
			
			string comment; int pos;
			this.matchNextToken("#switch");
			Vector<string> v = (new Vector())
				.push("#case")
				.push("#endswitch")
			;
			while (this.findNextToken("#case")){
				this.matchNextToken("#case");
				this.matchNextToken("ifcode");
				
				OpPreprocessorCase op_case = new OpPreprocessorCase();
				op_case.condition = this.readExpression();
				if (not this.findNextToken("then")){
					this.nextTokenExpected("then");
				}
				op_case.value = rs::trim(this.next_token.readUntilVector(v));
				childs.push(op_case);
				
				this.readAnyNextToken();
				pos = this.findNextTokenVector(v);
				if (pos == -1){
					this.parserError("Unknown preprocessor token " ~ this.lookNextToken());
				}
			}
			this.matchNextToken("#endswitch");
			delete v;
			
			return new OpPreprocessorSwitch(childs);
		}
		else if (this.findNextToken("#ifcode")){
			this.matchNextToken("#endifcode");
		}
		else{
			this.parserError("Unknown preprocessor token " ~ this.lookNextToken());
		}
	}
	
	
	
	/**
	 * Read program
	 * @return BaseOpCode
	 */
	Vector<BaseOpCode> readProgram(){
		
		BaseOpCode op_code = null;
		Vector<BaseOpCode> res = new Vector();
		
		while (not this.isEOF()){
			if (this.findNextToken("namespace")){
				res.push(this.readOperatorNamespace());
				continue;
			}
			else if (this.findNextToken("use")){
				res.push(this.readOperatorUse());
				continue;
			}
			else if (this.findNextTokenPreprocessor()){
				res.push(this.readPreprocessor());
				continue;
			}
			
			OpFlags flags = this.readFlags();
			if (this.findNextToken("class")){
				res.push(this.readDeclareClass(flags));
			}
			else if (this.findNextToken("interface")){
				res.push(this.readDeclareInterface(flags));
			}
			else {
				this.parserError("Unknown token " ~ this.lookNextToken());
			}
		}
		
		return res;
	}
	
	
	
	/**
	 * Parser function
	 */
	void runParser(){
		this._result = new OpNope( this.readProgram() );
	}
	
	
	
}