/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang;

use Runtime.BaseObject;
use Runtime.lib;
use Runtime.Reference;
use Runtime.Interfaces.ContextInterface;
use BayLang.Caret;
use BayLang.SaveOpCode;
use BayLang.TokenReader;
use BayLang.Exceptions.ParserExpected;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpNamespace;
use BayLang.OpCodes.OpTypeIdentifier;


class CoreParser extends BaseObject
{
	string file_name = "";
	string content = "";
	int content_size = 0;
	int tab_size = 4;
	int function_level = 0;
	bool find_variable = true;
	Map<Map> vars = {};
	Map<BaseOpCode> vars_uses = {};
	Map<string> uses = {};
	OpNamespace current_namespace = null;
	OpDeclareClass current_class = null;
	string current_namespace_name = "";
	
	
	/**
	 * Save vars
	 */
	Map saveVars() => this.vars.copy();
	
	
	/**
	 * Restore vars
	 */
	void restoreVars(Map vars)
	{
		this.vars = vars;
	}
	
	
	/**
	 * Add variable
	 */
	void addVariable(OpIdentifier op_code, OpTypeIdentifier pattern)
	{
		string name = op_code.value;
		this.vars.set(name, {
			"pattern": pattern,
			"function_level": this.function_level,
		});
	}
	
	
	/**
	 * Use variable
	 */
	void useVariable(OpIdentifier op_code)
	{
		Map variable = this.vars.get(op_code.value);
		if (not (variable instanceof Map) or
			variable.get("function_level") >= this.function_level)
		{
			return;
		}
		if (not this.vars_uses.has(op_code.value))
		{
			this.vars_uses.set(op_code.value, op_code);
		}
	}
	
	
	/**
	 * Set content
	 */
	CoreParser setContent(string content)
	{
		this.content = content;
		this.content_size = rs::strlen(content);
		return this;
	}
	
	
	/**
	 * Create reader
	 */
	TokenReader createReader()
	{
		TokenReader reader = new TokenReader();
		reader.init(
			new Caret{
				"content": new Reference(this.content),
				"tab_size": this.tab_size,
			}
		);
		return reader;
	}
	
	
	/**
	 * Parse file and convert to BaseOpCode
	 */
	BaseOpCode parse()
	{
		return null;
	}
}