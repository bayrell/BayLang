/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.Helper;

use Runtime.fs;
use Runtime.re;
use Runtime.lib;
use Runtime.BaseObject;
use Runtime.SerializeInterface;
use Runtime.Serializer;
use BayLang.Helper.Cache;
use BayLang.Helper.Project;
use BayLang.Helper.RouteProcessor;
use BayLang.Helper.Widget;
use BayLang.Helper.WidgetProcessor;
use BayLang.LangBay.ParserBay;
use BayLang.LangES6.TranslatorES6;
use BayLang.LangNode.TranslatorNode;
use BayLang.LangPHP.TranslatorPHP;
use BayLang.OpCodes.BaseOpCode;
use BayLang.CoreTranslator;
use BayLang.LangUtils;


class Module extends BaseObject implements SerializeInterface
{
	Project project = null;
	string path = "";
	string src_path = "";
	Dict<string> dest_path = {};
	string name = "";
	Dict submodules = null;
	Vector<Map> routes = null;
	Vector<string> groups = null;
	Vector<Widget> widgets = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(Project project)
	{
		parent();
		this.project = project;
	}
	
	
	/**
	 * Read project from folder
	 */
	static Module readModule(Project project, string path)
	{
		Module module = new Module(project);
		module.init(path);
		if (not module.exists()) return null;
		return module;
	}
	
	
	/**
	 * Init module
	 */
	bool init(string module_path)
	{
		this.path = "";
		
		string file_name = rs::basename(module_path);
		if (file_name == "") return false;
		if (file_name[0] == ".") return false;
		if (not(await fs::isFolder(module_path))) return false;
		
		/* Module json file */
		string module_json_path = module_path ~ "/" ~ "module.json";
		if (not(await fs::isFile(module_json_path))) return false;
		
		/* Read file */
		string content = await fs::readFile(module_json_path);
		Dict module_info = rtl::json_decode(content);
		
		if (not module_info) return false;
		if (not module_info.has("name")) return false;
		
		this.path = module_path;
		this.name = module_info.get("name");
		this.src_path = module_info.get("src");
		this.dest_path = module_info.get("dest");
		this.groups = module_info.get("groups");
		this.submodules = module_info.get("modules");
		
		return true;
	}
	
	
	/**
	 * Process project cache
	 */
	void serialize(Serializer serializer, Map data)
	{
		serializer.process(this, "groups", data);
		serializer.process(this, "name", data);
		serializer.process(this, "path", data);
		serializer.process(this, "routes", data);
		serializer.process(this, "src_path", data);
		serializer.process(this, "dest_path", data);
		serializer.process(this, "submodules", data);
		serializer.processItems(this, "widgets", data,
			Widget (Dict widget) => new Widget(this)
		);
	}
	
	
	/**
	 * Returns true if project is exists
	 */
	void exists() => this.path != "";
	
	
	/**
	 * Returns module path
	 */
	string getPath() => this.path;
	
	
	/**
	 * Returns module name
	 */
	string getName() => this.name;
	
	
	/**
	 * Returns routes
	 */
	Collection<Dict> getRoutes() => this.routes ? this.routes : [];
	
	
	/**
	 * Returns route by name
	 */
	Dict getRoute(string route_name) =>
		this.getRoutes().findItem(lib::equalAttr("name", route_name))
	;
	
	
	/**
	 * Returns widgets
	 */
	Collection<Widget> getWidgets() => this.widgets ? this.widgets : [];
	
	
	/**
	 * Returns widget by name
	 */
	Widget getWidget(string widget_name) =>
		this.getWidgets().findItem(lib::equalAttr("name", widget_name))
	;
	
	
	/**
	 * Returns source folder path
	 */
	string getSourceFolderPath() => fs::join([ this.getPath(), this.src_path ]);
	
	
	/**
	 * Returns dest folder path
	 */
	string getDestFolderPath(string lang)
	{
		if (not this.dest_path.has(lang)) return "";
		return fs::join([ this.getPath(), this.dest_path.get(lang) ]);
	}
	
	
	/**
	 * Returns relative source path
	 */
	string getRelativeSourcePath(string file_path)
	{
		string source_path = this.getSourceFolderPath();
		int source_path_sz = rs::strlen(source_path);
		if (rs::substr(file_path, 0, source_path_sz) != source_path) return "";
		return rs::addFirstSlash(rs::substr(file_path, source_path_sz));
	}
	
	
	/**
	 * Returns class name file path
	 */
	string resolveClassName(string class_name)
	{
		/* Check if class name start with module name */
		int module_name_sz = rs::strlen(this.getName());
		if (rs::substr(class_name, 0, module_name_sz) != this.getName())
		{
			return "";
		}
		
		/* Remove module name from class name */
		class_name = rs::substr(class_name, module_name_sz);
		
		/* Return path to class name */
		string path = this.getSourceFolderPath();
		Vector<string> arr = rs::split(".", class_name);
		arr.prepend(path);
		return fs::join(arr) ~ ".bay";
	}
	
	
	/**
	 * Resolve dest path
	 */
	string resolveDestPath(string file_path, string lang)
	{
		string dest_folder_path = this.getDestFolderPath(lang);
		if (dest_folder_path == "") return "";
		
		/* Get dest file path */
		string relative_path = this.getRelativeSourcePath(file_path);
		string dest_file_path = fs::join([ dest_folder_path, relative_path ]);
		
		/* Resolve extension */
		if (lang == "php")
		{
			dest_file_path = re::replace("\\.bay$", ".php", dest_file_path);
			dest_file_path = re::replace("\\.ui$", ".php", dest_file_path);
		}
		else if (lang == "es6")
		{
			dest_file_path = re::replace("\\.bay$", ".js", dest_file_path);
			dest_file_path = re::replace("\\.ui$", ".js", dest_file_path);
		}
		else if (lang == "nodejs")
		{
			dest_file_path = re::replace("\\.bay$", ".js", dest_file_path);
			dest_file_path = re::replace("\\.ui$", ".js", dest_file_path);
		}
		
		return dest_file_path;
	}
	
	
	/**
	 * Returns true if module has group
	 */
	bool hasGroup(string group_name)
	{
		if (rs::substr(group_name, 0, 1) != "@") return false;
		group_name = rs::substr(group_name, 1);
		if (this.groups == null) return false;
		if (this.groups.indexOf(group_name) == -1) return false;
		return true;
	}
	
	
	/**
	 * Returns true if this module contains in module list include groups
	 */
	bool inModuleList(Collection<string> module_names)
	{
		for (int i=0; i<module_names.count(); i++)
		{
			string module_name = module_names.get(i);
			if (this.name == module_name) return true;
			if (this.hasGroup(module_name)) return true;
		}
		return false;
	}
	
	
	/**
	 * Rename module
	 */
	void rename(string new_module_name)
	{
		return;
		
		if (not this.exists()) return;
		
		string new_file_name = rs::lower(new_module_name);
		string old_file_name = this.file_name;
		
		/* Set new module name */
		this.info.set("name", new_module_name);
		
		/* Save file */
		string module_path = "/data/projects/" ~ old_file_name;
		string module_json_path = module_path ~ "/" ~ "module.json";
		string content = rtl::json_encode(this.info, rtl::JSON_PRETTY);
		await fs::saveFile(module_json_path, content);
		
		/* Rename folder */
		#ifcode PHP then
		rename($this.path, "/data/projects/" . $new_file_name);
		#endif
	}
	
	
	/**
	 * Compile file
	 */
	bool compile(string file_path)
	{
		/* Read file */
		if (not(await fs::isFile(file_path))) return false;
		string file_content = await fs::readFile(file_path);
		
		/* Parse file */
		ParserBay parser = new ParserBay();
		list res = parser::parse(parser, file_content);
		BaseOpCode file_op_code = res.get(1);
		if (not file_op_code) return false;
		
		/* Translate project languages */
		Collection languages = this.project.getLanguages();
		for (int i=0; i<languages.count(); i++)
		{
			string lang = languages.get(i);
			if (lang == "bay") continue;
			this.translate(file_path, file_op_code, lang);
		}
		
		return true;
	}
	
	
	/**
	 * Translate file
	 */
	bool translate(string src_file_path, BaseOpCode op_code, string lang)
	{
		/* Get dest file path */
		string dest_file_path = this.resolveDestPath(src_file_path, lang);
		if (dest_file_path == "") return false;
		
		/* Create translator */
		CoreTranslator translator = LangUtils::createTranslator(lang);
		if (not translator) return false;
		
		/* Translate */
		list res = translator::translate(translator, op_code);
		string dest_file_content = res.get(1);
		
		/* Create dest folder if not exists */
		string dest_dir_name = rs::dirname(dest_file_path);
		if (not await fs::isFolder(dest_dir_name))
		{
			await fs::mkdir(dest_dir_name);
		}
		
		/* Save file */
		await fs::saveFile(dest_file_path, dest_file_content);
		
		return true;
	}
	
	
	/**
	 * Load routes
	 */
	async void loadRoutes()
	{
		RouteProcessor route_processor = new RouteProcessor(this);
		await route_processor.load();
	}
	
	
	/**
	 * Load widgets
	 */
	async void loadWidgets()
	{
		WidgetProcessor widget_processor = new WidgetProcessor(this);
		await widget_processor.load();
	}
}