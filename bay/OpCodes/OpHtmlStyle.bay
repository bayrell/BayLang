/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
 
namespace BayLang.OpCodes;

use Runtime.Reference;
use Runtime.Serializer;
use BayLang.Caret;
use BayLang.TokenReader;
use BayLang.Exceptions.ParserUnknownError;
use BayLang.LangBay.ParserBay;
use BayLang.LangBay.ParserBayHtml;
use BayLang.OpCodes.BaseOpCode;


class OpHtmlStyle extends BaseOpCode
{
	string op = "op_html_style";
	string content = "";
	bool is_global = false;
	BaseOpCode value = null;
	
	
	/**
	 * Serialize object
	 */
	void serialize(Serializer serializer, Map data)
	{
		parent(serializer, data);
		serializer.process(this, "content", data);
		serializer.process(this, "is_global", data);
		serializer.process(this, "value", data);
	}
	
	
	/**
	 * Read styles from content
	 */
	Dict readStyles()
	{
		TokenReader reader = new TokenReader();
		reader.init(
			new Caret{
				"content": new Reference(this.content),
			}
		);
		
		Map styles = {};
		while (not reader.eof() and reader.nextToken() == ".")
		{
			string selector = this.readSelector(reader);
			string code = this.readCssBlock(reader);
			styles.set(selector, code);
		}
		
		return styles;
	}
	
	
	/**
	 * Read selector
	 */
	string readSelector(TokenReader reader)
	{
		Collection items = [];
		while (not reader.eof() and reader.nextToken() != "{")
		{
			items.push(reader.readToken());
		}
		return rs::join("", items);
	}
	
	
	/**
	 * Read css block
	 */
	string readCssBlock(TokenReader reader)
	{
		reader.matchToken("{");
		Caret caret = reader.main_caret;
		
		int level = 0;
		Collection items = [];
		while (not caret.eof() and (caret.nextChar() != "}" and level == 0 or level > 0))
		{
			string ch = caret.readChar();
			if (ch == "{") level = level + 1;
			if (ch == "}") level = level - 1;
			items.push(ch);
		}
		
		reader.init(caret);
		reader.matchToken("}");
		
		return rs::join("", items);
	}
}