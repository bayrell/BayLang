/*!
 *  Bayrell Language
 *
 *  (c) Copyright 2016-2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.bayrell.org/licenses/APACHE-LICENSE-2.0.html
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.Lang.LangPHP;

use Runtime.re;
use Bayrell.Lang.SaveOpCode;
use Bayrell.Lang.LangPHP.TranslatorPHP;
use Bayrell.Lang.OpCodes.BaseOpCode;
use Bayrell.Lang.OpCodes.OpAttr;
use Bayrell.Lang.OpCodes.OpCall;
use Bayrell.Lang.OpCodes.OpClassOf;
use Bayrell.Lang.OpCodes.OpCollection;
use Bayrell.Lang.OpCodes.OpDeclareFunction;
use Bayrell.Lang.OpCodes.OpDeclareFunctionArg;
use Bayrell.Lang.OpCodes.OpDict;
use Bayrell.Lang.OpCodes.OpHtmlItems;
use Bayrell.Lang.OpCodes.OpHtmlTag;
use Bayrell.Lang.OpCodes.OpHtmlValue;
use Bayrell.Lang.OpCodes.OpIdentifier;
use Bayrell.Lang.OpCodes.OpInc;
use Bayrell.Lang.OpCodes.OpMath;
use Bayrell.Lang.OpCodes.OpNew;
use Bayrell.Lang.OpCodes.OpNumber;
use Bayrell.Lang.OpCodes.OpPipe;
use Bayrell.Lang.OpCodes.OpString;
use Bayrell.Lang.OpCodes.OpTernary;
use Bayrell.Lang.OpCodes.OpTypeConvert;
use Bayrell.Lang.OpCodes.OpTypeIdentifier;


static class TranslatorPHPExpression
{
	
	/**
	 * Returns string
	 */
	lambda string toString(string s)
	{
		s = re::replace('\\\\', '\\\\', s);
		s = re::replace('"', '\\\"', s);
		s = re::replace('\n', '\\n', s);
		s = re::replace('\r', '\\r', s);
		s = re::replace('\t', '\\t', s);
		return '"' ~ s ~ '"';
	}
	
	
	
	/**
	 * To pattern
	 */
	lambda string toPattern(TranslatorPHP t, OpTypeIdentifier pattern)
	{
		Collection names = static::findModuleNames(t, pattern.entity_name.names);
		string e = rs::join(".", names);
		Collection a = (pattern.template != null) ? 
			pattern.template.map
			(
				string (OpTypeIdentifier pattern) use (t) => static::toPattern(t, pattern)
			) : null
		;
		string b = (a != null) ? ",\"t\":[" ~ rs::join(",",a) ~ "]" : "";
		return "[\"e\"=>" ~ static::toString(e) ~ b ~"]";
	}
	
	
	
	/**
	 * Returns string
	 */
	lambda string rtlToStr(TranslatorPHP t, string s)
	{
		string module_name = static::getModuleName(t, "rtl");
		return module_name ~ "::toStr("~s~")";
	}
	
	
	
	/**
	 * Find module name
	 */
	lambda string findModuleName(TranslatorPHP t, string module_name)
	{
		if (module_name == "Collection") return "Runtime.Collection";
		else if (module_name == "Dict") return "Runtime.Dict";
		else if (module_name == "Map") return "Runtime.Map";
		else if (module_name == "Vector") return "Runtime.Vector";
		else if (module_name == "rs") return "Runtime.rs";
		else if (module_name == "rtl") return "Runtime.rtl";
		else if (module_name == "ArrayInterface") return "ArrayAccess";
		else if (t.modules.has(module_name)) return t.modules.item(module_name);
		return module_name;
	}
	
	
	
	/**
	 * Returns module name
	 */
	lambda Collection findModuleNames(TranslatorPHP t, Collection<string> names)
	{
		if (names.count() > 0)
		{
			string module_name = names.first();
			module_name = static::findModuleName(t, module_name);
			if (module_name != "")
			{
				names = names.removeFirstIm().prependCollectionIm(rs::split("\\.", module_name));
			}
		}
		return names;
	}
	
	
	
	/**
	 * Return module name
	 */
	lambda string getModuleName(TranslatorPHP t, string module_name)
	{
		module_name = static::findModuleName(t, module_name);
		module_name = rs::replace("\\.", "\\", module_name);
		return "\\" ~ module_name;
	}
	
	
	
	/**
	 * Return module name
	 */
	lambda string getModuleNames(TranslatorPHP t, Collection<string> names)
	{
		return "\\" ~ rs::join("\\", static::findModuleNames(t, names));
	}
	
	
	
	/**
	 * OpTypeIdentifier
	 */
	lambda list<TranslatorPHP, string> OpTypeIdentifier(TranslatorPHP t, OpTypeIdentifier op_code)
	{
		Collection<string> names = static::findModuleNames(t, op_code.entity_name.names);
		string s = "\\" ~ rs::join("\\", names);
		return [t, s];
	}
	
	
	
	/**
	 * OpIdentifier
	 */
	lambda list<TranslatorPHP, string> OpIdentifier(TranslatorPHP t, OpIdentifier op_code)
	{
		if (op_code.value == "@") return [t, "$ctx"];
		if (op_code.value == "_") return [t, "($ctx->staticMethod('translate'))"];
		if (op_code.value == "log") return [t, "var_dump"];
		
		if (t.modules.has(op_code.value) or op_code.kind == OpIdentifier::KIND_SYS_TYPE)
		{
			string module_name = op_code.value;
			string new_module_name = static::getModuleName(t, module_name);
			return [t, new_module_name];
		}
		else if (op_code.kind == OpIdentifier::KIND_VARIABLE)
		{
			string content = op_code.value;
			return [t, "$" ~ content];
		}
		else if (op_code.kind == OpIdentifier::KIND_CLASSREF)
		{
			string content = op_code.value;
			if (content == "this") content = "$this";
			return [t, content];
		}
		
		string content = op_code.value;
		return [t, content];
	}
	
	
	
	/**
	 * OpNumber
	 */
	lambda list<TranslatorPHP, string> OpNumber(TranslatorPHP t, OpNumber op_code)
	{
		string content = op_code.value;
		if (op_code.negative)
		{
			content = "-" ~ content;
			t <= opcode_level <= 15;
		}
		return [t, content];
	}
	
	
	
	/**
	 * OpString
	 */
	lambda list<TranslatorPHP, string> OpString(TranslatorPHP t, OpString op_code)
	{
		return [t, static::toString(op_code.value)];
	}
	
	
	
	/**
	 * OpCollection
	 */
	lambda list<TranslatorPHP, string> OpCollection(TranslatorPHP t, OpCollection op_code)
	{
		string content = "";
		Collection<string> values = op_code.values.map
		(
			string (BaseOpCode op_code) use (t)
			{
				list res = static::Expression(t, op_code); t = res[0]; string s = res[1];
				return s;
			}
		);
		string module_name = static::getModuleName(t, "Collection");
		content = module_name ~ "::from([" ~ rs::join(",", values) ~ "])";
		return [t, content];
	}
	
	
	
	/**
	 * OpDict
	 */
	lambda list<TranslatorPHP, string> OpDict(TranslatorPHP t, OpDict op_code, bool flag_array = false)
	{
		string content = "";
		Collection<string> values = op_code.values.transition
		(
			string (BaseOpCode op_code, string key) use (t)
			{
				list res = static::Expression(t, op_code); t = res[0]; string s = res[1];
				return static::toString(key) ~ "=>" ~ s;
			}
		);
		string module_name = static::getModuleName(t, "Dict");
		if (not flag_array)
		{
			content = module_name ~ "::from([" ~ rs::join(",", values) ~ "])";
		}
		else
		{
			content = "[" ~ rs::join(",", values) ~ "]";
		}
		return [t, content];
	}
	
	
	
	/**
	 * Dynamic
	 */
	lambda list<TranslatorPHP, string> Dynamic(TranslatorPHP t, BaseOpCode op_code, BaseOpCode next_op_code = null)
	{
		if (op_code instanceof OpIdentifier)
		{
			return static::OpIdentifier(t, op_code);
		}
		else if (op_code instanceof OpAttr)
		{
			Vector<BaseOpCode> attrs = new Vector();
			OpAttr op_code_item = op_code;
			BaseOpCode op_code_next = op_code;
			string prev_kind = "";
			string s = "";
			
			while (op_code_next instanceof OpAttr)
			{
				attrs.push(op_code_next);
				op_code_item = op_code_next;
				op_code_next = op_code_next.obj;
			}
			
			attrs = attrs.reverseIm();
			
			if (op_code_next instanceof OpCall)
			{
				prev_kind = "var";
				list res = static::OpCall(t, op_code_next); t = res[0]; s = res[1];
			}
			else if (op_code_next instanceof OpNew)
			{
				prev_kind = "var";
				list res = static::OpNew(t, op_code_next); t = res[0]; s = "(" ~ res[1] ~ ")";
			}
			else if (op_code_next instanceof OpIdentifier)
			{
				if (op_code_next.kind == OpIdentifier::KIND_CLASSREF)
				{
					if (op_code_next.value == "static")
					{
						s = "static";
						prev_kind = "static";
					}
					else if (op_code_next.value == "parent")
					{
						s = "parent";
						prev_kind = "static";
					}
					else if (op_code_next.value == "self")
					{
						prev_kind = "static";
						s = static::getModuleName(t, t.current_class_full_name);
					}
					else if (op_code_next.value == "this")
					{
						prev_kind = "var";
						s = "$this";
					}
				}
				else
				{
					list res = static::OpIdentifier(t, op_code_next); t = res[0]; s = res[1];
					prev_kind = "var";
					if (t.modules.has(op_code_next.value) or op_code_next.kind == OpIdentifier::KIND_SYS_TYPE)
					{
						prev_kind = "static";
					}
				}
			}
			
			int attrs_sz = attrs.count();
			for (int i=0; i<attrs.count(); i++)
			{
				OpAttr attr = attrs.item(i);
				OpAttr next_attr = attrs.get(i + 1, null);
				
				if (attr.kind == OpAttr::KIND_ATTR)
				{
					s ~= "->" ~ attr.value.value;
				}
				else if (attr.kind == OpAttr::KIND_STATIC)
				{
					if (prev_kind == "static")
					{
						string attr_val = attr.value.value;
						if (i == attrs_sz - 1 and next_op_code instanceof OpCall)
						{
							s ~= "::" ~ attr_val;
						}
						else if (rs::strtoupper(attr_val) == attr_val)
						{
							s ~= "::" ~ attr_val;
						}
						else
						{
							s ~= "::$" ~ attr_val;
						}
					}
					else
					{
						s = "(" ~ s ~ "->staticMethod(" ~ static::toString(attr.value.value) ~ "))";
					}
					prev_kind = "static";
				}
				else if (attr.kind == OpAttr::KIND_DYNAMIC)
				{
					list res = static::Expression(t, attr.value);
					t = res[0]; s ~= "[" ~ res[1] ~ "]";
				}
				else if (attr.kind == OpAttr::KIND_PIPE)
				{
					s ~= "->" ~ attr.value.value;
				}
			}
			
			return [t, s];
		}
		else if (op_code instanceof OpCall)
		{
			return static::OpCall(t, op_code);
		}
		else if (op_code instanceof OpPipe)
		{
			return static::OpPipe(t, op_code);
		}
		return [t, ""];
	}
	
	
	
	/**
	 * OpInc
	 */
	lambda list<TranslatorPHP, string> OpInc(TranslatorPHP t, OpInc op_code)
	{
		string content = "";
		list res = static::Expression(t, op_code.value); t = res[0]; string s = res[1];
		
		if (op_code.kind == OpInc::KIND_PRE_INC) content = "++$" ~ s;
		else if (op_code.kind == OpInc::KIND_PRE_DEC) content = "--$" ~ s;
		else if (op_code.kind == OpInc::KIND_POST_INC) content = "$" ~ s ~ "++";
		else if (op_code.kind == OpInc::KIND_POST_DEC) content = "$" ~ s ~ "--";
		
		return [t, content];
	}
	
	
	
	/**
	 * OpMath
	 */
	lambda list<TranslatorPHP, string> OpMath(TranslatorPHP t, OpMath op_code)
	{
		list res = static::Expression(t, op_code.value1); t = res[0];
		int opcode_level1 = res[0].opcode_level;
		string s1 = res[1];
		
		string op = ""; string op_math = op_code.math;
		int opcode_level = 0;
		if (op_code.math == "!") { opcode_level = 16; op = "!"; }
		if (op_code.math == ">>") { opcode_level = 12; op = ">>"; }
		if (op_code.math == "<<") { opcode_level = 12; op = "<<"; }
		if (op_code.math == "&") { opcode_level = 9; op = "&"; }
		if (op_code.math == "xor") { opcode_level = 8; op = "^"; }
		if (op_code.math == "|") { opcode_level = 7; op = "|"; }
		if (op_code.math == "*") { opcode_level = 14; op = "*"; }
		if (op_code.math == "/") { opcode_level = 14; op = "/"; }
		if (op_code.math == "%") { opcode_level = 14; op = "%"; }
		if (op_code.math == "div") { opcode_level = 14; op = "div"; }
		if (op_code.math == "mod") { opcode_level = 14; op = "mod"; }
		if (op_code.math == "+") { opcode_level = 13; op = "+"; }
		if (op_code.math == "-") { opcode_level = 13; op = "-"; }
		if (op_code.math == "~") { opcode_level = 13; op = "+"; }
		if (op_code.math == "!") { opcode_level = 13; op = "!"; }
		if (op_code.math == "===") { opcode_level = 10; op = "==="; }
		if (op_code.math == "!==") { opcode_level = 10; op = "!=="; }
		if (op_code.math == "==") { opcode_level = 10; op = "=="; }
		if (op_code.math == "!=") { opcode_level = 10; op = "!="; }
		if (op_code.math == ">=") { opcode_level = 10; op = ">="; }
		if (op_code.math == "<=") { opcode_level = 10; op = "<="; }
		if (op_code.math == ">") { opcode_level = 10; op = ">"; }
		if (op_code.math == "<") { opcode_level = 10; op = "<"; }
		if (op_code.math == "is") { opcode_level = 10; op = "instanceof"; }
		if (op_code.math == "instanceof") { opcode_level = 10; op = "instanceof"; }
		if (op_code.math == "implements") { opcode_level = 10; op = "implements"; }
		if (op_code.math == "not") { opcode_level = 16; op = "!"; }
		if (op_code.math == "and") { opcode_level = 6; op = "&&"; }
		if (op_code.math == "&&") { opcode_level = 6; op = "&&"; }
		if (op_code.math == "or") { opcode_level = 5; op = "||"; }
		if (op_code.math == "||") { opcode_level = 5; op = "||"; }
		
		string content = "";
		if (op_code.math == "!" or op_code.math == "not")
		{
			content = op ~ t.o(s1, opcode_level1, opcode_level);
		}
		else
		{
			list res = static::Expression(t, op_code.value2); t = res[0];
			int opcode_level2 = res[0].opcode_level;
			string s2 = res[1];
			string op1 = t.o(s1, opcode_level1, opcode_level);
			string op2 = t.o(s2, opcode_level2, opcode_level);
			
			if (op_math == "~")
			{
				content = op1 ~ " . " ~ static::rtlToStr(t, op2);
			}
			else if (op_math == "implements")
			{
				content = op1 ~ " instanceof " ~ op2;
			}
			else
			{
				content = op1 ~ " " ~ op ~ " " ~ op2;
			}
		}
		
		t <= opcode_level <= opcode_level;
		return [t, content];
	}
	
	
	
	/**
	 * OpNew
	 */
	lambda list<TranslatorPHP, string> OpNew(TranslatorPHP t, OpInc op_code)
	{
		string content = "new ";
		list res = static::OpTypeIdentifier(t, op_code.value); t = res[0]; content ~= res[1];
		bool flag = false;
		
		content ~= "(";
		if (t.current_function == null or t.current_function.is_context)
		{
			content ~= "$ctx";
			flag = true;
		}
		for (int i=0; i<op_code.args.count(); i++)
		{
			BaseOpCode item = op_code.args.item(i);
			list res = t.expression::Expression(t, item); t = res[0]; string s = res[1];
			content ~= (flag ? ", " : "") ~ s;
			flag = true;
		}
		content ~= ")";
		
		return [t, content];
	}
	
	
	
	/**
	 * OpCall
	 */
	lambda list<TranslatorPHP, string> OpCall(TranslatorPHP t, OpCall op_code)
	{
		string s = "";
		bool flag = false;
		
		list res = static::Dynamic(t, op_code.obj, op_code); t = res[0]; s = res[1];
		if (s == "parent")
		{
			string f_name = t.current_function.name;
			if (f_name == "constructor") f_name = "__construct";
			s = "parent::" ~ f_name ~ "(";
		}
		else s ~= "(";
		
		string content = s;
		if (op_code.obj instanceof OpIdentifier and op_code.obj.value == "_")
		{
			content ~= (flag ? ", " : "") ~ "$ctx, $ctx";
			flag = true;
		}
		else if ((t.current_function == null or t.current_function.is_context) and op_code.is_context)
		{
			content ~= (flag ? ", " : "") ~ "$ctx";
			flag = true;
		}
		for (int i=0; i<op_code.args.count(); i++)
		{
			BaseOpCode item = op_code.args.item(i);
			list res = static::Expression(t, item); t = res[0]; string s = res[1];
			content ~= (flag ? ", " : "") ~ s;
			flag = true;
		}
		content ~= ")";
		
		return [t, content];
	}
	
	
	
	/**
	 * OpPipe
	 */
	lambda list<TranslatorPHP, string> OpPipe(TranslatorPHP t, OpPipe op_code)
	{
		string content = "";
		string var_name = "";
		list res = static::Expression(t, op_code.obj, op_code); t = res[0];
		if (op_code.obj instanceof OpIdentifier)
		{
			var_name = res[1];
		}
		else
		{
			list res = t::addSaveOpCode
			(
				t,
				{
					"op_code": op_code.obj,
					"var_content": res[1],
				}
			);
			t = res[0]; var_name = res[1];
		}
		
		if (op_code.kind == OpPipe::KIND_METHOD)
		{
			content = "(" ~ var_name ~ "->staticMethod(" ~ static::toString(op_code.method_name.value) ~ "))";
		}
		else
		{
			list res = static::OpTypeIdentifier(t, op_code.class_name); t = res[0];
			content = res[1] ~ "::" ~ op_code.method_name.value;
		}
		
		bool flag = false;
		content ~= "(";
		if (t.current_function.is_context and op_code.is_context)
		{
			content ~= "$ctx";
			flag = true;
		}
		content ~= (flag ? ", " : "") ~ var_name; flag = true;
		for (int i=0; i<op_code.args.count(); i++)
		{
			BaseOpCode item = op_code.args.item(i);
			list res = static::Expression(t, item); t = res[0]; string s1 = res[1];
			content ~= (flag ? ", " : "") ~ s1;
			flag = true;
		}
		content ~= ")";
		
		return [t, content];
	}
	
	
	
	/**
	 * OpClassOf
	 */
	lambda list<TranslatorPHP, string> OpClassOf(TranslatorPHP t, OpClassOf op_code)
	{
		Collection<string> names = static::findModuleNames(t, op_code.entity_name.names);
		string s = rs::join(".", names);
		return [t, static::toString(s)];
	}
	
	
	
	/**
	 * OpTernary
	 */
	lambda list<TranslatorPHP, string> OpTernary(TranslatorPHP t, OpTernary op_code)
	{
		string content = "";
		
		t <= opcode_level <= 100;
		list res = static::Expression(t, op_code.condition); t = res[0]; string condition = res[1];
		list res = static::Expression(t, op_code.if_true); t = res[0]; string if_true = res[1];
		list res = static::Expression(t, op_code.if_false); t = res[0]; string if_false = res[1];
		
		content ~= "(" ~ condition ~ ") ? " ~ if_true ~ " : " ~ if_false;
		t <= opcode_level <= 11;
		
		/* OpTernary */
		return [t, content];
	}
	
	
	
	/**
	 * OpTypeConvert
	 */
	lambda list<TranslatorPHP, string> OpTypeConvert(TranslatorPHP t, OpTypeConvert op_code)
	{
		string content = "";
		list res = static::Expression(t, op_code.value); t = res[0]; string value = res[1];
		content = "\\Runtime\\rtl::to(" ~ value ~ ", " ~ static::toPattern(t, op_code.pattern) ~ ")";
		return [t, content];
	}
	
	
	
	/**
	 * OpTernary
	 */
	lambda list<TranslatorPHP, string> OpDeclareFunction(TranslatorPHP t, OpDeclareFunction op_code)
	{
		string content = "";
		
		/* Set function name */
		OpDeclareFunction save_f = t.current_function;
		t <= current_function <= op_code;
		
		list res = t.operator::OpDeclareFunctionArgs(t, op_code); string args = res[1];
		content ~= "function (" ~ args ~ ")";
		if (op_code.vars != null and op_code.vars.count() > 0)
		{
			Collection<string> vars = op_code.vars.map( string (string s) => "&$" ~ s );
			content ~= " use (" ~ rs::join(",", vars) ~ ")";
		}
		list res = t.operator::OpDeclareFunctionBody(t, op_code);
		content ~= res[1];
		
		/* Restore function */
		t <= current_function <= save_f;
		
		/* OpTernary */
		return [t, content];
	}
	
	
	
	/**
	 * Expression
	 */
	lambda list<TranslatorPHP, string> Expression(TranslatorPHP t, BaseOpCode op_code)
	{
		string content = "";
		t <= opcode_level <= 100;
		
		if (op_code instanceof OpIdentifier)
		{
			list res = static::OpIdentifier(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpTypeIdentifier)
		{
			list res = static::OpTypeIdentifier(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpNumber)
		{
			list res = static::OpNumber(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpString)
		{
			list res = static::OpString(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpCollection)
		{
			list res = static::OpCollection(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpDict)
		{
			list res = static::OpDict(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpInc)
		{
			t <= opcode_level <= 16;
			list res = static::OpInc(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpMath)
		{
			list res = static::OpMath(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpNew)
		{
			list res = static::OpNew(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpAttr or op_code instanceof OpPipe)
		{
			list res = static::Dynamic(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpCall)
		{
			list res = static::OpCall(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpClassOf)
		{
			list res = static::OpClassOf(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpTernary)
		{
			list res = static::OpTernary(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpTypeConvert)
		{
			list res = static::OpTypeConvert(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpDeclareFunction)
		{
			list res = static::OpDeclareFunction(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpHtmlItems)
		{
			list res = t.html::OpHtmlItems(t, op_code); t = res[0]; content = res[1];
		}
		
		return [t, content];
	}
	
}