/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangPHP;

use Runtime.re;
use Runtime.BaseObject;
use BayLang.SaveOpCode;
use BayLang.LangPHP.TranslatorPHP;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAttr;
use BayLang.OpCodes.OpCall;
use BayLang.OpCodes.OpClassOf;
use BayLang.OpCodes.OpCollection;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpDict;
use BayLang.OpCodes.OpDictPair;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpMath;
use BayLang.OpCodes.OpNew;
use BayLang.OpCodes.OpNumber;
use BayLang.OpCodes.OpPreprocessorIfDef;
use BayLang.OpCodes.OpString;
use BayLang.OpCodes.OpTypeIdentifier;


class TranslatorPHPExpression extends BaseObject
{
	TranslatorPHP translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorPHP translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpIdentifier
	 */
	void OpIdentifier(OpIdentifier op_code, Collection<string> result)
	{
		if (op_code.value == "print") result.push("echo");
		else result.push("$" ~ op_code.value);
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpNumber
	 */
	void OpNumber(OpNumber op_code, Collection<string> result)
	{
		result.push(op_code.value);
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpString
	 */
	void OpString(OpNumber op_code, Collection<string> result)
	{
		result.push(this.translator.toString(op_code.value));
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpCode generics
	 */
	void OpCodeGenerics(Collection<OpTypeIdentifier> items, Collection<string> result)
	{
		if (not items) return;
		
		/* Get items count */
		int items_count = items.count();
		if (items_count == 0) return;
		
		/* Output generics */
		result.push("<");
		for (int i=0; i<items_count; i++)
		{
			OpTypeIdentifier op_code_item = items.get(i);
			if (op_code_item instanceof OpIdentifier)
			{
				this.OpIdentifier(op_code_item, result);
			}
			else if (op_code_item instanceof OpTypeIdentifier)
			{
				this.OpTypeIdentifier(op_code_item, result);
			}
			if (i < items_count - 1) result.push(", ");
		}
		result.push(">");
	}
	
	
	/**
	 * OpTypeIdentifier
	 */
	void OpTypeIdentifier(OpTypeIdentifier op_code, Collection<string> result)
	{
		OpIdentifier pattern = op_code.entity_name.items.last();
		result.push(pattern.value);
		this.OpCodeGenerics(op_code.generics, result);
	}
	
	
	/**
	 * OpCollection
	 */
	void OpCollection(OpCollection op_code, Collection<string> result)
	{
		bool is_multiline = op_code.isMultiLine();
		result.push("[");
		if (is_multiline)
		{
			this.translator.levelInc();
		}
		int i = 0;
		int values_count = op_code.values.count();
		while (i < values_count)
		{
			BaseOpCode op_code_item = op_code.values.get(i);
			if (is_multiline)
			{
				result.push(this.translator.newLine());
			}
			
			/* Preprocessor */
			if (op_code_item instanceof OpPreprocessorIfDef)
			{
				Collection<OpPreprocessorIfDef> items = [];
				string condition = op_code_item.condition.value;
				while (
					op_code_item != null and
					op_code_item instanceof OpPreprocessorIfDef and
					op_code_item.condition.value == condition
				)
				{
					items.push(op_code_item);
					
					/* Get next item */
					i++;
					op_code_item = op_code.values.get(i);
				}
				this.OpPreprocessorCollection(items, result);
				continue;
			}
			
			/* Translate item */
			this.translate(op_code_item, result);
			if (not(op_code_item instanceof OpPreprocessorIfDef))
			{
				if (is_multiline) result.push(",");
				else if (i < values_count - 1) result.push(", ");
			}
			
			i++;
		}
		if (is_multiline)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
		}
		result.push("]");
	}
	
	
	/**
	 * Collection preprocessor
	 */
	void OpPreprocessorCollection(Collection<OpPreprocessorIfDef> items, Collection<string> result)
	{
		string condition = items.get(0).condition.value;
		result.push("#ifdef " ~ condition ~ " then");
		for (int i=0; i<items.count(); i++)
		{
			BaseOpCode op_code_item = items.get(i);
			result.push(this.translator.newLine());
			this.translate(op_code_item.items, result);
			result.push(",");
		}
		result.push(this.translator.newLine());
		result.push("#endif");
	}
	
	
	/**
	 * OpDict
	 */
	void OpDict(OpCollection op_code, Collection<string> result)
	{
		bool is_multiline = op_code.isMultiLine();
		if (op_code.values.count() == 0 and not is_multiline)
		{
			result.push("{");
			result.push("}");
			return;
		}
		
		/* Begin bracket */
		result.push("{");
		if (is_multiline)
		{
			this.translator.levelInc();
		}
		
		/* Items */
		int i = 0;
		int values_count = op_code.values.count();
		while (i < values_count)
		{
			OpDictPair op_code_item = op_code.values.get(i);
			if (is_multiline)
			{
				result.push(this.translator.newLine());
			}
			
			/* Preprocessor */
			if (op_code_item.condition != null)
			{
				Collection<OpPreprocessorIfDef> items = [];
				string condition = op_code_item.condition.value;
				while (
					op_code_item != null and
					op_code_item.condition != null
				)
				{
					items.push(op_code_item);
					
					/* Get next item */
					i++;
					op_code_item = op_code.values.get(i);
				}
				this.OpPreprocessorDict(items, result);
				continue;
			}
			
			/* Translate item */
			result.push(this.translator.toString(op_code_item.key));
			result.push(": ");
			this.translate(op_code_item.value, result);
			if (is_multiline) result.push(",");
			else if (i < values_count - 1) result.push(", ");
			
			i++;
		}
		
		/* End bracket */
		if (is_multiline)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
		}
		result.push("}");
	}
	
	
	/**
	 * Dict preprocessor
	 */
	void OpPreprocessorDict(Collection<OpPreprocessorIfDef> items, Collection<string> result)
	{
		string condition = items.get(0).condition.value;
		result.push("#ifdef " ~ condition ~ " then");
		for (int i=0; i<items.count(); i++)
		{
			OpDictPair op_code_item = items.get(i);
			result.push(this.translator.newLine());
			result.push(this.translator.toString(op_code_item.key));
			result.push(": ");
			this.translate(op_code_item.value, result);
			result.push(",");
		}
		result.push(this.translator.newLine());
		result.push("#endif");
	}
	
	
	/**
	 * OpAttr
	 */
	void OpAttr(OpAttr op_code, Collection<string> result)
	{
		Vector<BaseOpCode> attrs = new Vector();
		
		BaseOpCode op_code_first = op_code;
		while (op_code_first instanceof OpAttr)
		{
			attrs.push(op_code_first);
			op_code_first = op_code_first.obj;
		}
		
		attrs = attrs.reverse();
		
		/* first op_code */
		this.translateItem(op_code_first, result);
		
		/* Attrs */
		for (int i=0; i<attrs.count(); i++)
		{
			OpAttr item_attr = attrs.get(i);
			if (item_attr.kind == OpAttr::KIND_ATTR)
			{
				result.push(".");
				result.push(item_attr.value.value);
			}
			else if (item_attr.kind == OpAttr::KIND_STATIC)
			{
				result.push("::");
				result.push(item_attr.value.value);
			}
			else if (item_attr.kind == OpAttr::KIND_DYNAMIC)
			{
				result.push("[");
				this.translate(item_attr.value, result);
				result.push("]");
			}
			else if (item_attr.kind == OpAttr::KIND_DYNAMIC_ATTRS)
			{
				result.push("[");
				int item_attr_count = item_attr.attrs.count();
				for (int j=0; j<item_attr_count; j++)
				{
					BaseOpCode op_code_item = item_attr.attrs.get(j);
					this.translate(op_code_item, result);
					if (j < item_attr_count - 1) result.push(", ");
				}
				result.push("]");
			}
		}
		
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpClassOf
	 */
	void OpClassOf(OpCall op_code, Collection<string> result)
	{
		result.push("classof ");
		result.push(op_code.entity_name.names.last());
	}
	
	
	/**
	 * OpCall
	 */
	void OpCall(OpCall op_code, Collection<string> result)
	{
		this.translateItem(op_code.obj, result);
		
		if (op_code.args.count() == 1 and op_code.args.get(0) instanceof OpDict)
		{
			this.OpDict(op_code.args.get(0), result);
		}
		else
		{
			result.push("(");
			int args_count = op_code.args.count();
			for (int i=0; i<args_count; i++)
			{
				BaseOpCode op_code_item = op_code.args.get(i);
				this.Expression(op_code_item, result);
				if (i < args_count - 1) result.push(", ");
			}
			result.push(")");
		}
		
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpNew
	 */
	void OpNew(OpNew op_code, Collection<string> result)
	{
		result.push("new ");
		this.OpTypeIdentifier(op_code.value, result);
		
		if (op_code.args.count() == 1 and op_code.args.get(0) instanceof OpDict)
		{
			this.OpDict(op_code.args.get(0), result);
		}
		else
		{
			result.push("(");
			int args_count = op_code.args.count();
			for (int i=0; i<args_count; i++)
			{
				BaseOpCode op_code_item = op_code.args.get(i);
				this.Expression(op_code_item, result);
				if (i < args_count - 1) result.push(", ");
			}
			result.push(")");
		}
		
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpMath
	 */
	void OpMath(OpCall op_code, Collection<string> result)
	{
		Collection<string> result1 = [];
		this.Expression(op_code.value1, result1);
		int opcode_level1 = this.translator.opcode_level;
		
		string op = "";
		int opcode_level = 0;
		if (op_code.math == "!") { opcode_level = 16; op = "!"; }
		if (op_code.math == ">>") { opcode_level = 12; op = ">>"; }
		if (op_code.math == "<<") { opcode_level = 12; op = "<<"; }
		if (op_code.math == "&") { opcode_level = 9; op = "&"; }
		if (op_code.math == "xor") { opcode_level = 8; op = "^"; }
		if (op_code.math == "|") { opcode_level = 7; op = "|"; }
		if (op_code.math == "*") { opcode_level = 14; op = "*"; }
		if (op_code.math == "/") { opcode_level = 14; op = "/"; }
		if (op_code.math == "%") { opcode_level = 14; op = "%"; }
		if (op_code.math == "div") { opcode_level = 14; op = "div"; }
		if (op_code.math == "mod") { opcode_level = 14; op = "mod"; }
		if (op_code.math == "+") { opcode_level = 13; op = "+"; }
		if (op_code.math == "-") { opcode_level = 13; op = "-"; }
		if (op_code.math == "~") { opcode_level = 13; op = "~"; }
		if (op_code.math == "===") { opcode_level = 10; op = "==="; }
		if (op_code.math == "!==") { opcode_level = 10; op = "!=="; }
		if (op_code.math == "==") { opcode_level = 10; op = "=="; }
		if (op_code.math == "!=") { opcode_level = 10; op = "!="; }
		if (op_code.math == ">=") { opcode_level = 10; op = ">="; }
		if (op_code.math == "<=") { opcode_level = 10; op = "<="; }
		if (op_code.math == ">") { opcode_level = 10; op = ">"; }
		if (op_code.math == "<") { opcode_level = 10; op = "<"; }
		if (op_code.math == "is") { opcode_level = 10; op = "instanceof"; }
		if (op_code.math == "instanceof") { opcode_level = 10; op = "instanceof"; }
		if (op_code.math == "implements") { opcode_level = 10; op = "implements"; }
		if (op_code.math == "bitnot") { opcode_level = 16; op = "bitnot"; }
		if (op_code.math == "not") { opcode_level = 16; op = "!"; }
		if (op_code.math == "and") { opcode_level = 6; op = "and"; }
		if (op_code.math == "&&") { opcode_level = 6; op = "and"; }
		if (op_code.math == "or") { opcode_level = 5; op = "or"; }
		if (op_code.math == "||") { opcode_level = 5; op = "or"; }
		
		if (op_code.math == "not" or op_code.math == "bitnot")
		{
			if (op_code.math == "bitnot") result.push("~");
			else result.push(op);
			if (opcode_level1 < opcode_level)
			{
				result.push("(");
				result.appendItems(result1);
				result.push(")");
			}
			else
			{
				result.appendItems(result1);
			}
		}
		else
		{
			if (opcode_level1 < opcode_level)
			{
				result.push("(");
				result.appendItems(result1);
				result.push(")");
			}
			else
			{
				result.appendItems(result1);
			}
			
			result.push(" " ~ op ~ " ");
			
			Collection<string> result2 = [];
			this.Expression(op_code.value2, result2);
			int opcode_level2 = this.translator.opcode_level;
			
			if (opcode_level2 < opcode_level)
			{
				result.push("(");
				result.appendItems(result2);
				result.push(")");
			}
			else
			{
				result.appendItems(result2);
			}
		}
		
		this.translator.opcode_level = opcode_level;
	}
	
	
	/**
	 * Translate item
	 */
	void translateItem(OpCall op_code, Collection<string> result)
	{
		if (op_code instanceof OpNumber)
		{
			this.OpNumber(op_code, result);
		}
		if (op_code instanceof OpString)
		{
			this.OpString(op_code, result);
		}
		else if (op_code instanceof OpIdentifier)
		{
			this.OpIdentifier(op_code, result);
		}
		else if (op_code instanceof OpAttr)
		{
			this.OpAttr(op_code, result);
		}
		else if (op_code instanceof OpClassOf)
		{
			this.OpClassOf(op_code, result);
		}
		else if (op_code instanceof OpCollection)
		{
			this.OpCollection(op_code, result);
		}
		else if (op_code instanceof OpDict)
		{
			this.OpDict(op_code, result);
		}
		else if (op_code instanceof OpDeclareFunction)
		{
			this.translator.program.OpDeclareFunction(op_code, result);
		}
		else if (op_code instanceof OpCall)
		{
			this.OpCall(op_code, result);
		}
		else if (op_code instanceof OpNew)
		{
			this.OpNew(op_code, result);
		}
	}
	
	
	/**
	 * Expression
	 */
	void Expression(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpMath)
		{
			this.OpMath(op_code, result);
		}
		else
		{
			this.translateItem(op_code, result);
		}
	}
	
	
	/**
	 * Translate expression
	 */
	void translate(BaseOpCode op_code, Collection<string> result)
	{
		this.Expression(op_code, result);
	}
}