/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangPHP;

use Runtime.re;
use Runtime.BaseObject;
use BayLang.SaveOpCode;
use BayLang.LangPHP.TranslatorPHP;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAttr;
use BayLang.OpCodes.OpCall;
use BayLang.OpCodes.OpClassOf;
use BayLang.OpCodes.OpCollection;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpDict;
use BayLang.OpCodes.OpDictPair;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpMath;
use BayLang.OpCodes.OpNew;
use BayLang.OpCodes.OpNumber;
use BayLang.OpCodes.OpPreprocessorIfCode;
use BayLang.OpCodes.OpPreprocessorIfDef;
use BayLang.OpCodes.OpPreprocessorSwitch;
use BayLang.OpCodes.OpString;
use BayLang.OpCodes.OpTernary;
use BayLang.OpCodes.OpTypeIdentifier;


class TranslatorPHPExpression extends BaseObject
{
	TranslatorPHP translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorPHP translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpIdentifier
	 */
	void OpIdentifier(OpIdentifier op_code, Collection<string> result, bool is_const)
	{
		Collection variables = ["null", "static", "true", "false"];
		if (op_code.value == "print") result.push("echo");
		else if (op_code.value == "var_dump") result.push("var_dump");
		else if (variables.indexOf(op_code.value) >= 0) result.push(op_code.value);
		else if (op_code.value == "this")
		{
			if (this.translator.class_function and this.translator.class_function.isStatic())
			{
				result.push("static");
			}
			else
			{
				result.push("$this");
			}
		}
		else if (op_code.value == "parent")
		{
			result.push("parent");
		}
		else if (op_code.value == "@")
		{
			result.push("\\Runtime\\rtl::getContext()");
		}
		else
		{
			if (this.translator.uses.has(op_code.value))
			{
				string class_name = this.translator.uses.get(op_code.value);
				if (class_name == this.translator.current_class_name) result.push("static");
				else result.push("\\" ~ this.translator.getModuleName(class_name));
			}
			else result.push(not is_const ? "$" ~ op_code.value : op_code.value);
		}
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpNumber
	 */
	void OpNumber(OpNumber op_code, Collection<string> result)
	{
		result.push(op_code.value);
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpString
	 */
	void OpString(OpString op_code, Collection<string> result)
	{
		result.push(this.translator.toString(op_code.value));
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpCode generics
	 */
	void OpCodeGenerics(Collection<OpTypeIdentifier> items, Collection<string> result)
	{
		if (not items) return;
		
		/* Get items count */
		int items_count = items.count();
		if (items_count == 0) return;
		
		/* Output generics */
		result.push("<");
		for (int i=0; i<items_count; i++)
		{
			OpTypeIdentifier op_code_item = items.get(i);
			if (op_code_item instanceof OpIdentifier)
			{
				this.OpIdentifier(op_code_item, result);
			}
			else if (op_code_item instanceof OpTypeIdentifier)
			{
				this.OpTypeIdentifier(op_code_item, result);
			}
			if (i < items_count - 1) result.push(", ");
		}
		result.push(">");
	}
	
	
	/**
	 * OpTypeIdentifier
	 */
	void OpTypeIdentifier(OpTypeIdentifier op_code, Collection<string> result)
	{
		string name = op_code.entity_name.getName();
		if (this.translator.uses.has(name))
		{
			result.push("\\" ~ this.translator.getModuleName(this.translator.uses.get(name)));
		}
		else
		{
			OpIdentifier pattern = op_code.entity_name.items.last();
			result.push(pattern.value);
		}
	}
	
	
	/**
	 * OpCollection
	 */
	void OpCollection(OpCollection op_code, Collection<string> result)
	{
		bool is_multiline = op_code.isMultiLine();
		result.push("new \\Runtime\\Vector(");
		if (is_multiline)
		{
			this.translator.levelInc();
		}
		int i = 0;
		int items_count = op_code.items.count();
		bool last_result = true;
		while (i < items_count)
		{
			BaseOpCode op_code_item = op_code.items.get(i);
			Collection result1 = [];
			
			/* Preprocessor */
			bool is_result = false;
			bool is_preprocessor = true;
			if (op_code_item instanceof OpPreprocessorIfCode)
			{
				is_result = this.translator.program.OpPreprocessorIfCode(op_code_item, result1);
			}
			else if (op_code_item instanceof OpPreprocessorIfDef)
			{
				is_result = this.translator.program.OpPreprocessorIfDef(
					op_code_item, result1, OpPreprocessorIfDef::KIND_COLLECTION
				);
			}
			else if (op_code_item instanceof OpPreprocessorSwitch)
			{
				is_result = this.translator.program.OpPreprocessorSwitch(
					op_code_item, result1, OpPreprocessorSwitch::KIND_COLLECTION
				);
			}
			
			/* Translate item */
			else
			{
				is_preprocessor = false;
				this.translate(op_code_item, result1);
			}
			
			last_result = not is_preprocessor or is_result;
			if (last_result)
			{
				if (is_multiline) result.push(this.translator.newLine());
				result.appendItems(result1);
				if (is_multiline) result.push(",");
				else if (i < items_count - 1) result.push(", ");
			}
			
			i++;
		}
		if (is_multiline)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
		}
		result.push(")");
	}
	
	
	/**
	 * OpDict
	 */
	void OpDict(OpDict op_code, Collection<string> result)
	{
		bool is_multiline = op_code.isMultiLine();
		if (op_code.items.count() == 0 and not is_multiline)
		{
			result.push("new \\Runtime\\Map()");
			return;
		}
		
		/* Begin bracket */
		result.push("new \\Runtime\\Map([");
		if (is_multiline)
		{
			this.translator.levelInc();
		}
		
		/* Items */
		int i = 0;
		int items_count = op_code.items.count();
		while (i < items_count)
		{
			OpDictPair op_code_item = op_code.items.get(i);
			
			/* Preprocessor */
			if (op_code_item.condition != null)
			{
				string name = op_code_item.condition.value;
				if (not this.translator.preprocessor_flags.has(name))
				{
					i++;
					continue;
				}
			}
			
			/* Add new line */
			if (is_multiline)
			{
				result.push(this.translator.newLine());
			}
			
			/* Translate item */
			result.push(this.translator.toString(op_code_item.key.value));
			result.push(" => ");
			this.translate(op_code_item.expression, result);
			if (is_multiline) result.push(",");
			else if (i < items_count - 1) result.push(", ");
			
			i++;
		}
		
		/* End bracket */
		if (is_multiline)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
		}
		result.push("])");
	}
	
	
	/**
	 * OpAttr
	 */
	void OpAttr(OpAttr op_code, Collection<string> result)
	{
		Vector<BaseOpCode> attrs = new Vector();
		
		BaseOpCode op_code_first = op_code;
		while (op_code_first instanceof OpAttr)
		{
			attrs.push(op_code_first);
			op_code_first = op_code_first.prev;
		}
		
		attrs.reverse();
		
		/* First op_code */
		this.translateItem(op_code_first, result);
		
		/* Attrs */
		for (int i=0; i<attrs.count(); i++)
		{
			OpAttr item_attr = attrs.get(i);
			if (item_attr.kind == OpAttr::KIND_ATTR)
			{
				if (this.translator.class_function.isStatic() and
					item_attr.prev instanceof OpIdentifier and
					item_attr.prev.value == "this"
				)
				{
					result.push("::");
				}
				else result.push("->");
				result.push(item_attr.next.value);
			}
			else if (item_attr.kind == OpAttr::KIND_STATIC)
			{
				result.push("::");
				result.push(item_attr.next.value);
			}
			else if (item_attr.kind == OpAttr::KIND_DYNAMIC)
			{
				result.push("[");
				for (int j=0; j<item_attr.next.items.count(); j++)
				{
					this.translate(item_attr.next.items.get(j), result);
					if (j < item_attr.next.items.count() - 1) result.push(", ");
				}
				result.push("]");
			}
		}
		
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpClassOf
	 */
	void OpClassOf(OpClassOf op_code, Collection<string> result)
	{
		if (op_code.entity_name.items.count() == 1)
		{
			OpIdentifier item = op_code.entity_name.items.last();
			string name = item.value;
			if (this.translator.uses.has(name))
			{
				result.push(this.translator.toString(this.translator.uses.get(name)));
			}
			else
			{
				result.push(this.translator.toString(name));
			}
		}
		else
		{
			result.push(this.translator.toString(op_code.entity_name.getName()));
		}
	}
	
	
	/**
	 * OpCall
	 */
	void OpCall(OpCall op_code, Collection<string> result)
	{
		if (op_code.item instanceof OpIdentifier and op_code.item.value == "parent")
		{
			if (this.translator.class_function)
			{
				string name = this.translator.class_function.name;
				if (name == "constructor") name = "__construct";
				result.push("parent::" ~ name);
			}
			else
			{
				result.push("parent");
			}
		}
		else
		{
			this.translateItem(op_code.item, result);
		}
		
		result.push("(");
		int args_count = op_code.args.count();
		for (int i=0; i<args_count; i++)
		{
			BaseOpCode op_code_item = op_code.args.get(i);
			this.Expression(op_code_item, result);
			if (i < args_count - 1) result.push(", ");
		}
		result.push(")");
		
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpNew
	 */
	void OpNew(OpNew op_code, Collection<string> result)
	{
		result.push("new ");
		this.OpTypeIdentifier(op_code.pattern, result);
		
		result.push("(");
		int args_count = op_code.args.count();
		for (int i=0; i<args_count; i++)
		{
			BaseOpCode op_code_item = op_code.args.get(i);
			this.Expression(op_code_item, result);
			if (i < args_count - 1) result.push(", ");
		}
		result.push(")");
		
		this.translator.opcode_level = 20;
	}
	
	
	/**
	 * OpMath
	 */
	void OpMath(OpMath op_code, Collection<string> result)
	{
		Collection<string> result1 = [];
		this.Expression(op_code.value1, result1);
		int opcode_level1 = this.translator.opcode_level;
		
		string op = "";
		int opcode_level = 0;
		if (op_code.math == "!") { opcode_level = 16; op = "!"; }
		if (op_code.math == ">>") { opcode_level = 12; op = ">>"; }
		if (op_code.math == "<<") { opcode_level = 12; op = "<<"; }
		if (op_code.math == "&") { opcode_level = 9; op = "&"; }
		if (op_code.math == "xor") { opcode_level = 8; op = "^"; }
		if (op_code.math == "|") { opcode_level = 7; op = "|"; }
		if (op_code.math == "*") { opcode_level = 14; op = "*"; }
		if (op_code.math == "/") { opcode_level = 14; op = "/"; }
		if (op_code.math == "%") { opcode_level = 14; op = "%"; }
		if (op_code.math == "div") { opcode_level = 14; op = "div"; }
		if (op_code.math == "mod") { opcode_level = 14; op = "mod"; }
		if (op_code.math == "+") { opcode_level = 13; op = "+"; }
		if (op_code.math == "-") { opcode_level = 13; op = "-"; }
		if (op_code.math == "~") { opcode_level = 13; op = "~"; }
		if (op_code.math == "===") { opcode_level = 10; op = "==="; }
		if (op_code.math == "!==") { opcode_level = 10; op = "!=="; }
		if (op_code.math == "==") { opcode_level = 10; op = "=="; }
		if (op_code.math == "!=") { opcode_level = 10; op = "!="; }
		if (op_code.math == ">=") { opcode_level = 10; op = ">="; }
		if (op_code.math == "<=") { opcode_level = 10; op = "<="; }
		if (op_code.math == ">") { opcode_level = 10; op = ">"; }
		if (op_code.math == "<") { opcode_level = 10; op = "<"; }
		if (op_code.math == "is") { opcode_level = 10; op = "instanceof"; }
		if (op_code.math == "instanceof") { opcode_level = 10; op = "instanceof"; }
		if (op_code.math == "implements") { opcode_level = 10; op = "instanceof"; }
		if (op_code.math == "bitnot") { opcode_level = 16; op = "bitnot"; }
		if (op_code.math == "neg") { opcode_level = 16; op = "neg"; }
		if (op_code.math == "not") { opcode_level = 16; op = "!"; }
		if (op_code.math == "and" or op_code.math == "&&") { opcode_level = 6; op = "&&"; }
		if (op_code.math == "or" or op_code.math == "||") { opcode_level = 5; op = "||"; }
		
		if (op_code.math == "neg" or op_code.math == "not" or op_code.math == "bitnot")
		{
			if (op_code.math == "bitnot") result.push("~");
			else if (op_code.math == "neg") result.push("-");
			else result.push(op);
			if (opcode_level1 < opcode_level)
			{
				result.push("(");
				result.appendItems(result1);
				result.push(")");
			}
			else
			{
				result.appendItems(result1);
			}
		}
		else
		{
			if (opcode_level1 < opcode_level)
			{
				result.push("(");
				result.appendItems(result1);
				result.push(")");
			}
			else
			{
				result.appendItems(result1);
			}
			
			if (op == "~") op = ".";
			result.push(" " ~ op ~ " ");
			
			Collection<string> result2 = [];
			this.Expression(op_code.value2, result2);
			int opcode_level2 = this.translator.opcode_level;
			
			if (opcode_level2 < opcode_level)
			{
				result.push("(");
				result.appendItems(result2);
				result.push(")");
			}
			else
			{
				result.appendItems(result2);
			}
		}
		
		this.translator.opcode_level = opcode_level;
	}
	
	
	/**
	 * Translate ternary
	 */
	void OpTernary(BaseOpCode op_code, Collection<string> result)
	{
		Collection result1 = [];
		this.translate(op_code.condition, result1);
		if (this.translator.opcode_level < 9)
		{
			result.push("(");
			result.appendItems(result1);
			result.push(")");
		}
		else
		{
			result.appendItems(result1);
		}
		
		result1 = [];
		result.push(" ? ");
		this.translate(op_code.if_true, result1);
		if (this.translator.opcode_level < 9)
		{
			result.push("(");
			result.appendItems(result1);
			result.push(")");
		}
		else
		{
			result.appendItems(result1);
		}
		
		result1 = [];
		result.push(" : ");
		this.translate(op_code.if_false, result1);
		if (this.translator.opcode_level < 9)
		{
			result.push("(");
			result.appendItems(result1);
			result.push(")");
		}
		else
		{
			result.appendItems(result1);
		}
		
		this.translator.opcode_level = 0;
	}
	
	
	/**
	 * Translate item
	 */
	void translateItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpNumber)
		{
			this.OpNumber(op_code, result);
		}
		if (op_code instanceof OpString)
		{
			this.OpString(op_code, result);
		}
		else if (op_code instanceof OpIdentifier)
		{
			this.OpIdentifier(op_code, result);
		}
		else if (op_code instanceof OpAttr)
		{
			this.OpAttr(op_code, result);
		}
		else if (op_code instanceof OpClassOf)
		{
			this.OpClassOf(op_code, result);
		}
		else if (op_code instanceof OpCollection)
		{
			this.OpCollection(op_code, result);
		}
		else if (op_code instanceof OpDict)
		{
			this.OpDict(op_code, result);
		}
		else if (op_code instanceof OpDeclareFunction)
		{
			this.translator.program.OpDeclareFunction(op_code, result);
		}
		else if (op_code instanceof OpCall)
		{
			this.OpCall(op_code, result);
		}
		else if (op_code instanceof OpNew)
		{
			this.OpNew(op_code, result);
		}
		else if (op_code instanceof OpTernary)
		{
			this.OpTernary(op_code, result);
		}
		else if (op_code instanceof OpTypeIdentifier)
		{
			this.OpTypeIdentifier(op_code, result);
		}
	}
	
	
	/**
	 * Expression
	 */
	void Expression(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpMath)
		{
			this.OpMath(op_code, result);
		}
		else
		{
			this.translateItem(op_code, result);
		}
	}
	
	
	/**
	 * Translate expression
	 */
	void translate(BaseOpCode op_code, Collection<string> result)
	{
		this.Expression(op_code, result);
	}
}