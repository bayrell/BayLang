/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangPHP;

use Runtime.BaseObject;
use BayLang.LangBay.ParserBayHtml;
use BayLang.LangPHP.TranslatorPHP;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAssign;
use BayLang.OpCodes.OpComment;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpFor;
use BayLang.OpCodes.OpHtmlAttribute;
use BayLang.OpCodes.OpHtmlContent;
use BayLang.OpCodes.OpHtmlItems;
use BayLang.OpCodes.OpHtmlSlot;
use BayLang.OpCodes.OpHtmlStyle;
use BayLang.OpCodes.OpHtmlTag;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpIf;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpUse;


class TranslatorPHPHtml extends BaseObject
{
	TranslatorPHP translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorPHP translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpHtmlContent
	 */
	void OpHtmlContent(OpHtmlContent op_code, Collection<string> result)
	{
		string var_name = this.translator.html_var_names.last();
		result.push(var_name ~ "->push(" ~ this.translator.toString(op_code.value) ~ ");");
	}
	
	
	/**
	 * OpHtmlExpression
	 */
	void OpHtmlExpression(BaseOpCode op_code, Collection<string> result)
	{
		string var_name = this.translator.html_var_names.last();
		result.push(var_name ~ "->push(");
		this.translator.expression.translate(op_code, result);
		result.push(");");
	}
	
	
	/**
	 * OpHtmlSlot
	 */
	void OpHtmlSlot(BaseOpCode op_code, Collection<string> result)
	{
		string var_name = this.translator.html_var_names.last();
		result.push("/* Slot " ~ op_code.name ~ " */");
		result.push(this.translator.newLine());
		result.push(var_name ~ "->slot(");
		result.push(this.translator.toString(op_code.name) ~ ", ");
		this.translator.program.OpDeclareFunction(op_code, result, false);
		result.push(");");
	}
	
	
	/**
	 * OpHtmlAttrs
	 */
	Collection OpHtmlAttrs(OpHtmlTag op_code)
	{
		Collection<OpHtmlAttribute> attrs = op_code.attrs;
		
		Vector spread = [];
		Map result = {};
		for (int i=0; i<attrs.count(); i++)
		{
			OpHtmlAttribute item = attrs.get(i);
			if (item.is_spread)
			{
				Vector item_result = [];
				this.translator.expression.translate(item.expression, item_result);
				spread.push(rs::join("", item_result));
				continue;
			}
			
			string key = item.key;
			if (rs::charAt(key, 0) == "@" and key != "@raw") continue;
			Collection item_value = [];
			this.translator.expression.translate(item.expression, item_value);
			string value = rs::join("", item_value);
			if (result.has(key))
			{
				var arr = result.get(key);
				if (key == "class") arr.push(value);
				result.set(key, arr);
			}
			else
			{
				if (key == "class") value = [value];
				result.set(key, value);
			}
		}
		
		/* Add class name */
		Vector<string> class_name = result.get("class");
		if (class_name)
		{
			class_name.push("$componentHash");
		}
		
		/* Get attrs */
		Vector new_attrs = result.transition(string (string value, string key){
			if (key == "class")
			{
				return this.translator.toString(key) ~
					" => \\Runtime\\rs::className(new \\Runtime\\Vector(" ~
					rs::join(", ", value) ~ "))";
			}
			return this.translator.toString(key) ~ " => " ~ value;
		});
		return [new_attrs, spread];
	}
	
	
	/**
	 * Returns tag name
	 */
	string getTagName(var tag_name)
	{
		if (tag_name instanceof BaseOpCode)
		{
			Vector item_result = [];
			this.translator.expression.translate(tag_name, item_result);
			string value = rs::join("", item_result);
			return [value, value];
		}
		if (ParserBayHtml::isComponent(tag_name))
		{
			string module_name = this.translator.getUseModule(tag_name);
			return [module_name, this.translator.toString(module_name)];
		}
		return [tag_name, this.translator.toString(tag_name)];
	}
	
	
	/**
	 * OpHtmlTag
	 */
	void OpHtmlTag(OpHtmlTag op_code, Collection<string> result)
	{
		string var_name = this.translator.html_var_names.last();
		string current_var_name = "";
		
		Vector res = this.OpHtmlAttrs(op_code);
		Vector attrs = res.get(0);
		Vector spread = res.get(1);
		string attrs_str = "";
		if (attrs.count() > 0 or spread.count() > 0)
		{
			attrs_str = ", (new \\Runtime\\Map([" ~ rs::join(", ", attrs) ~ "]))";
			for (int i=0; i<spread.count(); i++)
			{
				attrs_str = attrs_str ~ "->concat(" ~ spread.get(i) ~ ")";
			}
		}
		
		string tag_name = this.getTagName(op_code.tag_name);
		result.push("/* Element " ~ tag_name.get(0) ~ " */");
		result.push(this.translator.newLine());
		if (op_code.content and op_code.content.count() > 0)
		{
			current_var_name = "$" ~ this.translator.varInc();
			result.push(current_var_name ~ " = ");
		}
		result.push(var_name ~ "->element(" ~ tag_name.get(1) ~ attrs_str ~ ");");
		
		if (op_code.content and op_code.content.count() > 0)
		{
			this.translator.html_var_names.push(current_var_name);
			bool is_slot = op_code.content.items
				.filter(bool (BaseOpCode item) => item instanceof OpHtmlSlot)
				.count() == op_code.content.count()
			;
			if (ParserBayHtml::isComponent(op_code.tag_name) and not is_slot)
			{
				OpDeclareFunction op_code_item = new OpDeclareFunction
				{
					"is_html": true,
					"content": op_code.content,
					"vars": op_code.vars,
				};
				result.push(this.translator.newLine());
				result.push(this.translator.newLine());
				result.push("/* Content */");
				result.push(this.translator.newLine());
				result.push(current_var_name ~ "->slot(\"default\", ");
				this.translator.program.OpDeclareFunction(op_code_item, result);
				result.push(");");
			}
			else
			{
				this.OpHtmlItems(op_code.content, result, true);
			}
			this.translator.html_var_names.pop();
		}
	}
	
	
	/**
	 * Translate item
	 */
	void translateItem(BaseOpCode op_code, Collection<string> result, bool new_line = false)
	{
		if (op_code instanceof OpHtmlContent)
		{
			this.OpHtmlContent(op_code, result);
		}
		else if (op_code instanceof OpHtmlSlot)
		{
			if (new_line) result.push(this.translator.newLine());
			this.OpHtmlSlot(op_code, result);
		}
		else if (op_code instanceof OpHtmlTag)
		{
			if (new_line) result.push(this.translator.newLine());
			this.OpHtmlTag(op_code, result);
		}
		else if (op_code instanceof OpAssign)
		{
			if (new_line) result.push(this.translator.newLine());
			this.translator.operator.OpAssign(op_code, result);
			result.push(";");
		}
		else if (op_code instanceof OpFor)
		{
			if (new_line) result.push(this.translator.newLine());
			this.translator.operator.OpFor(op_code, result);
		}
		else if (op_code instanceof OpIf)
		{
			if (new_line) result.push(this.translator.newLine());
			this.translator.operator.OpIf(op_code, result);
		}
		else if (op_code instanceof OpComment)
		{
			this.translator.operator.OpComment(op_code, result);
		}
		else
		{
			this.OpHtmlExpression(op_code, result);
		}
	}
	
	
	/**
	 * OpHtmlItems
	 */
	void OpHtmlItems(OpHtmlItems op_code, Collection<string> result, bool new_line = false)
	{
		if (not op_code) return;
		BaseOpCode prev_op_code = null;
		for (int i=0; i<op_code.count(); i++)
		{
			BaseOpCode op_code_item = op_code.get(i);
			result.push(this.translator.newLine());
			bool next_line = true;
			if (prev_op_code == null) next_line = new_line;
			else if (prev_op_code instanceof OpAssign and (
				op_code_item instanceof OpAssign or
				op_code_item instanceof OpIf or op_code_item instanceof OpFor
			) or prev_op_code instanceof OpComment)
			{
				next_line = false;
			}
			this.translateItem(op_code_item, result, next_line);
			prev_op_code = op_code_item;
		}
	}
	
	
	/**
	 * Translate OpItems
	 */
	void translateItems(OpItems op_code, Collection<string> result, bool match_brackets = true)
	{
		result.push(this.translator.newLine());
		result.push("{");
		this.translator.levelInc();
		
		/* Add component hash */
		result.push(this.translator.newLine());
		result.push("$componentHash = \\Runtime\\rs::getComponentHash(static::getClassName());");
		
		/* Create Virtual Dom */
		result.push(this.translator.newLine());
		result.push("$__v = new \\Runtime\\VirtualDom($this);");
		result.push(this.translator.newLine());
		
		/* Add is render */
		if (this.translator.current_function.name == "render")
		{
			result.push("$__v->is_render = true;");
			result.push(this.translator.newLine());
		}
		
		/* Save old var names */
		int old_var_inc = this.translator.var_inc;
		this.translator.var_inc = 0;
		Collection old_var_names = this.translator.html_var_names.slice();
		this.translator.html_var_names = [];
		this.translator.html_var_names.push("$__v");
		
		/* Translate HTML items */
		if (op_code.items.count() == 1 and op_code.get(0) instanceof OpHtmlContent)
		{
			this.OpHtmlContent(op_code.get(0), result);
		}
		else if (op_code.items.count() > 0)
		{
			this.OpHtmlItems(op_code, result);
			result.push(this.translator.newLine());
		}
		
		/* Restore */
		this.translator.var_inc = old_var_inc;
		this.translator.html_var_names = old_var_names;
		
		/* Return Virtual Dom */
		if (op_code.items.count() > 0)
		{
			result.push(this.translator.newLine());
		}
		result.push("return $__v;");
		
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push("}");
	}
	
	
	/**
	 * Translate component style
	 */
	void translateComponentStyle(OpDeclareClass op_code, Collection<string> result)
	{
		Collection<OpHtmlStyle> items = op_code.content.items.filter(
			bool (BaseOpCode item) => item instanceof OpHtmlStyle
		);
		
		Collection<string> css_content = [];
		for (int i=0; i<items.count(); i++)
		{
			OpHtmlStyle op_code_item = items.get(i);
			this.translator.style.OpHtmlStyle(op_code_item, css_content);
		}
		
		result.push(this.translator.newLine());
		result.push("static function getComponentStyle(){ ");
		result.push("return " ~ this.translator.toString(rs::join("", css_content)) ~ "; ");
		result.push("}");
	}
	
	
	/**
	 * Translate module components
	 */
	void translateModuleComponents(OpDeclareClass op_code, Collection<string> result)
	{
		Vector components = [];
		for (int i=0; i<this.translator.current_module.items.count(); i++)
		{
			BaseOpCode op_code_item = this.translator.current_module.items.get(i);
			if (op_code_item instanceof OpUse and op_code_item.is_component)
			{
				components.push(op_code_item.name);
			}
		}
		components = components.map(string (string name) => this.translator.toString(name));
		result.push(this.translator.newLine());
		result.push("static function getRequiredComponents(){ ");
		result.push("return new \\Runtime\\Vector(" ~ rs::join(", ", components) ~ "); ");
		result.push("}");
	}
}