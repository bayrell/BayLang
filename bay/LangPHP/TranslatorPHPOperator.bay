/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2024 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangPHP;

use Runtime.BaseObject;
use BayLang.SaveOpCode;
use BayLang.LangPHP.TranslatorPHP;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAssign;
use BayLang.OpCodes.OpAssignValue;
use BayLang.OpCodes.OpBreak;
use BayLang.OpCodes.OpCall;
use BayLang.OpCodes.OpComment;
use BayLang.OpCodes.OpContinue;
use BayLang.OpCodes.OpFor;
use BayLang.OpCodes.OpIf;
use BayLang.OpCodes.OpIfElse;
use BayLang.OpCodes.OpInc;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpReturn;
use BayLang.OpCodes.OpThrow;
use BayLang.OpCodes.OpTryCatch;
use BayLang.OpCodes.OpTryCatchItem;
use BayLang.OpCodes.OpWhile;


class TranslatorPHPOperator extends BaseObject
{
	TranslatorPHP translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorPHP translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpAssign
	 */
	bool OpAssign(OpAssign op_code, Collection<string> result)
	{
		if (op_code.pattern) return false;
		
		int values_count = op_code.items.count();
		for (int i=0; i<values_count; i++)
		{
			OpAssignValue op_code_value = op_code.items.get(i);
			if (op_code.pattern or i > 0)
			{
				result.push(" ");
			}
			this.translator.expression.OpIdentifier(op_code_value.value, result);
			if (op_code_value.expression)
			{
				result.push(" = ");
				this.translator.expression.translate(op_code_value.expression, result);
			}
			if (i < values_count - 1) result.push(",");
		}
		
		return true;
	}
	
	
	/**
	 * OpBreak
	 */
	void OpBreak(OpBreak op_code, Collection<string> result)
	{
		result.push("break");
	}
	
	
	/**
	 * OpContinue
	 */
	void OpContinue(OpContinue op_code, Collection<string> result)
	{
		result.push("continue");
	}
	
	
	/**
	 * OpReturn
	 */
	void OpReturn(OpReturn op_code, Collection<string> result)
	{
		result.push("return");
		if (op_code.expression)
		{
			result.push(" ");
			this.translator.expression.translate(op_code.expression, result);
		}
	}
	
	
	/**
	 * OpInc
	 */
	void OpInc(OpInc op_code, Collection<string> result)
	{
		if (op_code.kind == OpInc::KIND_PRE_INC)
		{
			result.push("++");
			this.translator.expression.translate(op_code.value, result);
		}
		if (op_code.kind == OpInc::KIND_PRE_DEC)
		{
			result.push("--");
			this.translator.expression.translate(op_code.value, result);
		}
		if (op_code.kind == OpInc::KIND_POST_INC)
		{
			this.translator.expression.translate(op_code.value, result);
			result.push("++");
		}
		if (op_code.kind == OpInc::KIND_POST_DEC)
		{
			this.translator.expression.translate(op_code.value, result);
			result.push("--");
		}
	}
	
	
	/**
	 * OpFor
	 */
	void OpFor(OpFor op_code, Collection<string> result)
	{
		result.push("for (");
		this.translateItem(op_code.expr1, result);
		result.push("; ");
		this.translator.expression.translate(op_code.expr2, result);
		result.push("; ");
		this.translateItem(op_code.expr3, result);
		result.push(")");
		result.push(this.translator.newLine());
		this.translateItems(op_code.value, result);
	}
	
	
	/**
	 * OpIf
	 */
	void OpIf(OpIf op_code, Collection<string> result)
	{
		result.push("if (");
		this.translator.expression.translate(op_code.condition, result);
		result.push(")");
		result.push(this.translator.newLine());
		this.translateItems(op_code.if_true, result);
		if (op_code.if_else and op_code.if_else.count() > 0)
		{
			for (int i=0; i<op_code.if_else.count(); i++)
			{
				OpIfElse op_code_item = op_code.if_else.get(i);
				result.push(this.translator.newLine());
				result.push("else if (");
				this.translator.expression.translate(op_code_item.condition, result);
				result.push(")");
				result.push(this.translator.newLine());
				this.translateItems(op_code_item.if_true, result);
			}
		}
		if (op_code.if_false)
		{
			result.push(this.translator.newLine());
			result.push("else");
			result.push(this.translator.newLine());
			this.translateItems(op_code.if_false, result);
		}
	}
	
	
	/**
	 * OpThrow
	 */
	void OpThrow(OpThrow op_code, Collection<string> result)
	{
		result.push("throw ");
		this.translator.expression.translate(op_code.expression, result);
	}
	
	
	/**
	 * OpTryCatch
	 */
	void OpTryCatch(OpTryCatch op_code, Collection<string> result)
	{
		result.push("try");
		result.push(this.translator.newLine());
		this.translateItems(op_code.op_try, result);
		if (op_code.items and op_code.items.count() > 0)
		{
			int items_count = op_code.items.count();
			for (int i=0; i<items_count; i++)
			{
				OpTryCatchItem op_code_item = op_code.items.get(i);
				result.push(this.translator.newLine());
				result.push("catch (");
				this.translator.expression.OpTypeIdentifier(op_code_item.pattern, result);
				result.push(" ");
				result.push(op_code_item.name);
				result.push(")");
				result.push(this.translator.newLine());
				this.translateItems(op_code_item.value, result);
			}
		}
	}
	
	
	/**
	 * OpWhile
	 */
	void OpWhile(OpWhile op_code, Collection<string> result)
	{
		result.push("while (");
		this.translator.expression.translate(op_code.condition, result);
		result.push(")");
		result.push(this.translator.newLine());
		this.translateItems(op_code.value, result);
	}
	
	
	/**
	 * OpComment
	 */
	void OpComment(OpComment op_code, Collection<string> result)
	{
		result.push("/*");
		result.push(op_code.value);
		result.push("*/");
	}
	
	
	/**
	 * Translate item
	 */
	bool translateItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpAssign)
		{
			return this.OpAssign(op_code, result);
		}
		else if (op_code instanceof OpBreak)
		{
			this.OpBreak(op_code, result);
		}
		else if (op_code instanceof OpCall)
		{
			this.translator.expression.OpCall(op_code, result);
		}
		else if (op_code instanceof OpContinue)
		{
			this.OpContinue(op_code, result);
		}
		else if (op_code instanceof OpReturn)
		{
			this.OpReturn(op_code, result);
		}
		else if (op_code instanceof OpInc)
		{
			this.OpInc(op_code, result);
		}
		else if (op_code instanceof OpFor)
		{
			this.OpFor(op_code, result);
		}
		else if (op_code instanceof OpIf)
		{
			this.OpIf(op_code, result);
		}
		else if (op_code instanceof OpThrow)
		{
			this.OpThrow(op_code, result);
		}
		else if (op_code instanceof OpTryCatch)
		{
			this.OpTryCatch(op_code, result);
		}
		else if (op_code instanceof OpWhile)
		{
			this.OpWhile(op_code, result);
		}
		else if (op_code instanceof OpComment)
		{
			this.OpComment(op_code, result);
		}
		else
		{
			return false;
		}
		return true;
	}
	
	
	/**
	 * Translate OpItems
	 */
	void translateItems(OpItems op_code, Collection<string> result)
	{
		if (op_code.items.count() == 0)
		{
			result.push("{");
			result.push("}");
			return;
		}
		
		/* Begin bracket */
		result.push("{");
		this.translator.levelInc();
		
		/* Items */
		int items_count = op_code.items.count();
		for (int i=0; i<items_count; i++)
		{
			BaseOpCode op_code_item = op_code.items.get(i);
			Collection<string> result_items = [];
			bool flag = this.translateItem(op_code_item, result_items);
			if (flag)
			{
				result.push(this.translator.newLine());
				result.appendItems(result_items);
				if (
					op_code_item instanceof OpAssign or
					op_code_item instanceof OpBreak or
					op_code_item instanceof OpCall or
					op_code_item instanceof OpContinue or
					op_code_item instanceof OpInc or
					op_code_item instanceof OpReturn or
					op_code_item instanceof OpThrow
				)
				{
					result.push(";");
				}
			}
		}
		
		/* End bracket */
		this.translator.levelDec();
		result.push(this.translator.newLine());
		result.push("}");
	}
}