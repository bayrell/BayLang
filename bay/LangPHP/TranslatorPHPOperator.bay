/*!
 *  Bayrell Language
 *
 *  (c) Copyright 2016-2023 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Bayrell.Lang.LangPHP;

use Bayrell.Lang.SaveOpCode;
use Bayrell.Lang.LangPHP.TranslatorPHP;
use Bayrell.Lang.OpCodes.BaseOpCode;
use Bayrell.Lang.OpCodes.OpAssign;
use Bayrell.Lang.OpCodes.OpAssignStruct;
use Bayrell.Lang.OpCodes.OpAssignValue;
use Bayrell.Lang.OpCodes.OpAttr;
use Bayrell.Lang.OpCodes.OpBreak;
use Bayrell.Lang.OpCodes.OpCall;
use Bayrell.Lang.OpCodes.OpComment;
use Bayrell.Lang.OpCodes.OpContinue;
use Bayrell.Lang.OpCodes.OpDeclareFunction;
use Bayrell.Lang.OpCodes.OpDeclareFunctionArg;
use Bayrell.Lang.OpCodes.OpDelete;
use Bayrell.Lang.OpCodes.OpFor;
use Bayrell.Lang.OpCodes.OpHtmlItems;
use Bayrell.Lang.OpCodes.OpIdentifier;
use Bayrell.Lang.OpCodes.OpIf;
use Bayrell.Lang.OpCodes.OpIfElse;
use Bayrell.Lang.OpCodes.OpInc;
use Bayrell.Lang.OpCodes.OpItems;
use Bayrell.Lang.OpCodes.OpPipe;
use Bayrell.Lang.OpCodes.OpPreprocessorIfCode;
use Bayrell.Lang.OpCodes.OpPreprocessorIfDef;
use Bayrell.Lang.OpCodes.OpPreprocessorSwitch;
use Bayrell.Lang.OpCodes.OpReturn;
use Bayrell.Lang.OpCodes.OpSafe;
use Bayrell.Lang.OpCodes.OpThrow;
use Bayrell.Lang.OpCodes.OpTryCatch;
use Bayrell.Lang.OpCodes.OpTryCatchItem;
use Bayrell.Lang.OpCodes.OpWhile;


static class TranslatorPHPOperator
{
	
	/**
	 * OpAssign
	 */
	pure list<TranslatorPHP, string> OpAssignStruct(TranslatorPHP t, OpAssign op_code, int pos = 0)
	{
		string content = "";
		string var_name = op_code.var_name;
		list res = t.expression::Expression(t, op_code.expression); t = res[0]; string expr = res[1];
		
		Collection names = op_code.names.map
		(
			string (var item) use (t)
			{
				if (item instanceof BaseOpCode)
				{
					list res = t.expression::Expression(t, item); t = res[0];
					return res[1];
				}
				return t.expression::toString(item);
			}
		);
		
		content = "\\Runtime\\rtl::setAttr($ctx, $" ~ var_name ~
			", [" ~ rs::join(", ", names) ~ "], " ~ expr ~ ")";
		return [t, content];
	}
	
	
	
	/**
	 * OpAssign
	 */
	pure list<TranslatorPHP, string> OpAssign(
		TranslatorPHP t, OpAssign op_code, bool flag_indent = true
	)
	{
		string content = "";
		
		if (op_code.kind == OpAssign::KIND_ASSIGN or op_code.kind == OpAssign::KIND_DECLARE)
		{
			for (int i=0; i<op_code.values.count(); i++)
			{
				OpAssignValue item = op_code.values.item(i);
				
				string index_s = "";
				string s = "";
				string op = item.op; if (op == "") op = "=";
				if (item.expression == null) continue;
				
				/* Expression */
				string item_expression = "";
				list res = t.expression::Expression(t, item.expression); t = res[0];
				if (op == "~=") item_expression = t.expression::rtlToStr(t, res[1]);
				else item_expression = res[1];
				
				if (item.op_code instanceof OpAttr)
				{
					Vector<string> items = new Vector();
					Vector<string> items2 = new Vector();
					
					BaseOpCode op_code_next = item.op_code;
					while (op_code_next instanceof OpAttr)
					{
						items.pushValue(op_code_next);
						op_code_next = op_code_next.obj;
					}
					
					items = items.reverseIm();
					list res = t.expression::OpIdentifier(t, op_code_next);
					t = res[0]; string obj_s = res[1];
					
					for (int j=0; j<items.count(); j++)
					{
						OpAttr item = items[j];
						
						if (item.kind == OpAttr::KIND_ATTR)
						{
							obj_s ~= "->" ~ item.value.value;
							items2.pushValue( t.expression::toString(item.value.value) );
						}
						else if (item.kind == OpAttr::KIND_DYNAMIC)
						{
							list res = t.expression::Expression(t, item.value); t = res[0];
							obj_s ~= "[" ~ res[1] ~ "]";
							items2.pushValue(res[1]);
						}
						else if (item.kind == OpAttr::KIND_DYNAMIC_ATTRS)
						{
							if (item.attrs != null)
							{
								for (int j=item.attrs.count() - 1; j>=0; j--)
								{
									list res = t.expression::Expression(t, item.attrs[j]);
									t = res[0]; obj_s ~= "[" ~ res[1] ~ "]";
									items2.pushValue(res[1]);
								}
							}
						}
					}
					
					if (op == "~=" or op == "+=" or op == "-=")
					{
						
						string op2 = "+";
						if (op == "~=" or op == "+=") op2 = "+";
						else if (op == "-=") op2 = "-";
						
						item_expression =
							"\\Runtime\\rtl\\attr($ctx, " ~ obj_s ~ ", [" ~
							rs::join(", ", items2) ~ "]) " ~ op2 ~ " " ~
							item_expression
						;
					}
					
					index_s = obj_s ~ " = " ~ item_expression ~ ";";
				}
				else
				{
					if (item.op_code != null and
						item.op_code.value == "@" and
						t.enable_context == false
					)
					{
						index_s = "\\Runtime\\rtl::setContext(" ~ item_expression ~ ");";
					}
					else
					{
						if (op_code.kind == OpAssign::KIND_DECLARE) s = "$" ~ item.var_name;
						else
						{
							list res = t.expression::OpIdentifier(t, item.op_code);
							t = res[0]; s = res[1];
						}
						if (item_expression != "")
						{
							if (op == "~=") s ~= " .= " ~ item_expression;
							else s ~= " " ~ op ~ " " ~ item_expression;
							index_s = s ~ ";";
						}
					}
				}
				
				if (item.var_name != "" and t.save_vars.indexOf(item.var_name) == -1)
				{
					t <= save_vars <= t.save_vars.pushIm(item.var_name);
				}
				
				if (index_s != "") content ~= flag_indent ? t.s(index_s) : index_s;
			}
		}
		else if (op_code.kind == OpAssign::KIND_STRUCT)
		{
			string s = "$" ~ op_code.var_name ~ " = ";
			list res = static::OpAssignStruct(t, op_code, 0); t = res[0];
			content = t.s(s ~ res[1] ~ ";");
		}
		
		return [t, content];
	}
	
	
	
	/**
	 * OpDelete
	 */
	pure list<TranslatorPHP, string> OpDelete(TranslatorPHP t, OpDelete op_code)
	{
		string content = "";
		return [t, content];
	}
	
	
	
	/**
	 * OpFor
	 */
	pure list<TranslatorPHP, string> OpFor(TranslatorPHP t, OpFor op_code)
	{
		string content = "";
		string s1 = "";
		string s2 = "";
		string s3 = "";
		
		if (op_code.expr1 instanceof OpAssign)
		{
			list res = static::OpAssign(t, op_code.expr1, false);
			t = res[0]; s1 = res[1];
		}
		else
		{
			list res = t.expression::Expression(t, op_code.expr1); 
			t = res[0]; s1 = res[1];
		}
		
		list res = t.expression::Expression(t, op_code.expr2); t = res[0]; s2 = res[1];
		list res = t.expression::Expression(t, op_code.expr3); t = res[0]; s3 = res[1];
		
		content = t.s("for ("~s1~s2~";"~s3~")");
		content ~= t.s("{");
		t = t.levelInc();
		list res = static::Operators(t, op_code.value); t = res[0]; content ~= res[1];
		t = t.levelDec();
		content ~= t.s("}");
		
		return [t, content];
	}
	
	
	
	/**
	 * OpIf
	 */
	pure list<TranslatorPHP, string> OpIf(TranslatorPHP t, OpIf op_code)
	{
		string content = "";
		list res = t.expression::Expression(t, op_code.condition);
		t = res[0]; string s1 = res[1];
		
		content = t.s("if ("~s1~")");
		content ~= t.s("{");
		t = t.levelInc();
		list res = static::Operators(t, op_code.if_true); t = res[0]; content ~= res[1];
		t = t.levelDec();
		content ~= t.s("}");
		
		for (int i=0; i<op_code.if_else.count(); i++)
		{
			OpIfElse if_else = op_code.if_else.item(i);
			list res = t.expression::Expression(t, if_else.condition); 
			t = res[0]; string s2 = res[1];
			content ~= t.s("else if ("~s2~")");
			content ~= t.s("{");
			t = t.levelInc();
			list res = static::Operators(t, if_else.if_true); t = res[0]; content ~= res[1];
			t = t.levelDec();
			content ~= t.s("}");
		}
		
		if (op_code.if_false != null)
		{
			content ~= t.s("else");
			content ~= t.s("{");
			t = t.levelInc();
			list res = static::Operators(t, op_code.if_false); 
			t = res[0]; content ~= res[1];
			t = t.levelDec();
			content ~= t.s("}");
		}
		
		return [t, content];
	}
	
	
	
	/**
	 * OpReturn
	 */
	pure list<TranslatorPHP, string> OpReturn(TranslatorPHP t, OpReturn op_code)
	{
		string content = "";
		string s1 = "";
		if (op_code.expression)
		{
			list res = t.expression::Expression(t, op_code.expression); t = res[0]; s1 = res[1];
		}
		
		if (t.current_function.flags != null and t.current_function.flags.isFlag("memorize"))
		{
			string content = t.s("$__memorize_value = " ~ s1 ~ ";");
			content ~= t.s(t.expression::getModuleName(t, "Runtime.rtl") ~ "::_memorizeSave(\"" ~ 
					t.current_class_full_name ~ "." ~ t.current_function.name ~
					"\", func_get_args(), $__memorize_value);");
			content ~= t.s("return $__memorize_value;");
			return [t, content];
		}
		
		content ~= t.s("return " ~ s1 ~ ";");
		return [t, content];
	}
	
	
	
	/**
	 * OpThrow
	 */
	pure list<TranslatorPHP, string> OpThrow(TranslatorPHP t, OpThrow op_code)
	{
		list res = t.expression::Expression(t, op_code.expression); t = res[0];
		string content = t.s("throw " ~ res[1] ~ ";");
		return [t, content];
	}
	
	
	
	/**
	 * OpTryCatch
	 */
	pure list<TranslatorPHP, string> OpTryCatch(TranslatorPHP t, OpTryCatch op_code)
	{
		string content = "";
		
		content ~= t.s("try");
		content ~= t.s("{");
		t = t.levelInc();
		list res = static::Operators(t, op_code.op_try); t = res[0]; content ~= t.s(res[1]);
		t = t.levelDec();
		content ~= t.s("}");
		content ~= t.s("catch (\\Exception $_ex)");
		content ~= t.s("{");
		t = t.levelInc();
		
		for (int i=0; i<op_code.items.count(); i++)
		{
			string s = ""; string pattern = "";
			OpTryCatchItem item = op_code.items.item(i);
			list res = t.expression::OpTypeIdentifier(t, item.pattern); t = res[0]; pattern ~= res[1];
			
			if (pattern != "\\var") s = "if ($_ex instanceof " ~ pattern ~ ")";
			else s = "if (true)";
			
			s ~= t.s("{");
			t = t.levelInc();
			
			s ~= (s != "") ? t.s("$" ~ item.name ~ " = $_ex;") : "$" ~ item.name ~ " = $_ex;";
			list res = static::Operators(t, item.value); t = res[0]; s ~= res[1];
			
			t = t.levelDec();
			s ~= t.s("}");
			
			if (i != 0) s = "else " ~ s;
			content ~= t.s(s);
		}
		
		content ~= t.s("else");
		content ~= t.s("{");
		t = t.levelInc();
		content ~= t.s("throw $_ex;");
		t = t.levelDec();
		content ~= t.s("}");
		t = t.levelDec();
		content ~= t.s("}");
		
		return [t, content];
	}
	
	
	
	/**
	 * OpWhile
	 */
	pure list<TranslatorPHP, string> OpWhile(TranslatorPHP t, OpWhile op_code)
	{
		string content = "";
		list res = t.expression::Expression(t, op_code.condition); t = res[0]; string s1 = res[1];
		
		content ~= t.s("while ("~s1~")");
		content ~= t.s("{");
		t = t.levelInc();
		list res = static::Operators(t, op_code.value); t = res[0]; content ~= res[1];
		t = t.levelDec();
		content ~= t.s("}");
		
		return [t, content];
	}
	
	
	
	/**
	 * OpPreprocessorIfCode
	 */
	pure list<TranslatorPHP, string> OpPreprocessorIfCode(TranslatorPHP t, OpPreprocessorIfCode op_code)
	{
		string content = "";
		if (t.preprocessor_flags[op_code.condition.value] == true)
		{
			content = rs::trim(op_code.content);
		}
		return [t, t.s(content)];
	}
	
	
	
	/**
	 * OpPreprocessorIfDef
	 */
	pure list<TranslatorPHP, string> OpPreprocessorIfDef
	(
		TranslatorPHP t, OpPreprocessorIfDef op_code, string kind
	)
	{
		if (not t.preprocessor_flags[op_code.condition.value] == true) return [t, ""];
		
		if (kind == OpPreprocessorIfDef::KIND_OPERATOR)
		{
			return static::Operators(t, op_code.items);
		}
		else if (kind == OpPreprocessorIfDef::KIND_EXPRESSION)
		{
			return t.expression::Expression(t, op_code.items);
		}
		
		string content = "";
		for (int i=0; i<op_code.items.count(); i++)
		{
			BaseOpCode item = op_code.items.item(i);
			if (item instanceof OpComment)
			{
				list res = t.operator::OpComment(t, item);
				t = res[0]; content ~= res[1];
			}
			else if (item instanceof OpDeclareFunction)
			{
				list res = t.program::OpDeclareFunction(t, item);
				t = res[0]; content ~= res[1];
			}
		}
		return [t, content];
	}
	
	
	
	/**
	 * OpComment
	 */
	pure list<TranslatorPHP, string> OpComment(TranslatorPHP t, OpComment op_code)
	{
		string content = t.s("/*" ~ op_code.value ~ "*/");
		return [t, content];
	}
	
	
	
	/**
	 * OpComments
	 */
	pure list<TranslatorPHP, string> OpComments(TranslatorPHP t, Collection<OpComment> comments)
	{
		string content = "";
		for (int i=0; i<comments.count(); i++)
		{
			list res = static::OpComment(t, comments.item(i));
			content ~= res[1];
		}
		return [t, content];
	}
	
	
	
	/**
	 * OpComments
	 */
	pure list<TranslatorPHP, string> AddComments
	(
		TranslatorPHP t, Collection<OpComment> comments, string content
	)
	{
		if (comments and comments.count() > 0)
		{
			list res = static::OpComments(t, comments);
			string s = res[1];
			if (s != "")
			{
				content = s ~ content;
			}
		}
		return [t, content];
	}
	
	
	
	/**
	 * Operator
	 */
	pure list<TranslatorPHP, string> Operator(TranslatorPHP t, BaseOpCode op_code)
	{
		string content = "";
		
		/* Save op codes */
		Collection<SaveOpCode> save_op_codes = t.save_op_codes;
		int save_op_code_inc = t.save_op_code_inc;
		
		if (op_code instanceof OpAssign)
		{
			list res = static::OpAssign(t, op_code); t = res[0]; string content = res[1];
			
			/* Output save op code */
			string save = t::outputSaveOpCode(t, save_op_codes.count());
			if (save != "") content = save ~ content;
			
			t <= save_op_codes <= save_op_codes;
			t <= save_op_code_inc <= save_op_code_inc;
			
			return [t, content];
		}
		else if (op_code instanceof OpAssignStruct)
		{
			list res = static::OpAssignStruct(t, op_code); t = res[0]; string s1 = res[1];
			
			/* Output save op code */
			string save = t::outputSaveOpCode(t, save_op_codes.count());
			if (save != "") content = save;
			content ~= t.s("$" ~ op_code.var_name ~ " = " ~ s1 ~ ";");
			
			t <= save_op_codes <= save_op_codes;
			t <= save_op_code_inc <= save_op_code_inc;
			
			return [t, content]
		}
		else if (op_code instanceof OpBreak)
		{
			content = t.s("break;");
		}
		else if (op_code instanceof OpCall)
		{
			list res = t.expression::OpCall(t, op_code); t = res[0]; content = t.s(res[1] ~ ";");
		}
		else if (op_code instanceof OpContinue)
		{
			content = t.s("continue;");
		}
		else if (op_code instanceof OpDelete)
		{
			list res = static::OpDelete(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpFor)
		{
			list res = static::OpFor(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpIf)
		{
			list res = static::OpIf(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpPipe)
		{
			list res = t.expression::OpPipe(t, op_code, false); t = res[0]; content = t.s(res[1] ~ ";");
		}
		else if (op_code instanceof OpReturn)
		{
			list res = static::OpReturn(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpThrow)
		{
			list res = static::OpThrow(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpTryCatch)
		{
			list res = static::OpTryCatch(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpWhile)
		{
			list res = static::OpWhile(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpInc)
		{
			list res = t.expression::OpInc(t, op_code); t = res[0]; content = t.s(res[1] ~ ";");
		}
		else if (op_code instanceof OpPreprocessorIfCode)
		{
			list res = static::OpPreprocessorIfCode(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpPreprocessorIfDef)
		{
			list res = static::OpPreprocessorIfDef(t, op_code, OpPreprocessorIfDef::KIND_OPERATOR);
			t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpPreprocessorSwitch)
		{
			for (int i=0; i<op_code.items.count(); i++)
			{
				list res = static::OpPreprocessorIfCode(t, op_code.items.item(i)); string s = res[1];
				if (s == "") continue;
				content ~= s;
			}
		}
		else if (op_code instanceof OpComment)
		{
			list res = static::OpComment(t, op_code); t = res[0]; content = res[1];
		}
		else if (op_code instanceof OpSafe)
		{
			list res = static::Operators(t, op_code.items); t = res[0]; content = res[1];
		}
		
		/* Output save op code */
		string save = t::outputSaveOpCode(t, save_op_codes.count());
		if (save != "") content = save ~ content;
		
		/* Restore save op codes */
		t <= save_op_codes <= save_op_codes;
		t <= save_op_code_inc <= save_op_code_inc;
		
		return [t, content];
	}
	
	
	
	/**
	 * Operators
	 */
	pure list<TranslatorPHP, string> Operators(TranslatorPHP t, BaseOpCode op_code)
	{
		string content = "";
		
		fn f1 = bool (BaseOpCode op_code) =>
			op_code instanceof OpBreak or
			op_code instanceof OpCall or
			op_code instanceof OpContinue or
			op_code instanceof OpReturn or
			op_code instanceof OpThrow
		;
		
		if (op_code instanceof OpItems)
		{
			for (int i=0; i<op_code.items.count(); i++)
			{
				BaseOpCode item = op_code.items.item(i);
				list res = static::Operator(t, item); t = res[0];
				content ~= res[1];
			}
		}
		else if (op_code instanceof OpHtmlItems)
		{
			string save_html_var_name = t.html_var_name;
			bool save_is_html = t.is_html;
			
			t <= is_html <= true;
			list res = t.html::OpHtmlItems(t, op_code, save_html_var_name, false);
			t = res[0]; content = res[1];
			t <= is_html <= save_is_html;
		}
		else
		{
			list res = static::Operator(t, op_code); t = res[0];
			content ~= res[1];
		}
		
		return [t, content];
	}
	
	
	
	/**
	 * OpDeclareFunction Arguments
	 */
	pure list<TranslatorPHP, string> OpDeclareFunctionArgs(TranslatorPHP t, OpDeclareFunction f)
	{
		string content = "";
		if (f.args != null)
		{
			bool flag = false;
			if (f.is_context) { content ~= "$ctx"; flag = true; }
			/*
			if (f.is_html)
			{
				flag = true;
				content ~= (flag ? ", " : "") ~
					"$layout, $model_path, $render_params, $render_content";
			}
			*/
			for (int i=0; i<f.args.count(i); i++)
			{
				OpDeclareFunctionArg arg = f.args.item(i);
				string name = arg.name;
				string expr = "";
				if (arg.expression != null)
				{
					list res = t.expression::Expression(t, arg.expression); t = res[0]; expr = res[1];
				}
				content ~= (flag ? ", " : "") ~ "$" ~ name ~ ((expr != "") ? "=" ~ expr : "");
				flag = true;
			}
		}
		return [t, content];
	}
	
	
	
	/**
	 * OpDeclareFunction Body
	 */
	pure list<TranslatorPHP, string> OpDeclareFunctionBody(TranslatorPHP t, OpDeclareFunction f)
	{
		TranslatorPHP save_t = t;
		t <= is_pipe <= false;
		t <= is_html <= false;
		
		string content = "";
		t = t.levelInc();
		/*
		if (f.is_html)
		{
			content ~= t.s("$model = \\Runtime\\rtl::attr($layout, $model_path);");
		}
		*/
		if (f.items)
		{
			list res = t.operator::Operators(t, f.items); t = res[0];
			content ~= res[1];
		}
		else if (f.expression)
		{
			/* Clear save op codes */
			t = t::clearSaveOpCode(t);
		
			list res = t.expression::Expression(t, f.expression); t = res[0];
			string expr = res[1];
			string s = "";
			if (f.flags != null and f.flags.isFlag("memorize"))
			{
				s = "$__memorize_value = " ~ expr ~ ";";
				s ~= t.s(t.expression::getModuleName(t, "Runtime.rtl") ~ "::_memorizeSave(\"" ~ 
						t.current_class_full_name ~ "." ~ f.name ~
						"\", func_get_args(), $__memorize_value);");
				s ~= t.s("return $__memorize_value;");
			}
			else
			{
				s = t.s("return " ~ expr ~ ";");
			}
			
			/* Output save op code */
			string save = t::outputSaveOpCode(t);
			if (save != "") content ~= save;
			content ~= s;
		}
		
		if (f.flags != null and f.flags.isFlag("memorize"))
		{
			string s = "";
			s ~= t.s("$__memorize_value = " ~ t.expression::getModuleName(t, "Runtime.rtl") ~
				"::_memorizeValue(\"" ~
				t.current_class_full_name ~ "." ~ f.name ~
				"\", func_get_args());");
			s ~= t.s("if ($__memorize_value != " ~ t.expression::getModuleName(t, "Runtime.rtl") ~
				"::$_memorize_not_found) return $__memorize_value;");
			content = s ~ content;
		}
		t = t.levelDec();
		content = t.s("{") ~ content;
		content ~= t.s("}");
		
		return [save_t, content];		
	}
	
}