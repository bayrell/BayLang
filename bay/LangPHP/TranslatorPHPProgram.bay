/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace BayLang.LangPHP;

use Runtime.re;
use Runtime.lib;
use Runtime.BaseObject;
use BayLang.SaveOpCode;
use BayLang.Exceptions.DeclaredClass;
use BayLang.LangPHP.TranslatorPHP;
use BayLang.OpCodes.BaseOpCode;
use BayLang.OpCodes.OpAnnotation;
use BayLang.OpCodes.OpAssign;
use BayLang.OpCodes.OpAssignValue;
use BayLang.OpCodes.OpComment;
use BayLang.OpCodes.OpDeclareClass;
use BayLang.OpCodes.OpDeclareFunction;
use BayLang.OpCodes.OpDeclareFunctionArg;
use BayLang.OpCodes.OpIdentifier;
use BayLang.OpCodes.OpItems;
use BayLang.OpCodes.OpModule;
use BayLang.OpCodes.OpNamespace;
use BayLang.OpCodes.OpPreprocessorIfCode;
use BayLang.OpCodes.OpPreprocessorIfDef;
use BayLang.OpCodes.OpPreprocessorSwitch;
use BayLang.OpCodes.OpTypeIdentifier;
use BayLang.OpCodes.OpUse;


class TranslatorPHPProgram extends BaseObject
{
	TranslatorPHP translator = null;
	
	
	/**
	 * Constructor
	 */
	void constructor(TranslatorPHP translator)
	{
		parent();
		this.translator = translator;
	}
	
	
	/**
	 * OpNamespace
	 */
	void OpNamespace(OpNamespace op_code, Collection<string> result)
	{
		this.translator.current_namespace_name = op_code.name;
		result.push("namespace ");
		result.push(this.translator.getModuleName(op_code.name));
		result.push(";");
	}
	
	
	/**
	 * OpUse
	 */
	void OpUse(OpUse op_code, Collection<string> result)
	{
		this.translator.uses.set(op_code.alias, op_code.name);
		Collection items = rs::split(".", op_code.name);
		string last_name = items.last();
		result.push("use ");
		result.push(this.translator.getModuleName(op_code.name));
		if (op_code.alias != "" and op_code.alias != last_name)
		{
			result.push(" as ");
			result.push(op_code.alias);
		}
		result.push(";");
	}
	
	
	/**
	 * OpAnnotation
	 */
	void OpAnnotation(OpAnnotation op_code, Collection<string> result)
	{
		result.push("@");
		this.translator.expression.OpTypeIdentifier(op_code.name, result);
		this.translator.expression.OpDict(op_code.params, result);
	}
	
	
	/**
	 * OpAssign
	 */
	void OpAssign(OpAssign op_code, Collection<string> result, bool is_expression = true)
	{
		bool last_result = false;
		for (int i=0; i<op_code.items.count(); i++)
		{
			OpAssignValue op_code_item = op_code.items.get(i);
			if (last_result)
			{
				result.push(this.translator.newLine());
			}
			if (op_code_item.value instanceof OpIdentifier)
			{
				if (op_code.flags.isFlag("const"))
				{
					result.push("const ");
				}
				else if (op_code.flags.isFlag("static"))
				{
					result.push("static ");
				}
				else
				{
					result.push("var ");
				}
				this.translator.expression.OpIdentifier(
					op_code_item.value, result, op_code.flags.isFlag("const")
				);
			}
			else
			{
				last_result = false;
				continue;
			}
			if (op_code_item.expression and (is_expression or op_code.isStatic()))
			{
				result.push(" = ");
				this.translator.expression.translate(op_code_item.expression, result);
			}
			result.push(";");
			last_result = true;
		}
	}
	
	
	/**
	 * OpComment
	 */
	void OpComment(OpComment op_code, Collection<string> result)
	{
		result.push("/*");
		result.push(op_code.value);
		result.push("*/");
	}
	
	
	/**
	 * OpPreprocessorIfCode
	 */
	bool OpPreprocessorIfCode(OpPreprocessorIfCode op_code, Collection<string> result)
	{
		if (op_code.condition instanceof OpIdentifier)
		{
			string name = op_code.condition.value;
			if (this.translator.preprocessor_flags.get(name))
			{
				result.push(op_code.content);
				return true;
			}
		}
		return false;
	}
	
	
	/**
	 * OpPreprocessorIfDef
	 */
	bool OpPreprocessorIfDef(
		OpPreprocessorIfDef op_code, Collection<string> result, string current_block = ""
	)
	{
		if (op_code.condition instanceof OpIdentifier)
		{
			string name = op_code.condition.value;
			if (this.translator.preprocessor_flags.get(name))
			{
				if (current_block == OpPreprocessorIfDef::KIND_PROGRAM)
				{
					this.translate(op_code.content, result);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_CLASS_BODY)
				{
					this.translateClassBody(op_code, result, false);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_CLASS_INIT)
				{
					this.translateClassInitItems(op_code.content, result);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_OPERATOR)
				{
					this.translator.operator.translateItems(op_code.content, result, false);
				}
				else if (current_block == OpPreprocessorIfDef::KIND_COLLECTION)
				{
					for (int i=0; i<op_code.content.items.count(); i++)
					{
						BaseOpCode op_code_item = op_code.content.items.get(i);
						if (i > 0) result.push(this.translator.newLine());
						this.translator.expression.translate(op_code_item, result);
						if (i < op_code.content.items.count() - 1) result.push(",");
					}
				}
				else if (current_block == OpPreprocessorIfDef::KIND_EXPRESSION)
				{
					this.translator.expression.translate(op_code.content, result);
				}
				return true;
			}
		}
		return false;
	}
	
	
	/**
	 * OpPreprocessorSwitch
	 */
	void OpPreprocessorSwitch(
		OpPreprocessorSwitch op_code, Collection<string> result, string current_block = ""
	)
	{
		bool last_result = false;
		for (int i=0; i<op_code.items.count(); i++)
		{
			BaseOpCode item = op_code.items.get(i);
			if (item instanceof OpPreprocessorIfCode)
			{
				Collection result1 = [];
				if (this.OpPreprocessorIfCode(item, result1))
				{
					result.push(this.translator.newLine());
					result.appendItems(result1);
					last_result = true;
				}
			}
			else if (item instanceof OpPreprocessorIfDef)
			{
				if (this.OpPreprocessorIfDef(item, result, current_block))
				{
					last_result = true;
				}
			}
		}
		return last_result;
	}
	
	
	/**
	 * OpDeclareFunctionArg
	 */
	void OpDeclareFunctionArg(OpDeclareFunctionArg op_code, Collection<string> result)
	{
		result.push("$" ~ op_code.name);
		if (op_code.expression)
		{
			result.push(" = ");
			this.translator.expression.translate(op_code.expression, result);
		}
	}
	
	
	/**
	 * OpDeclareFunctionArgs
	 */
	void OpDeclareFunctionArgs(OpDeclareFunction op_code, Collection<string> result)
	{
		if (op_code.args and op_code.args.count() > 0)
		{
			int args_count = op_code.args.count();
			for (int i=0; i<args_count; i++)
			{
				OpDeclareFunctionArg op_code_item = op_code.args.get(i);
				this.OpDeclareFunctionArg(op_code_item, result);
				if (i < args_count - 1) result.push(", ");
			}
		}
	}
	
	
	/**
	 * OpDeclareFunction
	 */
	void OpDeclareFunction(OpDeclareFunction op_code, Collection<string> result)
	{
		/*if (not (op_code.pattern instanceof OpTypeIdentifier)) return;*/
		
		/* Setup current function */
		OpDeclareFunction old_function = this.translator.current_function;
		this.translator.current_function = op_code;
		
		/* Comments */
		if (op_code.comments)
		{
			for (int i=0; i<op_code.comments.count(); i++)
			{
				OpComment op_code_item = op_code.comments.get(i);
				this.OpComment(op_code_item, result);
				result.push(this.translator.newLine());
			}
		}
		
		/* Function flags */
		if (op_code.flags and (op_code.flags.isFlag("static") or op_code.flags.isFlag("pure")))
		{
			result.push("static ");
		}
		
		/* Function name */
		result.push("function ");
		if (op_code.name == "constructor") result.push("__construct");
		else result.push(op_code.name);
		
		/* Arguments */
		result.push("(");
		this.OpDeclareFunctionArgs(op_code, result);
		result.push(")");
		
		/* If interface */
		if (this.translator.current_class and
			this.translator.current_class.kind == OpDeclareClass::KIND_INTERFACE
		)
		{
			result.push(";");
			return;
		}
		
		/* Variables */
		if (op_code.vars and op_code.vars.count() > 0)
		{
			result.push(" use (");
			Collection vars = op_code.vars.map(string (OpIdentifier item) => "&$" ~ item.value);
			result.push(rs::join(", ", vars));
			result.push(")");
		}
		
		/* If content is empty */
		if (op_code.content == null)
		{
			result.push("{");
			result.push("}");
			return;
		}
		
		/* Expression */
		bool is_expression = not (op_code.content instanceof OpItems);
		if (is_expression)
		{
			bool is_multiline = op_code.content.isMultiLine();
			if (is_multiline)
			{
				result.push(this.translator.newLine());
			}
			result.push("{");
			if (is_multiline)
			{
				this.translator.levelInc();
				result.push(this.translator.newLine());
			}
			else
			{
				result.push(" ");
			}
			result.push("return ");
			this.translator.expression.translate(op_code.content, result);
			result.push(";");
			if (is_multiline)
			{
				this.translator.levelDec();
				result.push(this.translator.newLine());
			}
			else
			{
				result.push(" ");
			}
			result.push("}");
		}
		
		/* Items */
		else
		{
			if (op_code.is_html)
			{
				this.translator.html.translateItems(op_code.content, result);
			}
			else
			{
				this.translator.operator.translateItems(op_code.content, result);
			}
		}
	}
	
	
	/**
	 * Translate class item
	 */
	bool translateClassItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpAnnotation)
		{
			this.OpAnnotation(op_code, result);
		}
		else if (op_code instanceof OpAssign)
		{
			this.OpAssign(op_code, result, false);
		}
		else if (op_code instanceof OpComment)
		{
			this.OpComment(op_code, result);
		}
		else if (op_code instanceof OpDeclareFunction)
		{
			this.translator.class_function = op_code;
			this.OpDeclareFunction(op_code, result);
			this.translator.class_function = null;
		}
		else if (op_code instanceof OpPreprocessorIfCode)
		{
			return this.OpPreprocessorIfCode(op_code, result);
		}
		else if (op_code instanceof OpPreprocessorIfDef)
		{
			return this.OpPreprocessorIfDef(
				op_code, result, OpPreprocessorIfDef::KIND_CLASS_BODY
			);
		}
		else if (op_code instanceof OpPreprocessorSwitch)
		{
			return this.OpPreprocessorSwitch(
				op_code, result, OpPreprocessorIfDef::KIND_CLASS_BODY
			);
		}
		else
		{
			return false;
		}
		
		return true;
	}
	
	
	/**
	 * Translate class body
	 */
	void translateClassBody(
		OpDeclareClass op_code,
		Collection<string> result, bool match_brackets = true
		)
	{
		/* Begin bracket */
		if (match_brackets)
		{
			result.push("{");
			this.translator.levelInc();
		}
		
		/* Class body items */
		BaseOpCode prev_op_code = null;
		int result_count = result.count();
		bool next_new_line = true, prev_next_new_line = true;
		for (int i=0; i<op_code.content.count(); i++)
		{
			Collection item_result = [];
			BaseOpCode op_code_item = op_code.content.get(i);
			if (next_new_line and not(op_code_item instanceof OpPreprocessorIfDef or
				op_code_item instanceof OpPreprocessorSwitch))
			{
				int lines = 1;
				if (prev_op_code and not this.translator.current_class.is_component)
				{
					lines = op_code_item.getOffset().get("start") -
						prev_op_code.getOffset().get("end");
				}
				for (int j=0; j<lines; j++) item_result.push(this.translator.newLine());
			}
			prev_next_new_line = next_new_line;
			next_new_line = this.translateClassItem(op_code_item, item_result);
			if (rs::trim(rs::join("", item_result)) != "")
			{
				prev_op_code = op_code_item;
				result.appendItems(item_result);
			}
			else next_new_line = prev_next_new_line;
		}
		
		/* Class init */
		if (op_code instanceof OpDeclareClass)
		{
			this.translateClassInit(op_code, result, result_count != result.count());
		}
		
		/* End bracket */
		if (match_brackets)
		{
			this.translator.levelDec();
			result.push(this.translator.newLine());
			result.push("}");
		}
	}
	
	
	/**
	 * Returns class count items
	 */
	static int getClassCountItems(OpDeclareClass op_code)
	{
		int count = 0;
		for (int i=0; i<op_code.content.count(); i++)
		{
			BaseOpCode op_code_item = op_code.content.get(i);
			if (op_code_item instanceof OpDeclareFunction) count++;
		}
		return count;
	}
	
	
	/**
	 * Translate class init items
	 */
	void translateClassInitItems(OpItems op_code, Vector result)
	{
		for (int i=0; i<op_code.count(); i++)
		{
			BaseOpCode op_code_item = op_code.get(i);
			if (op_code_item instanceof OpAssign and not op_code_item.isStatic())
			{
				for (int j=0; j<op_code_item.items.count(); j++)
				{
					OpAssignValue assign_item = op_code_item.items.get(j);
					if (assign_item.expression)
					{
						result.push(this.translator.newLine());
						result.push("$this->" ~ assign_item.value.value ~ " = ");
						this.translator.expression.translate(assign_item.expression, result);
						result.push(";");
					}
				}
			}
			else if (op_code_item instanceof OpPreprocessorIfCode)
			{
				return this.OpPreprocessorIfCode(op_code_item, result);
			}
			else if (op_code_item instanceof OpPreprocessorIfDef)
			{
				return this.OpPreprocessorIfDef(
					op_code_item, result, OpPreprocessorIfDef::KIND_CLASS_INIT
				);
			}
			else if (op_code_item instanceof OpPreprocessorSwitch)
			{
				return this.OpPreprocessorSwitch(
					op_code_item, result, OpPreprocessorIfDef::KIND_CLASS_INIT
				);
			}
		}
	}
	
	
	/**
	 * Translate class init
	 */
	void translateClassInit(OpDeclareClass op_code, Collection<string> result, bool newline = true)
	{
		if (op_code.kind == OpDeclareClass::KIND_INTERFACE) return;
		
		int lines = newline and op_code.content.count() > 0 ? 3 : 1;
		if (this.translator.current_class.is_component and lines == 3) lines = 2;
		result.push(this.translator.newLine(lines));
		result.push("/* ========= Class init functions ========= */");
		if (this.translator.current_class_name != "Runtime.BaseObject")
		{
			result.push(this.translator.newLine());
			result.push("function _init()");
			result.push(this.translator.newLine());
			result.push("{");
			this.translator.levelInc();
			if (this.translator.parent_class_name != "")
			{
				result.push(this.translator.newLine());
				result.push("parent::_init();");
			}
			this.translateClassInitItems(op_code.content, result);
			this.translator.levelDec();
			result.push(this.translator.newLine());
			result.push("}");
		}
		if (op_code.is_component)
		{
			this.translator.html.translateComponentStyle(op_code, result);
			this.translator.html.translateModuleComponents(op_code, result);
		}
		result.push(this.translator.newLine());
		result.push("static function getClassName(){ " ~
			"return \"" ~ this.translator.current_class_name ~ "\"; }");
		result.push(this.translator.newLine());
		result.push("static function getMethodsList(){ return []; }");
		result.push(this.translator.newLine());
		result.push("static function getMethodInfoByName($field_name){ return null; }");
	}
	
	
	/**
	 * Translate class
	 */
	void translateClass(OpDeclareClass op_code, Collection<string> result)
	{
		/* Current class */
		this.translator.current_class = op_code;
		
		/* Class name */
		string class_name = op_code.name.entity_name.items.last().value;
		this.translator.current_class_name = this.translator.current_namespace_name ~ "." ~
			op_code.name.entity_name.getName();
		this.translator.uses.set(class_name, this.translator.current_class_name);
		
		/* Abstract class */
		if (op_code.is_abstract) return false;
		
		/* Class kind */
		if (op_code.kind == OpDeclareClass::KIND_CLASS)
		{
			result.push("class ");
		}
		else if (op_code.kind == OpDeclareClass::KIND_INTERFACE)
		{
			result.push("interface ");
		}
		result.push(class_name);
		
		/* Template */
		if (op_code.template)
		{
			this.translator.expression.OpTypeTemplate(op_code.template, result);
		}
		
		/* Extends */
		if (op_code.class_extends)
		{
			result.push(" extends ");
			this.translator.expression.OpTypeIdentifier(op_code.class_extends, result);
			this.translator.parent_class_name = this.translator.getFullName(
				op_code.class_extends.entity_name.getName()
			);
		}
		else if (op_code.is_component)
		{
			if (this.translator.current_class_name == "Runtime.Component")
			{
				this.translator.parent_class_name = "Runtime.BaseObject";
				result.push(" extends \\Runtime\\BaseObject");
			}
			else
			{
				this.translator.parent_class_name = "Runtime.Component";
				result.push(" extends \\Runtime\\Component");
			}
		}
		
		/* Implements */
		if (op_code.class_implements and op_code.class_implements.count() > 0)
		{
			result.push(" implements ");
			int items_count = op_code.class_implements.count();
			for (int i=0; i<items_count; i++)
			{
				OpTypeIdentifier op_code_item = op_code.class_implements.get(i);
				this.translator.expression.OpTypeIdentifier(op_code_item, result);
				if (i < items_count - 1) result.push(", ");
			}
		}
		
		result.push(this.translator.newLine());
		this.translateClassBody(op_code, result);
		this.translator.current_class = null;
	}
	
	
	/**
	 * Translate item
	 */
	void translateItem(BaseOpCode op_code, Collection<string> result)
	{
		if (op_code instanceof OpDeclareClass)
		{
			this.translateClass(op_code, result);
		}
		else if (op_code instanceof OpNamespace)
		{
			this.OpNamespace(op_code, result);
		}
		else if (op_code instanceof OpUse)
		{
			this.OpUse(op_code, result);
		}
		else if (op_code instanceof OpPreprocessorIfCode)
		{
			return this.OpPreprocessorIfCode(op_code, result);
		}
		else if (op_code instanceof OpPreprocessorIfDef)
		{
			return this.OpPreprocessorIfDef(
				op_code, result, OpPreprocessorIfDef::KIND_PROGRAM
			);
		}
		else if (op_code instanceof OpPreprocessorSwitch)
		{
			return this.OpPreprocessorSwitch(
				op_code, result, OpPreprocessorIfDef::KIND_PROGRAM
			);
		}
		else
		{
			this.translator.last_semicolon = false;
			bool res = this.translator.operator.translateItem(op_code, result);
			this.translator.operator.addSemicolon(op_code, result);
			return res;
		}
		return true;
	}
	
	
	/**
	 * Translate items
	 */
	void translateItems(Collection<BaseOpCode> items, Collection<string> result)
	{
		int op_code_use_count = 0;
		bool prev_op_code_use = false;
		bool last_result = false;
		for (int i=0; i<items.count(); i++)
		{
			BaseOpCode op_code_item = items.get(i);
			if (op_code_item instanceof OpDeclareClass)
			{
				if (not op_code_item.is_abstract)
				{
					if (op_code_use_count > 0)
					{
						result.push(this.translator.newLine(op_code_use_count > 1 ? 3 : 2));
					}
					else
					{
						result.push(this.translator.newLine(last_result ? 3 : 2));
					}
				}
			}
			else if (last_result) result.push(this.translator.newLine());
			if (op_code_item instanceof OpUse)
			{
				if (op_code_use_count == 0) result.push(this.translator.newLine());
				op_code_use_count++;
			}
			else op_code_use_count = 0;
			last_result = this.translateItem(items.get(i), result);
		}
	}
	
	
	/**
	 * Translate
	 */
	void translate(OpModule op_code, Collection<string> result)
	{
		this.translator.current_module = op_code;
		this.translateItems(op_code.items, result);
	}
}